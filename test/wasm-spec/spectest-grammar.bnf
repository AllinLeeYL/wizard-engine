File ::= Command*

Command ::= AssertInvalid
          | AssertMalformed
          | AssertExhaustion
          | AssertReturn
          | AssertReturnFunc
          | AssertTrap
          | AssertUnlinkable
          | Register
          | Module

AssertInvalid ::= ("assert_invalid" Module STRING?)

AssertMalformed ::= ("assert_malformed" QuotedModule STRING?)

AssertExhaustion ::= ("assert_exhaustion" Action STRING?)

AssertReturn ::= ("assert_return" Action Constant*)

AssertReturnFunc ::= ("assert_return_func" Action)

AssertTrap ::= ("assert_trap" Trappable STRING?)

AssertUnlinkable ::= ("assert_unlinkable" Module STRING?)

Register ::= ("register" STRING VAR?)

Trappable ::= Action
            | Module

Action ::= Invoke
         | Get

Invoke ::= ("invoke" STRING VAR? Constant*)

Get = ("get" STRING VAR?)

Constant ::= (i32.const INT)
           | (i64.const INT)
           | (f32.const NUM)
           | (f64.const NUM)

Module ::= BinaryModule
         | QuotedModule
         | InlineModule

BinaryModule ::= ("module" "binary" STRING)
QuotedModule ::= ("module" quote STRING)
InlineModule ::= ("module" ModuleBody)
