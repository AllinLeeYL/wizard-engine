// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A utility class to build an in-memory {Module} object.
class ModuleBuilder {
	var extensions: Extension.set;
	def module = Module.new(null);
	var func: FunctionDecl;
	def locals = Vector<(ValueType, u32)>.new();
	var num_locals: int;
	new() {
		var sig = SigCache.v_i;
		module.types.declared.put(TypeDecl.Function(sig));
		func = FunctionDecl.new(0, 0, sig);
		module.functions.declared.put(func);
	}
	def sig(sig: ValueType.Function) -> this {
		module.types.declared[0] = TypeDecl.Function(sig);
		func = FunctionDecl.new(0, 0, sig);
		module.functions.declared[0] = func;
	}
	def addSig(sig: ValueType.Function) -> int {
		var index = module.types.declared.length;
		module.types.declared.put(TypeDecl.Function(sig));
		return index;
	}
	def code(raw: Array<byte>) -> this {
		var body = makeBody(raw, this.locals);
		func.code = FunctionCode.new(body);
		func.code.num_locals = num_locals;
	}
	def validate() -> bool {
		var v = CodeValidator.new(extensions,
			Limits.new().set(extensions), module,
			WasmErrorGen.new(module.filename));
		return CodeValidatorResult.Ok.?(v.validateFunc(func));
	}
	private def makeBody(raw: Array<byte>, locals: Vector<(ValueType, u32)>) -> Array<byte> {
		var copy = BinBuilder.new();
		copy.put_u32leb(u32.!(locals.length));
		for (i < locals.length) {
			var e = locals[i];
			copy.put_u32leb(u32.!(e.1));
			copy.put_ValueType(e.0);
		}
		copy.puta(raw);
		copy.put(Opcode.END.code);
		return copy.extract();
	}
	def addGlobal(typ: ValueType, init: InitExpr) -> int {
		var index = module.globals.size();
		module.globals.declared.put(GlobalDecl.new(module.globals.size(), typ, true, init));
		return index;
	}
	def addLocal(typ: ValueType) -> int {
		return addLocals(1, typ);
	}
	def addLocals(count: int, typ: ValueType) -> int {
		var size = locals.length;
		if (size > 0) {
			var last = locals[size-1];
			if (last.0 == typ) {
				locals[size-1] = (last.0, last.1 + u32.!(count));
				var prev = num_locals;
				num_locals += count;
				return func.sig.params.length + prev;
			}
		}
		locals.put((typ, u32.!(count)));
		var prev = num_locals;
		num_locals += count;
		return func.sig.params.length + prev;
	}
	def addFunction(sig: ValueType.Function, raw: Array<byte>) -> (int, FunctionDecl) {
		var sig_index = module.types.size();
		module.types.declared.put(TypeDecl.Function(sig));
		var func = FunctionDecl.new(module.types.size(), sig_index, sig);
		module.functions.declared.put(func);
		func.code = FunctionCode.new(makeBody(raw, Vector.new()));
		return (module.functions.size() - 1, func);
	}
	def addTable(length: int, offset: int, indices: Array<int>) -> this {
		var table = TableDecl.new(module.tables.size(), ValueType.FUNCREF, length, length);
		var index = module.tables.size();
		module.tables.declared.put(table);
		if (indices != null) {
			var mode = SegmentMode.Active(index, InitExpr.I32(offset));
			var details = ElemDetails.FuncRefs(indices);
			var elem = ElemDecl.new(mode, details);
			module.elems.put(elem);
		}
	}
	def addTableWithMax(initial: int, length: int) -> this {
		var table = TableDecl.new(module.tables.size(), ValueType.FUNCREF, initial, length);
		module.tables.declared.put(table);
	}
	def addMemory(initial: int, maximum: int) -> this {
		var memory = MemoryDecl.new(module.memories.size(), initial, maximum, false);
		module.memories.declared.put(memory);
	}
	def addData(offset: int, raw: Array<byte>) -> this {
		var mode = SegmentMode.Active(0, InitExpr.I32(offset));
		var data = DataDecl.new(mode, raw);
		module.data.put(data);
	}
	def addPassiveData(raw: Array<byte>) -> this {
		var data = DataDecl.new(SegmentMode.Passive, raw);
		module.data.put(data);
	}
	def addPassiveElems(indices: Array<int>) -> this {
		var details = ElemDetails.FuncRefs(indices);
		var elem = ElemDecl.new(SegmentMode.Passive, details);
		module.elems.put(elem);
	}
}