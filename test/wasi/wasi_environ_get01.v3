def numbuf = Array<int>.new(1);
def sizebuf = Array<int>.new(1);

// allocate the buffer to store the environment variables
def argv = Array<Pointer>.new(2); // two argument is given in .flags file
def argvbuf = Array<byte>.new(16); // total size of arguments is 16byte

def main() -> int {
    def numbuf_p = Pointer.atContents(numbuf);
    def sizebuf_p = Pointer.atContents(sizebuf);

    // call environ_sizes_get
    wasi_snapshot_preview1.environ_sizes_get(numbuf_p, sizebuf_p);
    if (numbuf[0] != 2 || sizebuf[0] != 16) {
        return 1;
    }

    // get the pointer to environment variable buffers
    def argv_p = Pointer.atContents(argv);
    def argvbuf_p = Pointer.atContents(argvbuf);

    // call environ_get
    wasi_snapshot_preview1.environ_get(argv_p, argvbuf_p);
    def result = Array<string>.new(2);
    var env_count = 0;

    for (ap in argv) {
        if (ap != Pointer.NULL) {
            var p = ap;
            var c = p.load<byte>();
            var str = StringBuilder.new();
            while (c != '\x00') {
                str.putc(c);
                p++;
                c = p.load<byte>();
            }
            result[env_count] = str.extract();
            env_count++;
        } else {
            break;
        }
    }

    if (env_count != 2) {
        return 2;
    }
    if (!Strings.equal(result[0], "FOO=BAR") || !Strings.equal(result[1], "BAR=BAZ")) {
        return 3;
    }

    return 0;
}