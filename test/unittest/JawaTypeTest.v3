// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def T = UnitTests.registerT("jawa:type:", _, JawaTypeTester.new, _);
def X = [
	T("prim_array1", test_prim_array1),
	T("prim_array2", test_prim_array2),
	T("ref_array1", test_ref_array1),
	T("ref_array2", test_ref_array2),
	T("ref_array3", test_ref_array3),
	T("intf1", test_intf1),
	T("intf2", test_intf2),
	T("intf3", test_intf3),
	()
];

class JawaTypeTester(t: Tester) {
	def assert_assignable(expected: bool, from: JawaType, to: JawaType) {
		if (from.isAssignableToH(to) != expected) {
			t.fail3("expected (%q).isAssignableToH(%q) == %z", from.render, to.render, expected);
		}
		if (from.isAssignableTo(ValueType.Host(to)) != expected) {
			t.fail3("expected (%q).isAssignableTo(%q) == %z", from.render, to.render, expected);
		}
		if (ValueTypes.isAssignable(ValueType.Host(from), ValueType.Host(to)) != expected) {
			t.fail3("expected ValueTypes.isAssignable(%q, %q) == %z", from.render, to.render, expected);
		}
	}
}


def test_prim_array1(t: JawaTypeTester) {
	for (x in JawaTypes.PRIMITIVES) {
		var ax = x.arrayOf();
		for (y in JawaTypes.PRIMITIVES) {
			var ay = y.arrayOf();
			t.assert_assignable(x == y, x, y);
			t.assert_assignable(x == y, ax, ay);
			t.assert_assignable(false, x, ay);
		}
	}
}

def test_prim_array2(t: JawaTypeTester) {
	for (x in JawaTypes.PRIMITIVES) {
		var ax = x.arrayOf();
		t.assert_assignable(true, ax, JawaTypes.OBJECT);
		t.assert_assignable(false, JawaTypes.OBJECT, ax);
		t.assert_assignable(false, ax, JawaTypes.STRING);
		t.assert_assignable(false, JawaTypes.STRING, ax);
	}
}

def test_ref_array1(t: JawaTypeTester) {
	var rt = JawaTypes.OBJECT.arrayOf();
	for (x in [JawaTypes.OBJECT, JawaTypes.STRING]) {
		var at = x.arrayOf();
		t.assert_assignable(true, at, JawaTypes.OBJECT);
		t.assert_assignable(false, JawaTypes.OBJECT, at);
		t.assert_assignable(true, at, rt);
	}
	t.assert_assignable(false, rt, JawaTypes.STRING.arrayOf());
}

def test_ref_array2(t: JawaTypeTester) {
	for (x in [JawaTypes.OBJECT, JawaTypes.STRING]) {
		var ax = x.arrayOf();
		for (y in JawaTypes.PRIMITIVES) {
			var ay = y.arrayOf();
			t.assert_assignable(false, ax, ay);
			t.assert_assignable(false, x, ay);
		}
	}
}

def test_ref_array3(t: JawaTypeTester) {
	var rt1 = JawaTypes.OBJECT.arrayOf(), rt2 = rt1.arrayOf();
	for (x in [JawaTypes.OBJECT, JawaTypes.STRING]) {
		var at = x.arrayOf().arrayOf();
		t.assert_assignable(true, at, JawaTypes.OBJECT);
		t.assert_assignable(false, JawaTypes.OBJECT, at);
		t.assert_assignable(true, at, rt1);
		t.assert_assignable(true, at, rt2);
	}
}

def test_intf1(t: JawaTypeTester) {
	var jlo = JawaClasses.OBJECT;

	var ita = JawaInterfaceType.new(JawaStrings.of("IA"), JawaClasses.NO_INTERFACES);
	var itb = JawaInterfaceType.new(JawaStrings.of("IB"), JawaClasses.NO_INTERFACES);
	var ctx = JawaClassType.new(JawaStrings.of("CX"), jlo, [ita]);
	var cty = JawaClassType.new(JawaStrings.of("CY"), jlo, [itb]);
	var ctz = JawaClassType.new(JawaStrings.of("CZ"), jlo, [ita, itb]);

	t.assert_assignable(false, ita, itb);
	t.assert_assignable(false, itb, ita);

	t.assert_assignable(true, ita, jlo);
	t.assert_assignable(true, itb, jlo);

	t.assert_assignable(true, ctx, ita);
	t.assert_assignable(false, cty, ita);
	t.assert_assignable(true, ctz, ita);

	t.assert_assignable(false, ctx, itb);
	t.assert_assignable(true, cty, itb);
	t.assert_assignable(true, ctz, itb);

	t.assert_assignable(false, ita, ctx);
	t.assert_assignable(false, ita, cty);
	t.assert_assignable(false, ita, ctz);

	t.assert_assignable(false, itb, ctx);
	t.assert_assignable(false, itb, cty);
	t.assert_assignable(false, itb, ctz);
}

def test_intf2(t: JawaTypeTester) {
	var jlo = JawaClasses.OBJECT;

	var ita = JawaInterfaceType.new(JawaStrings.of("IA"), JawaClasses.NO_INTERFACES);
	var itb = JawaInterfaceType.new(JawaStrings.of("IB"), JawaClasses.NO_INTERFACES);
	var itc = JawaInterfaceType.new(JawaStrings.of("IC"), [ita, itb]);

	t.assert_assignable(false, ita, itb);
	t.assert_assignable(false, itb, ita);

	t.assert_assignable(true, ita, jlo);
	t.assert_assignable(true, itb, jlo);
	t.assert_assignable(true, itc, jlo);

	t.assert_assignable(true, itc, ita);
	t.assert_assignable(true, itc, itb);
}

def test_intf3(t: JawaTypeTester) {
	var jlo = JawaClasses.OBJECT;

	var ita = JawaInterfaceType.new(JawaStrings.of("IA"), JawaClasses.NO_INTERFACES);
	var itb = JawaInterfaceType.new(JawaStrings.of("IB"), [ita]);
	var itc = JawaInterfaceType.new(JawaStrings.of("IC"), [ita]);
	var itd = JawaInterfaceType.new(JawaStrings.of("ID"), [itb, itc]);

	var ctx = JawaClassType.new(JawaStrings.of("CX"), jlo, [itd]);

	t.assert_assignable(true, itb, ita);
	t.assert_assignable(true, itc, ita);

	t.assert_assignable(true, itd, itb);
	t.assert_assignable(true, itd, itc);
	t.assert_assignable(true, itd, ita);
	t.assert_assignable(true, itd, itd);

	t.assert_assignable(true, ctx, itb);
	t.assert_assignable(true, ctx, itc);
	t.assert_assignable(true, ctx, ita);
	t.assert_assignable(true, ctx, itd);
}
