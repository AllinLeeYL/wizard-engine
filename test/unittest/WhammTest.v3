// Copyright 2024 Wizard authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def T = UnitTests.registerT("whamm:", _, WhammTester.new, _);
def X_ = [
	T("parse1", test_parse1),
	T("parse2", test_parse2),
	T("call1", test_call1),
	T("call2", test_call2),
	// TODO: immN, localN, argN
	()
];

class WhammTester(t: Tester) {
	def assert_params(expected: Array<WhammParam>, str: string) {
		var r = TextReader.new("<input>", str);
		var got = WhammUtil.parseParams(r);
		if (!r.ok) t.fail1("failed to parse: %s", r.error_msg);
		if (got.length != expected.length) return t.fail2("expected %d params, got %d", expected.length, got.length);
		for (i < expected.length) {
			if (!equal(expected[i], got[i])) {
				var buf = StringBuilder.new();
				buf.puts("expected [");
				for (i < expected.length) {
					if (i > 0) buf.csp();
					expected[i].render(buf);
				}
				buf.puts("], got [");
				for (i < got.length) {
					if (i > 0) buf.csp();
					got[i].render(buf);
				}
				buf.puts("]");
				return t.fail(buf.toString());
			}
		}
	}
	def equal(a: WhammParam, b: WhammParam) -> bool {
		if (a == b) return true;
		match (a) {
			Imm(orig, i) => match (b) {
				Imm(orig, j) => return i == j;
				_ => ;
			}
			Stack(orig, i) => match (b) {
				Stack(orig, j) => return i == j;
				_ => ;
			}
			Local(orig, i) => match (b) {
				Local(orig, j) => return i == j;
				_ => ;
			}
			Call(target, params) => match (b) {
				Call(t2, p2) => {
					return Strings.equal(target.image, t2.image) && Arrays.allTrue(params, p2, equal);
				}
				_ => ;
			}
			_ => ;
		}
		return false;
	}
}

def PC = WhammParam.Pc;
def FUNC = WhammParam.Func;
def FRAME = WhammParam.FrameAccessor;

def CALL(str: string, params: Array<WhammParam>) -> WhammParam.Call {
	return WhammParam.Call(Token.new("<file>", str, 0, 0), params);
}

def test_parse1(t: WhammTester) {
	t.assert_params([PC], "(pc)");
	t.assert_params([FUNC], "(func)");
	t.assert_params([FRAME], "(frame)");
}

def test_parse2(t: WhammTester) {
	t.assert_params([PC, PC], "(pc,pc)");
	t.assert_params([FUNC, PC], "(func,pc)");
	t.assert_params([FRAME, PC], "(frame,pc)");

	t.assert_params([PC, FUNC], "(pc, func)");
	t.assert_params([FUNC, FUNC], "(func, func)");
	t.assert_params([FRAME, FUNC], "(frame, func)");

	t.assert_params([PC, FRAME], "(pc,  frame)");
	t.assert_params([FUNC, FRAME], "(func,    frame)");
	t.assert_params([FRAME, FRAME], "(frame,     frame)");
}

def test_call1(t: WhammTester) {
	t.assert_params([CALL("foobar", [PC])], "(foobar(pc))");
	t.assert_params([CALL("bar", [FUNC])], "(bar(func))");
	t.assert_params([CALL("$BAZ", [FRAME])], "($BAZ(frame))");
	t.assert_params([CALL("$BAZ", [FRAME])], "($BAZ(frame))");
	t.assert_params([CALL("11_29", [])], "(11_29())");
}

def test_call2(t: WhammTester) {
	t.assert_params([CALL("foo", [FUNC]), CALL("bar", [])], "(foo(func), bar())");
}