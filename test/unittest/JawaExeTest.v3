// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def T = UnitTests.registerT(_, JawaTester.new, _);
def X = [
	T("jawa:exe_baload", test_exe_baload),
	T("jawa:exe_caload", test_exe_caload),
	T("jawa:exe_daload", test_exe_daload),
	T("jawa:exe_faload", test_exe_faload),
	T("jawa:exe_iaload", test_exe_iaload),
	T("jawa:exe_laload", test_exe_laload),
	T("jawa:exe_saload", test_exe_saload),
	T("jawa:exe_bastore", test_exe_bastore),
	T("jawa:exe_castore", test_exe_castore),
	T("jawa:exe_dastore", test_exe_dastore),
	T("jawa:exe_fastore", test_exe_fastore),
	T("jawa:exe_iastore", test_exe_iastore),
	T("jawa:exe_lastore", test_exe_lastore),
	T("jawa:exe_sastore", test_exe_sastore),
	T("jawa:exe_xnewarray", test_exe_xnewarray),
	T("jawa:exe_arraylength1", test_exe_arraylength1),
	T("jawa:exe_arraylength2", test_exe_arraylength2),
	T("jawa:exe_acmpeq", test_exe_acmpeq),
	T("jawa:exe_anewarray", test_exe_anewarray),
	T("jawa:exe_aaload", test_exe_aaload),
	T("jawa:exe_aastore", test_exe_aastore),
	T("jawa:exe_isnull", test_exe_isnull),
	T("jawa:exe_multianewarray1", test_exe_multianewarray1),
	T("jawa:exe_multianewarray2", test_exe_multianewarray2),
	T("jawa:exe_multianewarray3", test_exe_multianewarray3),
	T("jawa:exe_multianewarrayP1", test_exe_multianewarrayP1),
	T("jawa:exe_multianewarrayP2", test_exe_multianewarrayP2),
	T("jawa:exe_new", test_exe_new),
	T("jawa:exe_getfield", test_exe_getfield),
	T("jawa:exe_putfield", test_exe_putfield),
	()
];

def NPE = JawaTraps.NullPointerException; // TODO: more precise error checking
def AIOBE = JawaTraps.ArrayIndexOutOfBoundsException; // TODO: more precise error checking
def NASE = JawaTraps.NegativeArraySizeException; // TODO: more precise error checking

// Value conversion helpers.
def i_v = Values.i_v;
def l_v = Values.l_v;
def d_v = Value.F64;
def f_v = Value.F32;

def t_r<T>(f: T -> Value, v: T) -> Result { // T -> Value -> Result
	return Result.Value([f(v)]);
}

def i_r = t_r(Values.i_v, _); // i32 -> Result
def d_r = t_r(Value.F64, _);  // double -> Result
def f_r = t_r(Value.F32, _);  // float -> Result
def l_r = t_r(Values.l_v, _); // long -> Result
def o_r = t_r(Value.ExternRef, _); // HostObject -> Result
def v_r(v: Value) -> Result { return Result.Value([v]); }

def vi_vv(v1: Value, v2: int) -> Array<Value> {
	return [v1, i_v(v2)];
}

def rNONE = Result.Value(Values.NONE);

def test_exe_baload(t: JawaTester) {
	var func = t.make_xaload(JawaPrimArrayOpcodes.BYTE);
	if (func == null) return;
	def run = Interpreter.new().run(100, func, _);
	var array = Value.ExternRef(JawaArrayObjectOf<i8>.new([
		8, 102, -116
	]));

	t.assert_r(NPE, run(vi_vv(Values.EXTERNREF_NULL, 0)));
	t.assert_r(AIOBE, run(vi_vv(array, -1)));
	t.assert_r(AIOBE, run(vi_vv(array, 3)));
	t.assert_r(AIOBE, run(vi_vv(array, 5)));
	t.assert_r(AIOBE, run(vi_vv(array, 999)));
	t.assert_r(AIOBE, run(vi_vv(array, int.max)));
	t.assert_r(AIOBE, run(vi_vv(array, int.min)));

	t.assert_r(i_r(8), run(vi_vv(array, 0)));
	t.assert_r(i_r(102), run(vi_vv(array, 1)));
	t.assert_r(i_r(-116), run(vi_vv(array, 2)));
}

def test_exe_caload(t: JawaTester) {
	var func = t.make_xaload(JawaPrimArrayOpcodes.CHAR);
	if (func == null) return;
	def run = Interpreter.new().run(100, func, _);
	var array = Value.ExternRef(JawaArrayObjectOf<u16>.new([
		7, 10000, 32767, u16.max
	]));

	t.assert_r(NPE, run(vi_vv(Values.EXTERNREF_NULL, 0)));
	t.assert_r(AIOBE, run(vi_vv(array, -1)));
	t.assert_r(AIOBE, run(vi_vv(array, 4)));
	t.assert_r(AIOBE, run(vi_vv(array, 5)));
	t.assert_r(AIOBE, run(vi_vv(array, 9991)));
	t.assert_r(AIOBE, run(vi_vv(array, int.max)));
	t.assert_r(AIOBE, run(vi_vv(array, int.min)));

	t.assert_r(i_r(7), run(vi_vv(array, 0)));
	t.assert_r(i_r(10000), run(vi_vv(array, 1)));
	t.assert_r(i_r(32767), run(vi_vv(array, 2)));
	t.assert_r(i_r(65535), run(vi_vv(array, 3)));
}

def test_exe_daload(t: JawaTester) {
	var func = t.make_xaload(JawaPrimArrayOpcodes.DOUBLE);
	if (func == null) return;
	def run = Interpreter.new().run(100, func, _);
	var array = Value.ExternRef(JawaArrayObjectOf<double>.new([
		double.view(0x4021cccccccccccd),
		double.view(0xc073000000000000),
		double.view(0x7ff8000000000000),
		double.view(0x7ff0000000000000)
	]));

	t.assert_r(NPE, run(vi_vv(Values.EXTERNREF_NULL, 0)));
	t.assert_r(AIOBE, run(vi_vv(array, -1)));
	t.assert_r(AIOBE, run(vi_vv(array, 4)));
	t.assert_r(AIOBE, run(vi_vv(array, 5)));
	t.assert_r(AIOBE, run(vi_vv(array, 9991)));
	t.assert_r(AIOBE, run(vi_vv(array, int.max)));
	t.assert_r(AIOBE, run(vi_vv(array, int.min)));

	t.assert_r(d_r(0x4021cccccccccccd), run(vi_vv(array, 0)));
	t.assert_r(d_r(0xc073000000000000), run(vi_vv(array, 1)));
	t.assert_r(d_r(0x7ff8000000000000), run(vi_vv(array, 2)));
	t.assert_r(d_r(0x7ff0000000000000), run(vi_vv(array, 3)));
}

def test_exe_faload(t: JawaTester) {
	var func = t.make_xaload(JawaPrimArrayOpcodes.FLOAT);
	if (func == null) return;
	def run = Interpreter.new().run(100, func, _);
	var array = Value.ExternRef(JawaArrayObjectOf<float>.new([
		float.view(0x410e6666),
		float.view(0xc3510000),
		float.view(0x7fc00000),
		float.view(0x7f800000)
	]));

	t.assert_r(NPE, run(vi_vv(Values.EXTERNREF_NULL, 0)));
	t.assert_r(AIOBE, run(vi_vv(array, -1)));
	t.assert_r(AIOBE, run(vi_vv(array, 4)));
	t.assert_r(AIOBE, run(vi_vv(array, 5)));
	t.assert_r(AIOBE, run(vi_vv(array, 9991)));
	t.assert_r(AIOBE, run(vi_vv(array, int.max)));
	t.assert_r(AIOBE, run(vi_vv(array, int.min)));

	t.assert_r(f_r(0x410e6666), run(vi_vv(array, 0)));
	t.assert_r(f_r(0xc3510000), run(vi_vv(array, 1)));
	t.assert_r(f_r(0x7fc00000), run(vi_vv(array, 2)));
	t.assert_r(f_r(0x7f800000), run(vi_vv(array, 3)));
}

def test_exe_iaload(t: JawaTester) {
	var func = t.make_xaload(JawaPrimArrayOpcodes.INT);
	if (func == null) return;
	def run = Interpreter.new().run(100, func, _);
	var array = Value.ExternRef(JawaArrayObjectOf<i32>.new([
		5, 10100, int.min, int.max
	]));

	t.assert_r(NPE, run(vi_vv(Values.EXTERNREF_NULL, 0)));
	t.assert_r(AIOBE, run(vi_vv(array, -1)));
	t.assert_r(AIOBE, run(vi_vv(array, 4)));
	t.assert_r(AIOBE, run(vi_vv(array, 5)));
	t.assert_r(AIOBE, run(vi_vv(array, 9191)));
	t.assert_r(AIOBE, run(vi_vv(array, int.max)));
	t.assert_r(AIOBE, run(vi_vv(array, int.min)));

	t.assert_r(i_r(5), run(vi_vv(array, 0)));
	t.assert_r(i_r(10100), run(vi_vv(array, 1)));
	t.assert_r(i_r(int.min), run(vi_vv(array, 2)));
	t.assert_r(i_r(int.max), run(vi_vv(array, 3)));
}

def test_exe_laload(t: JawaTester) {
	var func = t.make_xaload(JawaPrimArrayOpcodes.LONG);
	if (func == null) return;
	def run = Interpreter.new().run(100, func, _);
	var array = Value.ExternRef(JawaArrayObjectOf<i64>.new([
		5, 10100, int.min, int.max, long.min, long.max
	]));

	t.assert_r(NPE, run(vi_vv(Values.EXTERNREF_NULL, 0)));
	t.assert_r(AIOBE, run(vi_vv(array, -1)));
	t.assert_r(AIOBE, run(vi_vv(array, 6)));
	t.assert_r(AIOBE, run(vi_vv(array, 7)));
	t.assert_r(AIOBE, run(vi_vv(array, 9291)));
	t.assert_r(AIOBE, run(vi_vv(array, int.max)));
	t.assert_r(AIOBE, run(vi_vv(array, int.min)));

	t.assert_r(l_r(5), run(vi_vv(array, 0)));
	t.assert_r(l_r(10100), run(vi_vv(array, 1)));
	t.assert_r(l_r(int.min), run(vi_vv(array, 2)));
	t.assert_r(l_r(int.max), run(vi_vv(array, 3)));
	t.assert_r(l_r(long.min), run(vi_vv(array, 4)));
	t.assert_r(l_r(long.max), run(vi_vv(array, 5)));
}

def test_exe_saload(t: JawaTester) {
	var func = t.make_xaload(JawaPrimArrayOpcodes.SHORT);
	if (func == null) return;
	def run = Interpreter.new().run(100, func, _);
	var array = Value.ExternRef(JawaArrayObjectOf<i16>.new([
		6, 10103, -20004, i16.min, i16.max
	]));

	t.assert_r(NPE, run(vi_vv(Values.EXTERNREF_NULL, 0)));
	t.assert_r(AIOBE, run(vi_vv(array, -1)));
	t.assert_r(AIOBE, run(vi_vv(array, 5)));
	t.assert_r(AIOBE, run(vi_vv(array, 6)));
	t.assert_r(AIOBE, run(vi_vv(array, 9995)));
	t.assert_r(AIOBE, run(vi_vv(array, int.max)));
	t.assert_r(AIOBE, run(vi_vv(array, int.min)));

	t.assert_r(i_r(6), run(vi_vv(array, 0)));
	t.assert_r(i_r(10103), run(vi_vv(array, 1)));
	t.assert_r(i_r(-20004), run(vi_vv(array, 2)));
	t.assert_r(i_r(i16.min), run(vi_vv(array, 3)));
	t.assert_r(i_r(i16.max), run(vi_vv(array, 4)));
}

def test_exe_bastore(t: JawaTester) {
	var func = t.make_xastore(JawaPrimArrayOpcodes.BYTE);
	if (func == null) return;
	def run = Interpreter.new().run(100, func, _);
	var array = JawaArrayObjectOf<i8>.new([
		-1, 2, 11, 123, -109
	]);
	var ref = Value.ExternRef(array), zero = Values.I32_0;

	t.assert_r(NPE, run([Values.EXTERNREF_NULL, zero, zero]));
	t.assert_r(AIOBE, run([ref, i_v(-1), zero]));
	t.assert_r(AIOBE, run([ref, i_v(5), zero]));
	t.assert_r(AIOBE, run([ref, i_v(6), zero]));
	t.assert_r(AIOBE, run([ref, i_v(29995), zero]));
	t.assert_r(AIOBE, run([ref, i_v(int.max), zero]));
	t.assert_r(AIOBE, run([ref, i_v(int.min), zero]));

	t.assert_r(rNONE, run([ref, i_v(0), zero]));
	t.assert_a<i8>([0, 2, 11, 123, -109], array.elems);

	t.assert_r(rNONE, run([ref, i_v(1), i_v(99)]));
	t.assert_a<i8>([0, 99, 11, 123, -109], array.elems);

	t.assert_r(rNONE, run([ref, i_v(2), i_v(-5)]));
	t.assert_a<i8>([0, 99, -5, 123, -109], array.elems);

	t.assert_r(rNONE, run([ref, i_v(3), i_v(44)]));
	t.assert_a<i8>([0, 99, -5, 44, -109], array.elems);

	t.assert_r(rNONE, run([ref, i_v(4), i_v(i8.max)]));
	t.assert_a<i8>([0, 99, -5, 44, i8.max], array.elems);
}

def test_exe_castore(t: JawaTester) {
	var func = t.make_xastore(JawaPrimArrayOpcodes.CHAR);
	if (func == null) return;
	def run = Interpreter.new().run(100, func, _);
	var array = JawaArrayObjectOf<u16>.new([
		1, 2, 11, 12345, 65535
	]);
	var ref = Value.ExternRef(array), zero = Values.I32_0;

	t.assert_r(NPE, run([Values.EXTERNREF_NULL, zero, zero]));
	t.assert_r(AIOBE, run([ref, i_v(-1), zero]));
	t.assert_r(AIOBE, run([ref, i_v(5), zero]));
	t.assert_r(AIOBE, run([ref, i_v(6), zero]));
	t.assert_r(AIOBE, run([ref, i_v(29995), zero]));
	t.assert_r(AIOBE, run([ref, i_v(int.max), zero]));
	t.assert_r(AIOBE, run([ref, i_v(int.min), zero]));

	t.assert_r(rNONE, run([ref, i_v(0), zero]));
	t.assert_a<u16>([0, 2, 11, 12345, 65535], array.elems);

	t.assert_r(rNONE, run([ref, i_v(1), i_v(99)]));
	t.assert_a<u16>([0, 99, 11, 12345, 65535], array.elems);

	t.assert_r(rNONE, run([ref, i_v(4), i_v(555)]));
	t.assert_a<u16>([0, 99, 11, 12345, 555], array.elems);

	t.assert_r(rNONE, run([ref, i_v(2), i_v(u16.max)]));
	t.assert_a<u16>([0, 99, 65535, 12345, 555], array.elems);
}

def test_exe_dastore(t: JawaTester) {
	var func = t.make_xastore(JawaPrimArrayOpcodes.DOUBLE);
	if (func == null) return;
	def run = Interpreter.new().run(100, func, _);
	var array = JawaArrayObjectOf<double>.new([
		double.view(0x4021cccccccccccd),
		double.view(0xc073000000000000),
		double.view(0x7ff8000000000000),
		double.view(0x7ff0000000000000),
		double.view(0xc071000456789ABC)
	]);
	var ref = Value.ExternRef(array), zero = Values.F64_0;

	t.assert_r(NPE, run([Values.EXTERNREF_NULL, i_v(0), zero]));
	t.assert_r(AIOBE, run([ref, i_v(-1), zero]));
	t.assert_r(AIOBE, run([ref, i_v(5), zero]));
	t.assert_r(AIOBE, run([ref, i_v(6), zero]));
	t.assert_r(AIOBE, run([ref, i_v(129995), zero]));
	t.assert_r(AIOBE, run([ref, i_v(int.max), zero]));
	t.assert_r(AIOBE, run([ref, i_v(int.min), zero]));

	t.assert_r(rNONE, run([ref, i_v(0), zero]));
	t.assert_da([
		double.view(0x0000000000000000),
		double.view(0xc073000000000000),
		double.view(0x7ff8000000000000),
		double.view(0x7ff0000000000000),
		double.view(0xc071000456789ABC)], array.elems);

	t.assert_r(rNONE, run([ref, i_v(3), d_v(0x1111222233334444)]));
	t.assert_da([
		double.view(0x0000000000000000),
		double.view(0xc073000000000000),
		double.view(0x7ff8000000000000),
		double.view(0x1111222233334444),
		double.view(0xc071000456789ABC)], array.elems);
}

def test_exe_fastore(t: JawaTester) {
	var func = t.make_xastore(JawaPrimArrayOpcodes.FLOAT);
	if (func == null) return;
	def run = Interpreter.new().run(100, func, _);
	var array = JawaArrayObjectOf<float>.new([
		float.view(0x410e6666),
		float.view(0xc3510000),
		float.view(0x7fc00000),
		float.view(0x7f800000),
		float.view(0xd2110c43)
	]);
	var ref = Value.ExternRef(array), zero = Values.F32_0;

	t.assert_r(NPE, run([Values.EXTERNREF_NULL, i_v(0), zero]));
	t.assert_r(AIOBE, run([ref, i_v(-1), zero]));
	t.assert_r(AIOBE, run([ref, i_v(5), zero]));
	t.assert_r(AIOBE, run([ref, i_v(6), zero]));
	t.assert_r(AIOBE, run([ref, i_v(129995), zero]));
	t.assert_r(AIOBE, run([ref, i_v(int.max), zero]));
	t.assert_r(AIOBE, run([ref, i_v(int.min), zero]));

	t.assert_r(rNONE, run([ref, i_v(0), zero]));
	t.assert_fa([
		float.view(0x00000000),
		float.view(0xc3510000),
		float.view(0x7fc00000),
		float.view(0x7f800000),
		float.view(0xd2110c43)
	], array.elems);

	t.assert_r(rNONE, run([ref, i_v(3), f_v(0x11223344)]));
	t.assert_fa([
		float.view(0x00000000),
		float.view(0xc3510000),
		float.view(0x7fc00000),
		float.view(0x11223344),
		float.view(0xd2110c43)
	], array.elems);
}

def test_exe_iastore(t: JawaTester) {
	var func = t.make_xastore(JawaPrimArrayOpcodes.INT);
	if (func == null) return;
	def run = Interpreter.new().run(100, func, _);
	var array = JawaArrayObjectOf<i32>.new([
		1, 2, -13, 72345, 6553509
	]);
	var ref = Value.ExternRef(array), zero = Values.I32_0;

	t.assert_r(NPE, run([Values.EXTERNREF_NULL, zero, zero]));
	t.assert_r(AIOBE, run([ref, i_v(-1), zero]));
	t.assert_r(AIOBE, run([ref, i_v(5), zero]));
	t.assert_r(AIOBE, run([ref, i_v(6), zero]));
	t.assert_r(AIOBE, run([ref, i_v(39995), zero]));
	t.assert_r(AIOBE, run([ref, i_v(int.max), zero]));
	t.assert_r(AIOBE, run([ref, i_v(int.min), zero]));

	t.assert_r(rNONE, run([ref, i_v(0), i_v(555666777)]));
	t.assert_a<i32>([555666777, 2, -13, 72345, 6553509], array.elems);

	t.assert_r(rNONE, run([ref, i_v(1), i_v(997)]));
	t.assert_a<i32>([555666777, 997, -13, 72345, 6553509], array.elems);

	t.assert_r(rNONE, run([ref, i_v(4), i_v(-777888999)]));
	t.assert_a<i32>([555666777, 997, -13, 72345, -777888999], array.elems);

	t.assert_r(rNONE, run([ref, i_v(2), i_v(u16.max)]));
	t.assert_a<i32>([555666777, 997, 65535, 72345, -777888999], array.elems);
}

def test_exe_lastore(t: JawaTester) {
	var func = t.make_xastore(JawaPrimArrayOpcodes.LONG);
	if (func == null) return;
	def run = Interpreter.new().run(100, func, _);
	var array = JawaArrayObjectOf<i64>.new([
		1, 5, -13998744, 72345999888, 6553509998888
	]);
	var ref = Value.ExternRef(array), zero = Values.I64_0;

	t.assert_r(NPE, run([Values.EXTERNREF_NULL, i_v(0), zero]));
	t.assert_r(AIOBE, run([ref, i_v(-1), zero]));
	t.assert_r(AIOBE, run([ref, i_v(5), zero]));
	t.assert_r(AIOBE, run([ref, i_v(6), zero]));
	t.assert_r(AIOBE, run([ref, i_v(49995), zero]));
	t.assert_r(AIOBE, run([ref, i_v(int.max), zero]));
	t.assert_r(AIOBE, run([ref, i_v(int.min), zero]));

	t.assert_r(rNONE, run([ref, i_v(0), l_v(555666777888999)]));
	t.assert_a<i64>([555666777888999, 5, -13998744, 72345999888, 6553509998888], array.elems);

	t.assert_r(rNONE, run([ref, i_v(1), l_v(-997)]));
	t.assert_a<i64>([555666777888999, -997, -13998744, 72345999888, 6553509998888], array.elems);

	t.assert_r(rNONE, run([ref, i_v(4), l_v(-777888999)]));
	t.assert_a<i64>([555666777888999, -997, -13998744, 72345999888, -777888999], array.elems);

	t.assert_r(rNONE, run([ref, i_v(2), l_v(u16.max)]));
	t.assert_a<i64>([555666777888999, -997, 65535, 72345999888, -777888999], array.elems);
}

def test_exe_sastore(t: JawaTester) {
	var func = t.make_xastore(JawaPrimArrayOpcodes.SHORT);
	if (func == null) return;
	def run = Interpreter.new().run(100, func, _);
	var array = JawaArrayObjectOf<i16>.new([
		-1, 2, 11, 12365, -10988
	]);
	var ref = Value.ExternRef(array), zero = Values.I32_0;

	t.assert_r(NPE, run([Values.EXTERNREF_NULL, zero, zero]));
	t.assert_r(AIOBE, run([ref, i_v(-1), zero]));
	t.assert_r(AIOBE, run([ref, i_v(5), zero]));
	t.assert_r(AIOBE, run([ref, i_v(6), zero]));
	t.assert_r(AIOBE, run([ref, i_v(99995), zero]));
	t.assert_r(AIOBE, run([ref, i_v(int.max), zero]));
	t.assert_r(AIOBE, run([ref, i_v(int.min), zero]));

	t.assert_r(rNONE, run([ref, i_v(0), zero]));
	t.assert_a<i16>([0, 2, 11, 12365, -10988], array.elems);

	t.assert_r(rNONE, run([ref, i_v(1), i_v(9937)]));
	t.assert_a<i16>([0, 9937, 11, 12365, -10988], array.elems);

	t.assert_r(rNONE, run([ref, i_v(2), i_v(-5)]));
	t.assert_a<i16>([0, 9937, -5, 12365, -10988], array.elems);

	t.assert_r(rNONE, run([ref, i_v(3), i_v(4444)]));
	t.assert_a<i16>([0, 9937, -5, 4444, -10988], array.elems);

	t.assert_r(rNONE, run([ref, i_v(4), i_v(i16.min)]));
	t.assert_a<i16>([0, 9937, -5, 4444, -32768], array.elems);
}

def test_exe_xnewarrayof<T>(t: JawaTester, c: JawaPrimArrayOpcodes) {
	// XXX(code size): reduce polymorphic duplication of this method
	var i = Interpreter.new();
	var f = t.make_xnewarray(c);
	if (f == null) return;

	t.assert_r(NASE, i.run(100, f, [i_v(-1)]));

	for (len in [0, 1, 5, 257]) {
		var r = i.run(100, f, [i_v(len)]);
		var obj = t.assertIsJawaObject(r);
		if (obj == null) return;
		var array = t.assertIsJawaArrayObjectOf<T>(obj);
		if (array == null) return;
		t.t.asserti(len, array.length());
		t.t.asserti(len, array.elems.length);
	}
}

def test_exe_xnewarray(t: JawaTester) {
	test_exe_xnewarrayof<i8>(t, JawaPrimArrayOpcodes.BYTE);
	test_exe_xnewarrayof<bool>(t, JawaPrimArrayOpcodes.BOOL);
	test_exe_xnewarrayof<u16>(t, JawaPrimArrayOpcodes.CHAR);
	test_exe_xnewarrayof<i16>(t, JawaPrimArrayOpcodes.SHORT);
	test_exe_xnewarrayof<i32>(t, JawaPrimArrayOpcodes.INT);
	test_exe_xnewarrayof<i64>(t, JawaPrimArrayOpcodes.LONG);
	test_exe_xnewarrayof<float>(t, JawaPrimArrayOpcodes.FLOAT);
	test_exe_xnewarrayof<double>(t, JawaPrimArrayOpcodes.DOUBLE);
}

def test_exe_arraylength1(t: JawaTester) {
	var i = Interpreter.new();
	for (c in JawaPrimArrayOpcodes) {
		var f = t.make_xarraylength(c);
		if (f == null) return;

		t.assert_r(NPE, i.run(100, f, [Values.EXTERNREF_NULL]));
		for (len in [0, 1, 258]) {
			var obj = c.map.newarray(len);
			t.assert_r(i_r(len), i.run(100, f, [Value.ExternRef(obj)]));
		}
	}
}

def test_exe_arraylength2(t: JawaTester) {
	var jlo = t.importJawaLangObject();
	var ajlo = t.importArrayOf(jlo);

	var sig = FuncSig.new([ValueType.Imported(ajlo)], SigCache.arr_i);
	def str = JawaAsm.new()
		.putc(JawaFuncOpcode.ARRAYLENGTH.code)
		.put_jawa_i4(ajlo.type_index)
		.extract();
	t.addFuncImport(str, sig);
	var r = t.process();

	var elem = t.assertIsJawaClassType(JawaClasses.OBJECT, r[0]);
	var at = t.assertIsJawaArrayType(elem, r[1]);
	var expected_sig = FuncSig.new([ValueType.Host(at)], SigCache.arr_i);
	var f = t.assertIsFunction(expected_sig, r[2]);
	if (f == null) return;

	var i = Interpreter.new();
	for (len in [0, 1, 259]) {
		var obj = JawaRefArrayObject.new(at, Array<JawaObject>.new(len));
		t.assert_r(i_r(len), i.run(100, f, [Value.ExternRef(obj)]));
	}
}

def test_exe_acmpeq(t: JawaTester) {
	var sig = FuncSig.new([ValueType.Host(JawaTypes.OBJECT), ValueType.Host(JawaTypes.OBJECT)], SigCache.arr_i);
	t.addFuncImport([JawaFuncOpcode.ACMPEQ.code], sig);
	var r = t.process();
	var f = t.assertIsFunction(sig, r[0]);
	if (f == null) return;

	def cases: Array<JawaObject> = [
		null,
		JawaArrayObjectOf<i8>.new(Array<i8>.new(2)),
		JawaArrayObjectOf<float>.new(Array<float>.new(2)),
		JawaObject.new()
	];
	var i = Interpreter.new(), TRUE = i_r(1), FALSE = i_r(0);
	for (x < cases.length) {
		var a = Value.ExternRef(cases[x]);
		for (y < cases.length) {
			var b = Value.ExternRef(cases[y]);
			t.assert_r(if(x == y, TRUE, FALSE), i.run(100, f, [a, b]));
		}
	}
}

def test_exe_anewarray(t: JawaTester) {
	var jlo = t.importJawaLangObject();
	var ajlo = t.importArrayOf(jlo);

	var sig = FuncSig.new(SigCache.arr_i, [ValueType.Imported(ajlo)]);
	var cmd = JawaAsm.new()
		.putc(JawaFuncOpcode.ANEWARRAY.code)
		.put_jawa_i4(ajlo.type_index)
		.extract();
	t.addFuncImport(cmd, sig);
	var r = t.process();

	var elem = t.assertIsJawaClassType(JawaClasses.OBJECT, r[0]);
	var at = t.assertIsJawaArrayType(elem, r[1]);
	var expected_sig = FuncSig.new(SigCache.arr_i, [ValueType.Host(at)]);
	var f = t.assertIsFunction(expected_sig, r[2]);
	if (f == null) return;

	var i = Interpreter.new();
	for (len in [0, 1, 7, 257]) {
		var r = i.run(100, f, [i_v(len)]);
		var obj = t.assertIsJawaObject(r);
		if (obj == null) return;
		var array = t.assertIsJawaRefArrayObject(obj);
		if (array == null) return;
		t.t.asserti(len, array.length());
		t.t.asserti(len, array.elems.length);
	}
}

def test_exe_aaload(t: JawaTester) {
	var jlo = t.importJawaLangObject();
	var ajlo = t.importArrayOf(jlo);

	var sig = FuncSig.new([ValueType.Imported(ajlo), ValueType.I32], [ValueType.Imported(jlo)]);
	var cmd = JawaAsm.new()
		.putc(JawaFuncOpcode.AALOAD.code)
		.put_jawa_i4(ajlo.type_index)
		.extract();
	t.addFuncImport(cmd, sig);
	var r = t.process();

	var elem = t.assertIsJawaClassType(JawaClasses.OBJECT, r[0]);
	var at = t.assertIsJawaArrayType(elem, r[1]);
	var expected_sig = FuncSig.new([ValueType.Host(at), ValueType.I32], [ValueType.Host(at.elem)]);
	var f = t.assertIsFunction(expected_sig, r[2]);
	if (f == null) return;

	var i = Interpreter.new();
	var objs = [
		null,
		JawaObject.new(),
		JawaObject.new(),
		JawaObject.new()
	];
	var array = JawaRefArrayObject.new(at, objs), av = Value.ExternRef(array);
	def run = i.run(100, f, _);
	for (i < objs.length) {
		t.assert_r(o_r(objs[i]), run([av, i_v(i)]));
	}
	t.assert_r(NPE, run([Value.ExternRef(null), i_v(0)]));
	t.assert_r(AIOBE, run([av, i_v(-1)]));
	t.assert_r(AIOBE, run([av, i_v(objs.length)]));
	t.assert_r(AIOBE, run([av, i_v(65555)]));
	t.assert_r(AIOBE, run([av, i_v(int.max)]));
}

def test_exe_aastore(t: JawaTester) {
	var jlo = t.importJawaLangObject();
	var ajlo = t.importArrayOf(jlo);

	var sig = FuncSig.new([ValueType.Imported(ajlo), ValueType.I32, ValueType.Imported(jlo)], SigCache.arr_v);
	var cmd = JawaAsm.new()
		.putc(JawaFuncOpcode.AASTORE.code)
		.put_jawa_i4(ajlo.type_index)
		.extract();
	t.addFuncImport(cmd, sig);
	var r = t.process();

	var elem = t.assertIsJawaClassType(JawaClasses.OBJECT, r[0]);
	var at = t.assertIsJawaArrayType(elem, r[1]);
	var expected_sig = FuncSig.new([ValueType.Host(at), ValueType.I32, ValueType.Host(at.elem)], SigCache.arr_v);
	var f = t.assertIsFunction(expected_sig, r[2]);
	if (f == null) return;

	var i = Interpreter.new();
	var obj1 = JawaObject.new(), obj2 = JawaObject.new(), obj3 = JawaObject.new();
	var array = JawaRefArrayObject.new(at, [null, obj1, obj2, obj3]);
	var av = Value.ExternRef(array), nv = Value.ExternRef(null);
	def run = i.run(100, f, _);

	t.assert_r(rNONE, run([av, i_v(0), nv]));
	t.assert_a([null, obj1, obj2, obj3], array.elems);

	t.assert_r(rNONE, run([av, i_v(0), Value.ExternRef(obj2)]));
	t.assert_a([obj2, obj1, obj2, obj3], array.elems);

	t.assert_r(rNONE, run([av, i_v(2), Value.ExternRef(obj3)]));
	t.assert_a([obj2, obj1, obj3, obj3], array.elems);

	t.assert_r(rNONE, run([av, i_v(3), nv]));
	t.assert_a([obj2, obj1, obj3, null], array.elems);

	t.assert_r(NPE, run([Value.ExternRef(null), i_v(0), nv]));
	t.assert_r(AIOBE, run([av, i_v(-1), nv]));
	t.assert_r(AIOBE, run([av, i_v(4), nv]));
	t.assert_r(AIOBE, run([av, i_v(65555), nv]));
	t.assert_r(AIOBE, run([av, i_v(int.max), nv]));
}

def test_exe_isnull(t: JawaTester) {
	var sig = FuncSig.new([ValueType.Host(JawaTypes.OBJECT)], SigCache.arr_i);
	t.addFuncImport([JawaFuncOpcode.ISNULL.code], sig);
	var r = t.process();
	var f = t.assertIsFunction(sig, r[0]);
	if (f == null) return;

	def cases: Array<JawaObject> = [
		null,
		JawaArrayObjectOf<u16>.new(Array<u16>.new(0)),
		JawaArrayObjectOf<double>.new(Array<double>.new(0)),
		JawaObject.new(),
		JawaObject.new()
	];
	var i = Interpreter.new(), TRUE = i_r(1), FALSE = i_r(0);
	for (x < cases.length) {
		var obj = cases[x];
		t.assert_r(if(obj == null, TRUE, FALSE), i.run(100, f, [Value.ExternRef(obj)]));
	}
}

def test_exe_multianewarray1(t: JawaTester) {
	def dims = 1;
	var jlo = t.importJawaLangObject();
	var ajlo = t.importArrayOf(jlo);

	var sig = FuncSig.new(SigCache.arr_i, [ValueType.Imported(ajlo)]);
	var cmd = JawaAsm.new()
		.putc(JawaFuncOpcode.MULTIANEWARRAY.code)
		.put_jawa_i4(dims)
		.put_jawa_i4(ajlo.type_index)
		.extract();
	t.addFuncImport(cmd, sig);
	var r = t.process();

	var elem = t.assertIsJawaClassType(JawaClasses.OBJECT, r[0]);
	var at = t.assertIsJawaArrayType(elem, r[1]);
	var expected_sig = FuncSig.new(SigCache.arr_i, [ValueType.Host(at)]);
	var f = t.assertIsFunction(expected_sig, r[2]);
	if (f == null) return;

	var i = Interpreter.new();
	for (len in [0, 1, 7, 257]) {
		var r = i.run(100, f, [i_v(len)]);
		var obj = t.assertIsJawaObject(r);
		if (obj == null) return;
		var array = t.assertIsJawaRefArrayObject(obj);
		if (array == null) return;
		t.t.asserti(len, array.length());
		t.t.asserti(len, array.elems.length);
	}
}

def test_exe_multianewarray2(t: JawaTester) {
	def dims = 1;
	var it = t.importMultiArrayOf(t.importJawaLangObject(), dims);
	var pair = t.make_multianewarray(it, dims), at = pair.0, f = pair.1;
	if (f == null) return;

	var i = Interpreter.new();
	for (len in [0, 1, 7, 257]) {
		var r = i.run(100, f, [i_v(len)]);
		var obj = t.assertIsJawaObject(r);
		if (obj == null) return;
		var array = t.assertIsJawaRefArrayObject(obj);
		if (array == null) return;
		t.t.asserti(len, array.length());
		t.t.asserti(len, array.elems.length);
	}
}

def test_exe_multianewarray3(t: JawaTester) {
	def dims = 2;
	var it = t.importMultiArrayOf(t.importJawaLangObject(), dims);
	var pair = t.make_multianewarray(it, dims), at = pair.0, f = pair.1;
	if (f == null) return;

	var i = Interpreter.new();
	for (d1 in [0, 1, 3]) {
		for (d2 in [0, 1, 3]) {
			var r = i.run(100, f, [i_v(d1), i_v(d2)]);
			var obj = t.assertIsJawaObject(r);
			if (obj == null) return;
			var array = t.assertIsJawaRefArrayObject(obj);
			if (array == null) return;
			t.t.asserti(d1, array.length());
			t.t.asserti(d1, array.elems.length);
			for (s in array.elems) {
				var sub = t.assertIsJawaRefArrayObject(s);
				if (sub == null) return;
				t.t.asserti(d2, sub.length());
				t.t.asserti(d2, sub.elems.length);
				for (x in sub.elems) {
					if (x != null) t.t.fail("expected null");
				}
			}
		}
	}
}

def test_exe_multianewarrayP1(t: JawaTester) {
	def dims = 1;
	for (c in JawaPrimArrayOpcodes) {
		var n = JawaTester.new(t.t);
		var it = n.importPrimArray(c);
		var pair = n.make_multianewarray(it, dims), at = pair.0, f = pair.1;
		if (f == null) return;

		var i = Interpreter.new();
		for (len in [0, 1, 7, 257]) {
			var r = i.run(100, f, [i_v(len)]);
			var obj = t.assertIsJawaObject(r);
			if (obj == null) return;
			var array = t.assertIsJawaArrayObject(obj);
			if (array == null) return;
			t.t.asserti(len, array.length());
		}
	}
}

def test_exe_multianewarrayP2(t: JawaTester) {
	def dims = 2;
	for (c in JawaPrimArrayOpcodes) {
		var n = JawaTester.new(t.t);
		var it = n.importArrayOf(n.importPrimArray(c));
		var pair = n.make_multianewarray(it, dims), at = pair.0, f = pair.1;
		if (f == null) return;

		var i = Interpreter.new();
		for (d1 in [0, 1, 3, 5]) {
			for (d2 in [0, 1, 3, 5]) {
				var r = i.run(100, f, [i_v(d1), i_v(d2)]);
				var obj = t.assertIsJawaObject(r);
				if (obj == null) return;
				var array = t.assertIsJawaRefArrayObject(obj);
				if (array == null) return;
				t.t.asserti(d1, array.length());
				t.t.asserti(d1, array.elems.length);
				for (s in array.elems) {
					var sub = t.assertIsJawaArrayObject(s);
					if (sub == null) return;
					t.t.asserti(d2, sub.length());
				}
			}
		}
	}
}

def test_exe_new(t: JawaTester) {
	var jlo = t.importJawaLangObject();
	var name = JawaStrings.of("Foo");
	var str1 = JawaAsm.new()
		.putc(JawaTypeOpcode.FW_CLASS_DECL.code)
		.put_jawa_name(name)
		.put_jawa_i4(jlo.type_index) // super
		.put_jawa_i4(0) // interfaces
		.extract();
	var it1 = t.addTypeImport(str1, ValueTypes.NONE); // fw_class
	var str2 = JawaAsm.new()
		.putc(JawaTypeOpcode.DEF_CLASS.code)
		.put_jawa_i4(it1.type_index) // class index
		.put_jawa_i4(0) // # instance fields
		.put_jawa_i4(0) // # instance methods
		.put_jawa_i4(0) // # static fields
		.put_jawa_i4(0) // # static methods
		.extract();
	var it2 = t.addTypeImport(str2, ValueTypes.NONE);
	var str3 = JawaAsm.new()
		.putc(JawaFuncOpcode.NEW.code)
		.put_jawa_i4(it1.type_index)
		.extract();
	var f1 = t.addFuncImport(str3, FuncSig.new([], [ValueType.Imported(it1)]));

	var r = t.process();
	var ct1 = t.assertIsJawaClassType(null, r[it1.import_index]);
	if (ct1 == null) return;
	var f = t.assertIsFunction(FuncSig.new([], [ValueType.Host(ct1)]), r[f1]);
	if (f == null) return;

	var i = Interpreter.new();
	var got = i.run(100, f, []);
	var obj = t.assertIsJawaInstanceObject(ct1, got);
	if (obj == null) return;
	if (obj.fields.length != 0) return t.t.fail("expected 0 fields");
}

def test_exe_getfield(t: JawaTester) {
	var className = "MyClass", jclassName = JawaStrings.of(className);
	var fieldName = "f1", jfieldName = JawaStrings.of(fieldName);
	var ct = JawaClassType.new(jclassName, null, []);
	ct.boilerplate = [Values.I32_0];
	ct.instanceFields = [JawaField.new(jfieldName, 0, JawaTypes.BYTE)];
	t.env.add(ct);
	var it = t.importClass(className);
	var str1 = JawaAsm.new()
		.putc(JawaFuncOpcode.GETFIELD.code)
		.put_jawa_i4(it.type_index)
		.put_jawa_name(jfieldName)
		.extract();
	var fi = t.addFuncImport(str1, FuncSig.new([ValueType.Imported(it)], [ValueType.I32]));

	var r = t.process();
	t.assertIsJawaClassType(ct, r[it.import_index]);
	var f = t.assertIsFunction(FuncSig.new([ValueType.Host(ct)], [ValueType.I32]), r[fi]);
	if (f == null) return;

	var i = Interpreter.new();
	var obj = JawaInstanceObject.new(ct), ref = Value.ExternRef(obj);
	def run = i.run(100, f, _);

	t.assert_r(i_r(0), run([ref]));

	obj.fields[0] = i_v(33);
	t.assert_r(i_r(33), run([ref]));

	obj.fields[0] = i_v(77);
	t.assert_r(i_r(77), run([ref]));

	t.assert_r(NPE, run([Values.EXTERNREF_NULL]));
}

def test_exe_putfield(t: JawaTester) {
	var className = "MyClass", jclassName = JawaStrings.of(className);
	var fieldName = "f1", jfieldName = JawaStrings.of(fieldName);
	var ct = JawaClassType.new(jclassName, null, []);
	ct.boilerplate = [Values.I32_0];
	ct.instanceFields = [JawaField.new(jfieldName, 0, JawaTypes.INT)];
	t.env.add(ct);
	var it = t.importClass(className);
	var str1 = JawaAsm.new()
		.putc(JawaFuncOpcode.PUTFIELD.code)
		.put_jawa_i4(it.type_index)
		.put_jawa_name(jfieldName)
		.extract();
	var fi = t.addFuncImport(str1, FuncSig.new([ValueType.Imported(it), ValueType.I32], SigCache.arr_v));

	var r = t.process();
	t.assertIsJawaClassType(ct, r[it.import_index]);
	var f = t.assertIsFunction(FuncSig.new([ValueType.Host(ct), ValueType.I32], SigCache.arr_v), r[fi]);
	if (f == null) return;

	var i = Interpreter.new();
	var obj = JawaInstanceObject.new(ct), ref = Value.ExternRef(obj);
	def run = i.run(100, f, _);

	t.assert_r(rNONE, run([ref, i_v(22)]));
	t.assert_r(i_r(22), v_r(obj.fields[0]));

	t.assert_r(rNONE, run([ref, i_v(-89)]));
	t.assert_r(i_r(-89), v_r(obj.fields[0]));

	t.assert_r(rNONE, run([ref, i_v(-99999999)]));
	t.assert_r(i_r(-99999999), v_r(obj.fields[0]));

	t.assert_r(NPE, run([Values.EXTERNREF_NULL, i_v(0)]));
}

// TODO	ATHROW
// TODO	CHECKCAST
// TODO	DCMPG
// TODO	DCMPL
// TODO	DREM
// TODO	FCMPG
// TODO	FCMPL
// TODO	FREM
// TODO	GETSTATIC
// TODO	INSTANCEOF
// TODO	INVOKEDYNAMIC
// TODO	INVOKEINTERFACE
// TODO	INVOKESPECIAL
// TODO	INVOKESTATIC
// TODO	INVOKEVIRTUAL
// TODO	MONITORENTER
// TODO	MONITOREXIT
// TODO	NEW
// TODO	PUTSTATIC
