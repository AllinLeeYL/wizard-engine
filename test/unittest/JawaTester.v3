// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def TEST_GC = false;
component JawaTests {
	private def buf = StringBuilder.new();
	def registerRtTest(prefix: string, name: string, func: JawaTester -> void) {
		if (TEST_GC) {
			buf.reset().put2("jawa:gc:%s:%s", prefix, name);
			UnitTests.register(buf.extract(), run(_, JawaGcRuntime.new, func));
		}
		buf.reset().put2("jawa:host:%s:%s", prefix, name);
		UnitTests.register(buf.extract(), run(_, JawaHostRuntime.new, func));
	}
	def run(t: Tester, newRt: () -> JawaRuntime, func: JawaTester -> void) {
		func(JawaTester.new(t, newRt()));
	}
}

def newSig = SigDecl.new(ValueTypes.NO_HEAPTYPES, _, _);

class JawaTester extends ModuleBuilder {
	def NO_CLASS_ATTRS: JawaClassAttr.set;
	def NO_METHOD_ATTRS: JawaMethodAttr.set;
	def NO_FIELD_ATTRS: JawaFieldAttr.set;

	def reftypes = Vector<(JawaRefType, AbsTypeDecl)>.new();

	def t: Tester;
	def rt: JawaRuntime;
	def err = ErrorGen.new("<JawaTest>");
	def env = JawaEnvironment.new(null);

	private def asm = JawaAsm.new();

	new(t, rt) {
		extensions |= Jawa.ENGINE_EXTENSIONS;
	}
	def dup() -> JawaTester {
		return JawaTester.new(t, rt);
	}

	def addTypeImport(asm: JawaAsm, sup: Array<ValueType>) -> AbsTypeDecl {
		var it = AbsTypeDecl.new(true, true, sup);
		module.addImport("jawa", asm.extract(), asm.args.extract(), it);
		return it;
	}
	def addFuncImport(asm: JawaAsm, sig: SigDecl) -> FuncDecl {
		var sig_decl = sig.dup();
		module.addDecl(sig_decl);
		var func = FuncDecl.new(sig_decl.sig_index);
		module.addImport("jawa", asm.extract(), asm.args.extract(), func);
		return func;
	}
	def addGlobalImport(asm: JawaAsm, vt: ValueType) -> GlobalDecl {
		var global = GlobalDecl.new(vt, false, InitExpr.I32(0));
		module.addImport("jawa", asm.extract(), asm.args.extract(), global);
		return global;
	}
	def importClass(name: string) -> AbsTypeDecl {
		asm.reset()
			.putc(JawaTypeOpcode.EXT_CLASS.code)
			.put_jname(JawaStrings.of(name));
		return addTypeImport(asm, ValueTypes.NONE);
	}
	def importJawaLangObject() -> AbsTypeDecl {
		env.add(JawaLang.OBJECT);
		return importRefType(JawaLang.OBJECT);
	}
	def importJawaLangString() -> AbsTypeDecl {
		env.add(JawaLang.STRING);
		return importRefType(JawaLang.STRING);
	}
	def importJawaLangClass() -> AbsTypeDecl {
		env.add(JawaLang.CLASS);
		return importRefType(JawaLang.CLASS);
	}
	def importPrimArray(c: JawaPrimArrayOpcodes) -> AbsTypeDecl {
		return addTypeImport(strCode(c.type_code.code), ValueTypes.NONE);
	}
	def importInterface(name: string) -> AbsTypeDecl {
		asm.reset()
			.putc(JawaTypeOpcode.EXT_INTERFACE.code)
			.put_jname(JawaStrings.of(name));
		return addTypeImport(asm, ValueTypes.NONE);
	}
	def importArrayOf(it: AbsTypeDecl) -> AbsTypeDecl {
		asm.reset()
			.putc(JawaTypeOpcode.REF_ARRAY.code)
			.put_abstype(it);
		return addTypeImport(asm, ValueTypes.NONE);
	}
	def importMultiArrayOf(it: AbsTypeDecl, rank: int) -> AbsTypeDecl {
		var last = it;
		for (i < rank) last = importArrayOf(last);
		return last;
	}
	def importType(t: JawaType) -> JawaTypeRef {
		if (t.kind == JawaKind.VOID) return JawaTypeRef.Void;
		match (t) {
			x: JawaPrimType => return JawaTypeRef.Prim(x);
			x: JawaArrayType => return JawaTypeRef.Abstract(importRefType(x));
			x: JawaClassType => return JawaTypeRef.Abstract(importRefType(x));
			x: JawaInterfaceType => return JawaTypeRef.Abstract(importRefType(x));
			_ => return JawaTypeRef.Void; // TODO: that's an error
		}
	}
	def importRefType(t: JawaRefType) -> AbsTypeDecl {
		for (i < reftypes.length) {
			var e = reftypes[i];
			if (e.0 == t) return e.1;
		}
		var r: AbsTypeDecl;
		match (t) {
			x: JawaArrayType => match (x.elem.kind) {
				BYTE => r = importPrimArray(JawaPrimArrayOpcodes.BYTE);
				BOOL => r = importPrimArray(JawaPrimArrayOpcodes.BOOL);
				CHAR => r = importPrimArray(JawaPrimArrayOpcodes.CHAR);
				SHORT => r = importPrimArray(JawaPrimArrayOpcodes.SHORT);
				INT => r = importPrimArray(JawaPrimArrayOpcodes.INT);
				LONG => r = importPrimArray(JawaPrimArrayOpcodes.LONG);
				FLOAT => r = importPrimArray(JawaPrimArrayOpcodes.FLOAT);
				DOUBLE => r = importPrimArray(JawaPrimArrayOpcodes.DOUBLE);
				VOID => r = null;
				REF => r = importArrayOf(importRefType(JawaRefType.!(x.elem)));
			}
			x: JawaClassType => {
				asm.reset()
					.putc(JawaTypeOpcode.EXT_CLASS.code)
					.put_jname(x.name);
				r = addTypeImport(asm, ValueTypes.NONE);
			}
			x: JawaInterfaceType => {
				asm.reset()
					.putc(JawaTypeOpcode.EXT_INTERFACE.code)
					.put_jname(x.name);
				r = addTypeImport(asm, ValueTypes.NONE);
			}
			_ => r = null;
		}
		reftypes.put(t, r);
		return r;
	}
	def importSig(jsig: JawaMethodSig) -> JawaSigRef {
		return JawaSigRef.new(Arrays.map(jsig.params, importType), importType(jsig.ret));
	}
	def declareClass(name: string, attrs: JawaClassAttr.set, sup: AbsTypeDecl, intfs: Array<AbsTypeDecl>) -> AbsTypeDecl {
		if (sup == null) sup = importJawaLangObject();
		var jname = JawaStrings.of(name);
		var cmd = asm.reset()
			.putc(JawaTypeOpcode.DECL_CLASS.code)
			.put_jname(jname)
			.put_class_attrs(attrs)
			.put_abstype(sup)
			.put_ji4(intfs.length);
		var its = Array<AbsTypeDecl>.new(intfs.length);
		var constraints = Array<ValueType>.new(intfs.length + 1);
		constraints[0] = ValueType.Abstract(sup);
		for (i < intfs.length) {
			var it = intfs[i];
			cmd.put_abstype(it);
			constraints[i + 1] = ValueType.Abstract(it);
		}
		return addTypeImport(cmd, constraints);
	}
	def declareInterface(name: string, intfs: Array<AbsTypeDecl>) -> AbsTypeDecl {
		var jname = JawaStrings.of(name);
		var cmd = asm.reset()
			.putc(JawaTypeOpcode.DECL_INTERFACE.code)
			.put_jname(jname)
			.put_ji4(intfs.length);
		var its = Array<AbsTypeDecl>.new(intfs.length);
		var constraints = Array<ValueType>.new(intfs.length);
		for (i < intfs.length) {
			var it = intfs[i];
			cmd.put_abstype(it);
			constraints[i] = ValueType.Abstract(it);
		}
		return addTypeImport(cmd, constraints);
	}
	def defineClass(it: AbsTypeDecl,
		instanceFields: Array<(JawaString, JawaFieldAttr.set, JawaTypeRef)>,
		instanceMethods: Array<(JawaString, JawaMethodAttr.set, JawaSigRef, FuncDecl)>,
		staticFields: Array<(JawaString, JawaFieldAttr.set, JawaTypeRef)>,
		staticMethods: Array<(JawaString, JawaMethodAttr.set, JawaSigRef, FuncDecl)>) {
		var cmd = asm.reset()
			.putc(JawaCommandOpcode.DEF_CLASS.code)
			.put_abstype(it);

		cmd.put_ji4(instanceFields.length);
		for (f in instanceFields) cmd.put_field_decl(f);

		cmd.put_ji4(instanceMethods.length);
		for (m in instanceMethods) cmd.put_method_def(m);

		cmd.put_ji4(staticFields.length);
		for (f in staticFields) cmd.put_field_decl(f);

		cmd.put_ji4(staticMethods.length);
		for (m in staticMethods) cmd.put_method_def(m);

		module.addImport("jawa", cmd.extract(), cmd.args.extract(), ImportCommandDecl.new());
	}
	def defineInterface(it: AbsTypeDecl,
		instanceMethods: Array<(JawaString, JawaMethodAttr.set, JawaSigRef)>) {
		var cmd = asm.reset()
			.putc(JawaCommandOpcode.DEF_INTERFACE.code)
			.put_abstype(it);

		cmd.put_ji4(instanceMethods.length);
		for (m in instanceMethods) cmd.put_method_decl(m);

		module.addImport("jawa", cmd.extract(), cmd.args.extract(), ImportCommandDecl.new());
	}

	// Runs the import processor and produces the imports.
	def process() -> Array<Exportable> {
		var bindings = Array<Exportable>.new(module.imports.length);
		var i = Instantiator.new(Extension.set.all, module, bindings, err);
		i.processors.put(JawaImportProcessor.new(env, rt, err));
		i.run();
		if (err.error()) {
			t.fail(Strings.format1("Jawa import processing failed: %s", err.error_msg));
			return null;
		}
		// check bindings
		for (i < reftypes.length) {
			var entry = reftypes[i], jt = entry.0, index = entry.1.imp.import_index, b = bindings[index];
			b = if(ExportedValueType.?(b), ExportedValueType.!(b).src, b);
			if (jt == b) continue;
			t.fail3("import[%d]: expected jawa class %q, got %q", index, safeRender(jt), safeRender(b));
		}
		return bindings;
	}
	// Runs the import processor and produces the imports.
	def instantiate() -> Instance {
		var bindings = Array<Exportable>.new(module.imports.length);
		var i = Instantiator.new(Extension.set.all, module, bindings, err);
		i.processors.put(JawaImportProcessor.new(env, rt, err));
		var instance = i.run();
		if (err.error()) {
			t.fail(Strings.format1("Jawa import processing failed: %s", err.error_msg));
		}
		return instance;
	}
	def assertProcessFails() {
		var bindings = Array<Exportable>.new(module.imports.length);
		var i = Instantiator.new(Extension.set.all, module, bindings, err);
		i.processors.put(JawaImportProcessor.new(env, rt, err));
		i.run();
		if (!err.error()) t.fail("expected jawa import processing to fail, but passed");
	}
	def buildIVT(ct: JawaClassType) -> bool {
		var err = ErrorGen.new("");
		ct.finish(err);
		if (err.error()) t.fail1("got IVT building error: %s", err.error_msg);
		return err.ok();
	}
	def assertBuildIVTFails(ct: JawaClassType) {
		var err = ErrorGen.new("");
		ct.finish(err);
		if (err.ok()) t.fail("expected IVT building error, but passed");
	}
	def assertVTable(expected: Array<JawaMethod>, ct: JawaClassType) {
		if (ct.vtable == null) buildIVT(ct);
		var got = ct.vtable;
		if (got == null) {
			return t.fail1("expected vtable of length %d, got null", expected.length);
		}
		if (got.length != expected.length) {
			return t.fail2("expected vtable of length %d, got %d", expected.length, got.length);
		}
		for (i < expected.length) {
			var em = expected[i];
			if (em != got[i]) {
				return t.fail3("expected vtable[%d] == \"%q\", got \"%q\"", i, em.name.render, got[i].name.render);
			}
			if (em.vtable_index != i) {
				return t.fail3("expected \"%q\".vtable_index == %d, got %d", em.name.render, i, em.vtable_index);
			}
		}
	}
	def assertITable(expected: (JawaInterfaceType, Array<int>), ct: JawaClassType) {
		if (ct.itable == null) buildIVT(ct);
		var im = expected.0.instanceMethods;
		for (i < im.length) {
			var j = im[i].itable_index;
			if (j != i) return t.fail2("expected itable index == %d, got %d", i, j);
		}
		var got: (JawaInterfaceType, Array<int>);
		for (l = ct.itable; l != null; l = l.tail) {
			got = ct.itable.head;
			if (got.0 == expected.0) break;
		}
		if (got.0 == null) {
			return t.fail1("could not find itable for %q", expected.0.render);
		}
		t.asserta("itable", expected.1, got.1, StringBuilder.putd<int>);
	}
	def assertOverride(expected: JawaOverride, m: JawaMethod, pm: JawaMethod) {
		var got = m.override(pm);
		if (expected != got) t.fail2("expected override = %s, got %s", expected.name, got.name);
	}

	// Assertion helpers
	def assertParent(exp: JawaClassType, ct: JawaClassType) {
		if (ct.parent != exp) {
			t.fail(Strings.format2("expected parent=%q, got %q", safeRender(exp), safeRender(ct.parent)));
		}
	}
	def assertIsJawaClassType(exp: JawaClassType, exported: Exportable) -> JawaClassType {
		var e = if(ExportedValueType.?(exported), ExportedValueType.!(exported).src, exported);
		if (exp == null && JawaClassType.?(e)) return JawaClassType.!(e);
		if (exp == e) return exp;
		t.fail(Strings.format2("expected jawa class %q, got %q", safeRender(exp), safeRender(exported)));
		return null;
	}
	def assertIsJawaClassTypeWithName(name: JawaString, exported: Exportable) -> JawaClassType {
		var e = if(ExportedValueType.?(exported), ExportedValueType.!(exported).src, exported);
		if (JawaClassType.?(e)) {
			var ct = JawaClassType.!(e);
			if (!name.equals(ct.name)) {
				t.fail(Strings.format2("expected jawa class with name %q, got %q", name.render, ct.name.render));
				return null;
			}
			return ct;
		}
		t.fail(Strings.format2("expected jawa class %q, got %q", name.render, safeRender(exported)));
		return null;
	}
	def assertIsJawaInterfaceType(exp: JawaInterfaceType, exported: Exportable) -> JawaInterfaceType {
		var e = if(ExportedValueType.?(exported), ExportedValueType.!(exported).src, exported);
		if (exp == null && JawaInterfaceType.?(e)) return JawaInterfaceType.!(e);
		if (exp == e) return exp;
		t.fail(Strings.format2("expected jawa interface %q, got %q", safeRender(exp), safeRender(exported)));
		return null;
	}
	def assertIsJawaInterfaceTypeWithName(name: JawaString, exported: Exportable) -> JawaInterfaceType {
		var e = if(ExportedValueType.?(exported), ExportedValueType.!(exported).src, exported);
		if (JawaInterfaceType.?(e)) {
			var ct = JawaInterfaceType.!(e);
			if (!name.equals(ct.name)) {
				t.fail(Strings.format2("expected jawa interface with name %q, got %q", name.render, ct.name.render));
				return null;
			}
			return ct;
		}
		t.fail(Strings.format2("expected jawa interface %q, got %q", name.render, safeRender(exported)));
		return null;
	}
	def assertIsJawaArrayType(elem: JawaType, exported: Exportable) -> JawaArrayType {
		var e = if(ExportedValueType.?(exported), ExportedValueType.!(exported).src, exported);
		if (elem == null && JawaArrayType.?(e)) return JawaArrayType.!(e);
		match (e) {
			r: JawaArrayType => if (r.elem == elem) return r;
			_ => ;
		}
		t.fail(Strings.format2("expected jawa array of %q, got %q", safeRender(elem), safeRender(exported)));
		return null;
	}
	def assertIsJawaObject(ct: JawaRefType, r: Result) -> Value {
		var v = assert_is_v(r);
		var jt = rt.getJawaType(v);
		if (jt != ct) {
			t.fail(Strings.format2("expected jawa object of type %q, got %q", safeRender(ct), v.render));
		}
		return v;
	}
	def assertIsFunction(expected_sig: SigDecl, e: Exportable) -> Function {
		if (!Function.?(e)) {
			t.fail(Strings.format1("expected Function, got %q", safeRender(e)));
			return null;
		}
		var f = Function.!(e);
		if (!f.sig.isAssignableSig(expected_sig)) {
			t.fail(Strings.format2("expected Function of type %q, got %q", expected_sig.render, safeRender(f.sig)));
			return null;
		}
		return f;
	}
	def assertIsJawaField(fields: Array<JawaField>, index: int, name: JawaString, typ: JawaType) -> JawaField {
		if (fields == null) {
			t.fail("expected non-null fields");
			return null;
		}
		if (index >= fields.length) {
			t.fail2("expected at least %d fields, got %d", index+1, fields.length);
			return null;
		}
		var f = fields[index];
		if (f == null) {
			t.fail1("expected field name \"%q\", got null", name.render);
			return null;
		}
		if (!name.equals(f.name)) {
			t.fail2("expected field name \"%q\", got \"%q\"", name.render, f.name.render);
			return null;
		}
		if (typ != f.typ) {
			t.fail2("expected field type \"%q\", got \"%q\"", typ.render, f.typ.render);
			return null;
		}
		return f;
	}
	def assertIsJawaMethod(methods: Array<JawaMethod>, index: int, name: JawaString, params: Array<JawaType>, ret: JawaType) -> JawaMethod {
		if (methods == null) {
			t.fail("expected non-null methods");
			return null;
		}
		if (index >= methods.length) {
			t.fail2("expected at least %d methods, got %d", index+1, methods.length);
			return null;
		}
		var m = methods[index];
		if (m == null) {
			t.fail1("expected method name \"%q\", got null", name.render);
			return null;
		}
		if (!name.equals(m.name)) {
			t.fail2("expected method name \"%q\", got \"%q\"", name.render, m.name.render);
			return null;
		}
		if (ret != m.sig.ret) {
			t.fail2("expected method ret \"%q\", got \"%q\"", ret.render, m.sig.ret.render);
			return null;
		}
		t.assertar("param", params, m.sig.params, JawaType.render);
		return m;
	}
	def assertIsGlobal(expected_type: ValueType, e: Exportable) -> Value {
		if (!Global.?(e)) {
			t.fail(Strings.format1("expected global, got %q", safeRender(e)));
			return Values.I32_0;
		}
		var g = Global.!(e);
		if (!ValueTypes.isAssignable(g.valtype, expected_type)) {
			t.fail(Strings.format2("expected global of type %q, got %q", expected_type.render, g.valtype.render));
			return Values.I32_0;
		}
		return g.value;
	}
	def assert_r(expected: Result, got: Result) {
		if (expected.equals(got)) return;
		t.fail2("expected %q, got %q", expected.render, got.render);
	}
	def assert_i(expected: int, got: Result) {
		assert_r(Result.Value([Value.I32(u32.view(expected))]), got);
	}
	def assert_v(expected: Value, got: Result) {
		assert_r(Result.Value([expected]), got);
	}
	def assert_is_v(got: Result) -> Value {
		match (got) {
			Value(vals) => if (vals.length == 1) return vals[0];
			_ => ;
		}
		t.fail1("expected value, got %q", got.render);
		return Values.I32_0;
	}
	def assert_a<T>(expected: Array<T>, got: Value, map: JawaPrimMappingOf<T>) {
		for (i < expected.length) {
			if (map.box(expected[i]) != rt.getElem(got, i)) return t.fail(Strings.format1("mismatch at array[%d]", i));
		}
	}
	def assert_av(expected: Array<Value>, av: Value) {
		for (i < expected.length) {
			assert_v(expected[i], Result.Value([rt.getElem(av, i)]));
		}
	}
	def safeRender<T>(t: T) -> StringBuilder -> StringBuilder {
		var d: T;
		if (t == d) return Strings.puts("<null>");
		if (Exportable.?(t)) return Exportable.!(t).render;
		if (SigDecl.?(t)) return SigDecl.!(t).render;
		if (HostObject.?(t)) return HostObject.!(t).render;
		return Strings.puts("<?>");
	}
	def strCodeType(code: byte, it: AbsTypeDecl) -> JawaAsm {
		return asm.reset()
			.putc(code)
			.put_abstype(it);
	}
	def strCodeTypeName(code: byte, it: AbsTypeDecl, name: string) -> JawaAsm {
		return asm.reset()
			.putc(code)
			.put_abstype(it)
			.put_jname(JawaStrings.of(name));
	}
	def strCodeJawaName(code: byte, jname: JawaString) -> JawaAsm {
		return asm.reset()
			.putc(code)
			.put_jname(jname);
	}
	def strCodeTypeJawaName(code: byte, it: AbsTypeDecl, jname: JawaString) -> JawaAsm {
		return asm.reset()
			.putc(code)
			.put_abstype(it)
			.put_jname(jname);
	}
	def strCodeTypeJawaNameSig(code: byte, it: AbsTypeDecl, jname: JawaString, jtsig: JawaSigRef) -> JawaAsm {
		return asm.reset()
			.putc(code)
			.put_abstype(it)
			.put_jname(jname)
			.put_jtsig(jtsig);
	}
	def strCode(code: byte) -> JawaAsm {
		return asm.reset().putc(code);
	}
	def strCodeString(code: byte, jstr: JawaString) -> JawaAsm {
		return asm.reset().putc(code).put_jstr(jstr);
	}

	// Helpers for specific, complex bytecodes.
	def make_xaload(c: JawaPrimArrayOpcodes) -> Function {
		var n = dup();
		var bt = n.importPrimArray(c);
		var expected_sig = newSig([toWasmType(c.arrayType), ValueType.I32], [c.map.valueType]);
		n.addFuncImport(strCode(c.xaload.code), expected_sig);
		var r = n.process();
		if (r == null) return null;
		n.assertIsJawaArrayType(c.arrayType.elem, r[0]);
		return assertIsFunction(expected_sig, r[1]);
	}
	def make_xastore(c: JawaPrimArrayOpcodes) -> Function {
		var n = dup();
		var bt = n.importPrimArray(c);
		var expected_sig = newSig([toWasmType(c.arrayType), ValueType.I32, c.map.valueType], SigCache.arr_v);
		n.addFuncImport(strCode(c.xastore.code), expected_sig);
		var r = n.process();
		if (r == null) return null;
		n.assertIsJawaArrayType(c.arrayType.elem, r[0]);
		return assertIsFunction(expected_sig, r[1]);
	}
	def make_xnewarray(c: JawaPrimArrayOpcodes) -> Function {
		var n = dup();
		var bt = n.importPrimArray(c);
		var sig = newSig(SigCache.arr_i, [toWasmType(c.arrayType)]);
		var asm = n.asm.reset()
			.putc(JawaFuncOpcode.NEWARRAY.code)
			.put_abstype(bt);
		n.addFuncImport(asm, sig);
		var r = n.process();
		if (r == null) return null;
		n.assertIsJawaArrayType(c.arrayType.elem, r[0]);
		return assertIsFunction(sig, r[1]);
	}
	def make_xarraylength(c: JawaPrimArrayOpcodes) -> Function {
		var n = dup();
		var bt = n.importPrimArray(c);
		var sig = newSig([toWasmType(c.arrayType)], SigCache.arr_i);
		var asm = n.asm.reset()
			.putc(JawaFuncOpcode.ARRAYLENGTH.code)
			.put_abstype(bt);
		n.addFuncImport(asm, sig);
		var r = n.process();
		if (r == null) return null;
		n.assertIsJawaArrayType(c.arrayType.elem, r[0]);
		return assertIsFunction(sig, r[1]);
	}
	def make_multianewarray(it: AbsTypeDecl, dims: int) -> (JawaArrayType, Function) {
		var param_types = Array<ValueType>.new(dims);
		for (i < dims) param_types[i] = ValueType.I32;

		var sig = newSig(param_types, [ValueType.Abstract(it)]);
		asm.reset()
			.putc(JawaFuncOpcode.MULTIANEWARRAY.code)
			.put_ji4(dims)
			.put_abstype(it);
		var fi = addFuncImport(asm, sig);
		var r = process();
		if (r == null) return (null, null);

		var xt = r[it.imp.import_index];
		var at = assertIsJawaArrayType(null, xt);
		var expected_sig = newSig(param_types, [toWasmType(at)]);
		return (at, assertIsFunction(expected_sig, r[fi.imp.import_index]));
	}
	def sig_xx_i(x: ValueType) -> SigDecl {
		return newSig([x, x], SigCache.arr_i);
	}
	def sig_x_i(x: ValueType) -> SigDecl {
		return newSig([x], SigCache.arr_i);
	}
	def sig_x_v(x: ValueType) -> SigDecl {
		return newSig([x], SigCache.arr_v);
	}
	def sig_xi_y(x: ValueType, y: ValueType) -> SigDecl {
		return newSig([x, ValueType.I32], [y]);
	}
	def sig_xiy_v(x: ValueType, y: ValueType) -> SigDecl {
		return newSig([x, ValueType.I32, y], SigCache.arr_v);
	}
	def toWasmType(t: JawaType) -> ValueType {
		return rt.toWasmType(t);
	}
}

// A helper to emit the import language.
class JawaAsm extends StringBuilder {
	def args = Vector<Decl>.new();

	def put_jname(j: JawaString) -> this {
		put_ji2(j.chars.length);
		for (c in j.chars) pututf8(c);
	}
	def put_jstr(j: JawaString) -> this {
		put_ji4(j.chars.length);
		for (c in j.chars) pututf8(c);
	}
	def put_ji2(v: int) -> this {
		for (j < 2) {
			putc(u7.view(v));
			v = v >> 7;
		}
	}
	def put_ji4(v: int) -> this {
		for (j < 4) {
			putc(u7.view(v));
			v = v >> 7;
		}
	}
	def put_abstype(t: AbsTypeDecl) -> this {
		args.put(t);
	}
	def put_field_decl(name: JawaString, attrs: JawaFieldAttr.set, t: JawaTypeRef) -> this {
		put_jname(name);
		put_field_attrs(attrs);
		put_type_ref(t);
	}
	def put_method_decl(name: JawaString, attrs: JawaMethodAttr.set, sig: JawaSigRef) -> this {
		put_jname(name);
		put_method_attrs(attrs);
		put_jtsig(sig);
	}
	def put_method_def(name: JawaString, attrs: JawaMethodAttr.set, sig: JawaSigRef, func: FuncDecl) -> this {
		put_jname(name);
		put_method_attrs(attrs);
		put_jtsig(sig);
		args.put(func);
	}
	def put_type_ref(t: JawaTypeRef) -> this {
		match (t) {
			Void => putc('V');
			Prim(t) => putc(t.char);
			Abstract(t) => {
				putc('L');
				args.put(t);
			}
		}
	}
	def put_jtsig(sig: JawaSigRef) -> this {
		put_ji2(sig.params.length);
		for (p in sig.params) put_type_ref(p);
		put_type_ref(sig.result);
	}
	def put_class_attrs(attrs: JawaClassAttr.set) -> this {
		var v: u16;
		for (a in attrs) v |= a.code;
		put_ji2(v);
	}
	def put_method_attrs(attrs: JawaMethodAttr.set) -> this {
		var v: u16;
		for (a in attrs) v |= a.code;
		put_ji2(v);
	}
	def put_field_attrs(attrs: JawaFieldAttr.set) -> this {
		var v: u16;
		for (a in attrs) v |= a.code;
		put_ji2(v);
	}
}
type JawaTypeRef {
	case Void;
	case Prim(t: JawaPrimType);
	case Abstract(t: AbsTypeDecl);
	def putInto(v: Vector<ValueType>) -> Vector<ValueType> {
		match (this) {
			Void => ;
			Prim(t) => v.put(t.prim.valueType);
			Abstract(it) => v.put(ValueType.Abstract(it));
		}
		return v;
	}
}
class JawaSigRef(params: Array<JawaTypeRef>, result: JawaTypeRef) {
}

enum JawaPrimArrayOpcodes(type_code: JawaTypeOpcode, xaload: JawaFuncOpcode, xastore: JawaFuncOpcode, arrayType: JawaArrayType, map: JawaPrimMapping) {
	BYTE(JawaTypeOpcode.BYTE_ARRAY, JawaFuncOpcode.BALOAD, JawaFuncOpcode.BASTORE, JawaTypes.BYTE_ARRAY, JawaTypes.BYTE_MAPPING),
	BOOL(JawaTypeOpcode.BOOL_ARRAY, JawaFuncOpcode.ZALOAD, JawaFuncOpcode.ZASTORE, JawaTypes.BOOL_ARRAY, JawaTypes.BOOL_MAPPING),
	CHAR(JawaTypeOpcode.CHAR_ARRAY, JawaFuncOpcode.CALOAD, JawaFuncOpcode.CASTORE, JawaTypes.CHAR_ARRAY, JawaTypes.CHAR_MAPPING),
	SHORT(JawaTypeOpcode.SHORT_ARRAY, JawaFuncOpcode.SALOAD, JawaFuncOpcode.SASTORE, JawaTypes.SHORT_ARRAY, JawaTypes.SHORT_MAPPING),
	INT(JawaTypeOpcode.INT_ARRAY, JawaFuncOpcode.IALOAD, JawaFuncOpcode.IASTORE, JawaTypes.INT_ARRAY, JawaTypes.INT_MAPPING),
	LONG(JawaTypeOpcode.LONG_ARRAY, JawaFuncOpcode.LALOAD, JawaFuncOpcode.LASTORE, JawaTypes.LONG_ARRAY, JawaTypes.LONG_MAPPING),
	FLOAT(JawaTypeOpcode.FLOAT_ARRAY, JawaFuncOpcode.FALOAD, JawaFuncOpcode.FASTORE, JawaTypes.FLOAT_ARRAY, JawaTypes.FLOAT_MAPPING),
	DOUBLE(JawaTypeOpcode.DOUBLE_ARRAY, JawaFuncOpcode.DALOAD, JawaFuncOpcode.DASTORE, JawaTypes.DOUBLE_ARRAY, JawaTypes.DOUBLE_MAPPING)
}

class JawaClassBuilder(t: JawaTester, name: string, attrs: JawaClassAttr.set, parent: AbsTypeDecl, interfaces: Array<AbsTypeDecl>) {
	private def instanceFields = Vector<(JawaString, JawaFieldAttr.set, JawaTypeRef)>.new();
	private def instanceMethods = Vector<(JawaString, JawaMethodAttr.set, JawaSigRef, FuncDecl)>.new();
	private def staticFields = Vector<(JawaString, JawaFieldAttr.set, JawaTypeRef)>.new();
	private def staticMethods = Vector<(JawaString, JawaMethodAttr.set, JawaSigRef, FuncDecl)>.new();
	def it = t.declareClass(name, attrs, parent, interfaces);
	def at = ValueType.Abstract(it);
	def arr_at: Array<ValueType> = [at];
	private var defined = false;

	def addInstanceField(name: string, t: JawaTypeRef) -> this {
		checkNotDefined();
		var attrs: JawaFieldAttr.set;
		instanceFields.put(JawaStrings.of(name), attrs, t);
		checkFieldType(t);
	}
	def addInstanceMethod(name: string, pt: Array<JawaTypeRef>, rt: JawaTypeRef, impl: FuncDecl) -> this {
		checkNotDefined();
		var attrs: JawaMethodAttr.set;
		instanceMethods.put(JawaStrings.of(name), attrs, JawaSigRef.new(pt, rt), impl);
	}
	def addStaticField(name: string, t: JawaTypeRef) -> this {
		checkNotDefined();
		var attrs: JawaFieldAttr.set;
		staticFields.put(JawaStrings.of(name), attrs, t);
		checkFieldType(t);
	}
	def addStaticMethod(name: string, pt: Array<JawaTypeRef>, rt: JawaTypeRef, impl: FuncDecl) -> this {
		checkNotDefined();
		var attrs: JawaMethodAttr.set;
		staticMethods.put(JawaStrings.of(name), attrs, JawaSigRef.new(pt, rt), impl);
	}
	def define() {
		checkNotDefined();
		defined = true;
		t.defineClass(it,
			instanceFields.copy(),
			instanceMethods.copy(),
			staticFields.copy(),
			staticMethods.copy()
		);
	}

	def NEW() -> FuncDecl {
		checkDefined();
		var asm = t.strCodeType(JawaFuncOpcode.NEW.code, it);
		return t.addFuncImport(asm, newSig(SigCache.arr_v, arr_at));
	}
	def GETFIELD(name: string) -> FuncDecl {
		checkDefined();
		var asm = t.strCodeTypeName(JawaFuncOpcode.GETFIELD.code, it, name);
		var vt = getFieldType(instanceFields, name);
		return t.addFuncImport(asm, newSig(arr_at, [vt]));
	}
	def GETSTATIC(name: string) -> FuncDecl {
		checkDefined();
		var asm = t.strCodeTypeName(JawaFuncOpcode.GETSTATIC.code, it, name);
		var vt = getFieldType(staticFields, name);
		return t.addFuncImport(asm, newSig(SigCache.arr_v, [vt]));
	}
	def PUTFIELD(name: string) -> FuncDecl {
		checkDefined();
		var asm = t.strCodeTypeName(JawaFuncOpcode.PUTFIELD.code, it, name);
		var vt = getFieldType(instanceFields, name);
		return t.addFuncImport(asm, newSig([at, vt], SigCache.arr_v));
	}
	def PUTSTATIC(name: string) -> FuncDecl {
		checkDefined();
		var asm = t.strCodeTypeName(JawaFuncOpcode.PUTSTATIC.code, it, name);
		var vt = getFieldType(staticFields, name);
		return t.addFuncImport(asm, newSig([vt], SigCache.arr_v));
	}
	def INVOKESTATIC(name: string) -> FuncDecl {
		checkDefined();
		var sig = getMethodSig(false, name);
		var asm = t.strCodeTypeJawaNameSig(JawaFuncOpcode.INVOKESTATIC.code, it, JawaStrings.of(name), sig.0);
		return t.addFuncImport(asm, sig.1);
	}
	def INVOKESPECIAL(name: string) -> FuncDecl {
		checkDefined();
		var sig = getMethodSig(true, name);
		var asm = t.strCodeTypeJawaNameSig(JawaFuncOpcode.INVOKESPECIAL.code, it, JawaStrings.of(name), sig.0);
		return t.addFuncImport(asm, sig.1);
	}
	def INVOKEVIRTUAL(name: string) -> FuncDecl {
		checkDefined();
		var sig = getMethodSig(true, name);
		var asm = t.strCodeTypeJawaNameSig(JawaFuncOpcode.INVOKEVIRTUAL.code, it, JawaStrings.of(name), sig.0);
		return t.addFuncImport(asm, sig.1);
	}

	def getFieldType(v: Vector<(JawaString, JawaFieldAttr.set, JawaTypeRef)>, name: string) -> ValueType {
		var jname = JawaStrings.of(name);
		for (i < v.length) {
			var t = v[i];
			if (jname.equals(t.0)) return t.2.putInto(Vector.new())[0];
		}
		System.error("InternalTestError", Strings.format1("field %s not found", name));
		return ValueType.I32;
	}

	def getMethodSig(instance: bool, name: string) -> (JawaSigRef, SigDecl) {
		var v = if(instance, instanceMethods, staticMethods);
		var jname = JawaStrings.of(name);
		for (i < v.length) {
			var t = v[i];
			if (jname.equals(t.0)) {
				var params = Vector<ValueType>.new();
				var results = Vector<ValueType>.new();
				if (instance) params.put(at);
				for (p in t.2.params) p.putInto(params);
				t.2.result.putInto(results);
				return (t.2, newSig(params.extract(), results.extract()));
			}
		}
		System.error("InternalTestError", Strings.format1("method %s not found", name));
		return (null, SigCache.v_v);
	}

	def checkNotDefined() {
		if (defined) System.error("InternalTestError", "class is already defined");
	}
	def checkDefined() {
		if (!defined) System.error("InternalTestError", "class is not yet defined");
	}
	def checkFieldType(t: JawaTypeRef) {
		if (t == JawaTypeRef.Void) System.error("InternalTestError", "jawa field cannot be void type");
	}
}
