// Copyright 2023 Wizard authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def T = UnitTests.registerT("wasi:", _, WasiTester.new, _);
def X_ = void(
	T("normalize_path0", test_normalize_path0),
	T("normalize_path1", test_normalize_path1),
	T("resolve_path0", test_resolve_path0)
);

class WasiTester(t: Tester) {
	def assert_normalize(expected: string, input: string) {
		var result = WasiPathUtil.normalize_path(input);
		t.assert_string(expected, result);
	}

	def assert_resolve(expected: string, fdPath: string, input: string) {
		var fd = WasiFd.new(4, wasi_filetype.CHARACTER_DEVICE, false, fdPath);
		var result = WasiPathUtil.resolve_path(fd, input, 0);
		t.assert_string(expected, result.0);
		t.assert(0 == result.1, "Error Number Incorrect");
	}
}

def test_normalize_path0(t: WasiTester) {
	def ok = t.assert_normalize;
	ok(".", "");
	ok(".", "./");
	ok("abc", "./abc/");
}

def test_normalize_path1(t: WasiTester) {
	def ok = t.assert_normalize;
	ok(".", "././");
	ok("abc/sa", "./abc/sa");
	ok("aaa/ac", "./abc/../aaa/ac");
	ok(".", "./.");
	ok("..", "./..");
	ok("..", "./../");
	ok("..", "..");
	ok("..", "../");
	ok("..", "../.");
	ok("../..", "../..");
	ok("/", "/");
	ok("foo", "./foo");
	ok("bar", "./foo/../bar");
	ok("foo/..bar", "./foo/..bar");
	ok("/foo/bar", "/foo/bar");
	ok("/foo/bar", "/foo/baz/../bar");
	ok("/bar", "/foo/../bar");
	ok("/bar", "/../bar");
	ok("/bar", "/../../../bar");
	ok("/bar", "/../../../bar/");
	ok("/", "/../../../");
	ok("/", "////..//../..///");
	ok("foo", "./foo//");
	ok("foo/bar", "./foo/////bar");
	ok("/", "//");
	ok("..", "..//");
	ok(".", ".//");
	ok("..", "./foo/bar/baz/../../../..");
	ok("..", "./foo/bar/baz/../../../../");
	ok("../..", "./foo/bar/baz/../../../../..");
	ok("../..", "./foo/bar/baz/../../../../../");
	ok("../../../test_path", "../../../test_path");
	ok("test_path", "./././test_path");

}

def test_resolve_path0(t: WasiTester) {
	def ok = t.assert_resolve;
	ok("/foo/test_path", "/foo", "/foo/test_path");
	ok("/baz/test_path", "/baz", "../baz/./test_path/../test_path");
	ok("/foo/test_path", "/foo", "test_path");
	ok("/foo/test_path", "/foo", "./test_path");
	ok("/foo/test_path", "/foo", "bar/../test_path");
}
