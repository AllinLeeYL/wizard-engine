// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Main entrypoint to the jawa execution environment.
def main(args: Array<string>) -> int {
	var trace = false;
	var engine = Engine.new();
	var programArgs: Array<string>;
	var path: string;

	// Parse options and extract module and module arguments
	for (i < args.length) {
		var a = args[i];
		if (Strings.equals(a, "-trace-jawa")) {
			trace = true;
		} else if (a.length > 0 && a[0] == '-') {
			if (!engine.parseOption(a)) return ErrorBuilder.new()
				.puts("jawa: unrecognized option ")
				.putsq(a)
				.ln()
				.exit(1);
		} else {
			path = a;
			programArgs = Arrays.range(args, i+1, args.length);
			break;
		}
	}

	if (path == null) return ErrorBuilder.new()
		.puts("jawa: no input files\n")
		.exit(3);

	if (!Strings.endsWith(path, ".wasm")) return ErrorBuilder.new()
		.puts(path)
		.puts(": unknown file format, expected .wasm\n")
		.exit(4);

	// Parse the binary module
	var result = engine.loadWasmFile(path);
	var module: Module;
	match (result) {
		Ok(m) => module = m;
		FileNotFound => return ErrorBuilder.new()
			.puts("jawa: could not load file ")
			.puts(path)
			.puts("\n")
			.exit(2);
		ParseError(code, path, error_pos, error_msg) => return ErrorBuilder.new()
			.render(code, path, error_pos, error_msg)
			.exit(3);
	}

	// Provide imports to the module and instantiate
	var err = WasmErrorGen.new(path);
	var imports = Array<Exportable>.new(module.imports.length); // TODO
	var i = Instantiator.new(engine.extensions, module, imports);
	var instance = i.run();

	if (!i.err.ok()) return ErrorBuilder.new()
		.render(i.err.error_code, path, i.err.error_pos, i.err.error_msg)
		.exit(6);

	// Find the main exported function
	var main: WasmFunction; // TODO

	if (main == null) return ErrorBuilder.new()
		.puts(path)
		.puts(": no main export from module\n")
		.exit(7);

	// Provide the main arguments
	var exe = Interpreter.new();
	var exeArgs = Array<Value>.new(0); // TODO

	// Run the program
	var r = exe.run(-1, main, exeArgs);
	match (r) {
		Break => ;
		Value(vals) => return if(vals.length == 1, Values.v_i(vals[0]));
		Trap(reason) => {
			var e = ErrorBuilder.new()
				.puts("trap: ")
				.puts(reason.name)
				.ln();
			// TODO: provide a source-level stacktrace
			for (i = exe.call_stack.top - 1; i >= 0; i--) {
				var f = exe.call_stack.elems[i];
				e.puts("\t@ f");
				e.putd(f.func.decl.index);
				e.puts(" +");
				e.putd(f.pc);
				e.ln();
			}
			e.exit(10);
		}
	}
	return 0;
}
