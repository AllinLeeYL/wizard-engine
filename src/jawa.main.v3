// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Main entrypoint to the Jawa execution environment.
def main(args: Array<string>) -> int {
	var engine = Engine.new();
	var programArgs: Array<string>;
	var path: string;

	engine.extensions |= Jawa.ENGINE_EXTENSIONS;

	// Parse options and extract module and module arguments
	for (i < args.length) {
		var a = args[i];
		if (a.length > 0 && a[0] == '-') {
			if (!engine.parseOption(a)) return ErrorBuilder.new()
				.puts("jawa: unrecognized option ")
				.putsq(a)
				.ln()
				.exit(1);
		} else {
			path = a;
			programArgs = Arrays.range(args, i+1, args.length);
			break;
		}
	}

	if (path == null) return ErrorBuilder.new()
		.puts("jawa: no input files\n")
		.exit(3);

	if (!Strings.endsWith(path, ".wasm")) return ErrorBuilder.new()
		.puts(path)
		.puts(": unknown file format, expected .wasm\n")
		.exit(4);

	// Parse the binary module
	var result = engine.loadWasmFile(path);
	var module: Module;
	match (result) {
		Ok(m) => module = m;
		FileNotFound => return ErrorBuilder.new()
			.puts("jawa: could not load file ")
			.puts(path)
			.puts("\n")
			.exit(2);
		ParseError(code, path, error_pos, error_msg) => return ErrorBuilder.new()
			.render(code, path, error_pos, error_msg)
			.exit(3);
	}

	// Provide imports to the module and instantiate
	var err = ErrorGen.new(path);
	var env = JawaEnvironment.new(null);
	JawaLang.install(env);
	var i = Instantiator.new(engine.extensions, module, Array.new(module.imports.length), err);
	var jip = JawaImportProcessor.new(env, JawaHostRuntime.new(), err);
	i.processors.put(jip);
	var instance = i.run();

	if (!err.ok()) return ErrorBuilder.new()
		.render(err.error_code, path, err.error_pos, err.error_msg)
		.exit(6);

	// Find the main exported function
	var main = jip.extractMainExport(instance);

	if (main == null) return ErrorBuilder.new()
		.puts(path)
		.puts(": no main export from module\n")
		.exit(7);

	if (!checkMainSig(jip, main.sig)) return ErrorBuilder.new()
		.puts(path)
		.put1(": expected main export of type [jawa.lang.String[]] -> [], got %q\n", main.sig.render)
		.exit(8);

	// Provide the main arguments as an array of JawaStrings
	var elems = Array<JawaObject>.new(programArgs.length);
	var exeArgs = JawaRefArrayObject.new(JawaTypes.STRING.arrayOf(), elems);

	for (i < elems.length) {
		var a = programArgs[i];
		var t = Utf8.decodeUtf16(a, 0, a.length);
		if (t.0 == null) return ErrorBuilder.new()
			.puts("invalid UTF-8 argument: ")
			.putsq(a)
			.exit(8);
		elems[i] = JawaString.new(t.0).toObject();
	}

	// Run the program
	var exe = Target.newInterpreter();
	var r = exe.run(-1, main, [Value.Ref(exeArgs)]);
	match (r) {
		Break => ;
		Value(vals) => return if(vals.length == 1, Values.v_i(vals[0]));
		Trap(reason) => {
			var e = ErrorBuilder.new()
				.puts("trap: ")
				.puts(reason.name);
			if (exe.error_msg != null) e.puts(":").puts(exe.error_msg);
			for (i = exe.call_stack.top - 1; i >= 0; i--) {
				var f = exe.call_stack.elems[i];
				e.puts("\t@ ");
				f.func.render(e);
				e.puts(" +");
				e.putd(f.pc);
				e.ln();
			}
			e.exit(10);
		}
	}
	return 0;
}
def checkMainSig(jip: JawaImportProcessor, sig: SigDecl) -> bool {
	if (sig.params.length != 1) return false;
	if (sig.results.length != 0) return false;
	match (sig.params[0]) {
		Host(host) => match (host) {
			x: JawaArrayType => return jip.env.isStringType(x.elem);
		}
		_ => ;
	}
	return false;
}
