// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Implementation of the Jawa runtime using the GC extension to Wasm.
def CLASS_META_FIELD = 0;
def CLASS_HEADER_FIELDS = 1;
def META_STRUCT_HEADER_FIELDS = 2;
def ARRAY_STRUCT_ARRAY_FIELD = 1;
class JawaGcRuntime extends JawaRuntime {
	def arrayReps = HashMap<JawaArrayType, JawaGcRuntimeArrayRep>.new(JawaRefType.hash, JawaArrayType.==);
	def classReps = HashMap<JawaClassType, JawaGcRuntimeClassRep>.new(JawaRefType.hash, JawaClassType.==);
	def interfaceReps = HashMap<JawaInterfaceType, JawaGcRuntimeInterfaceRep>.new(JawaRefType.hash, JawaInterfaceType.==);
	def comodule = Module.new(null);
	def reftypes = Vector<JawaRefType>.new(); // parallel mapping of deftype <-> jawa type
	def coinstance = Instance.new(comodule, []);
	def validator = CodeValidator.new(Jawa.ENGINE_EXTENSIONS, Jawa.ENGINE_LIMITS, comodule, WasmErrorGen.new(null));
	var iTableStruct: StructDecl;
	def trace = if(Trace.jawa, TraceBuilder.new());

	var asm = WasmGcAsm.new();

	new() super(JawaGcStringLens.new(), JawaGcRefTypeClassObjectLens.new()) { }

	def toWasmType(t: JawaType) -> ValueType {
		match (t) {
			x: JawaPrimType => return x.prim.valueType;
			x: JawaArrayType => return makeArrayRep(x).valueType;
			x: JawaClassType => return makeClassRep(x).valueType;
			x: JawaInterfaceType => return makeInterfaceRep(x).valueType;
			_ => return ValueType.Host(t); // XXX: that's an error
		}
	}
	def makeArrayRep(at: JawaArrayType) -> JawaGcRuntimeArrayRep {
		var gcrep = arrayReps[at];
		if (gcrep != null) return gcrep;

		// Build structs before recursing on field/method types
		var parent = JawaLang.OBJECT;
		var pair = newStructs(at, 1, parent.vtable.length),
			struct = pair.1, metaStruct = pair.0;

		var array = ArrayDecl.new(Array.new(1));
		comodule.addDecl(array);
		arrayReps[at] = gcrep = JawaGcRuntimeArrayRep.new(metaStruct, struct, array);

		// Build rtt
		var rtt = newRtt(parent, struct);
		gcrep.rtt = rtt.1;
		gcrep.rttGlobal = addGlobal(rtt.0, false, rtt.1);
		var artt = newRtt(null, array);
		gcrep.arrayRtt = artt.1;
		gcrep.elemsRttGlobal = addGlobal(artt.0, false, artt.1);

		// Finish meta struct
		finishMetaStruct(parent, metaStruct);

		// Finish struct fields
		var field_types = struct.field_types;
		var meta_field = StorageType.new(ValueType.RefStruct(false, metaStruct), Packedness.UNPACKED, false);
		var j = 0;
		field_types[j++] = meta_field;
		field_types[j++] = StorageType.new(ValueType.RefArray(false, array), Packedness.UNPACKED, false);

		array.elem_types[0] = toStorageType(at.elem, true);
		return gcrep;
	}
	def makeClassRep(ct: JawaClassType) -> JawaGcRuntimeClassRep {
		var gcrep = classReps[ct];
		if (gcrep != null) return gcrep;
		if (trace != null) trace.put1("JawaGcRuntime.makeClassRep(%q)", ct.render).outln();

		// Build struct before recursing on field/method types
		var pair = newStructs(ct,
			if(ct.instanceFields != null, ct.instanceFields.length),
			if(ct.vtable != null, ct.vtable.length)), struct = pair.1, metaStruct = pair.0;

		classReps[ct] = gcrep = JawaGcRuntimeClassRep.new(metaStruct, struct);

		// Build rtt
		var rtt = newRtt(ct.parent, struct);
		gcrep.rtt = rtt.1;
		gcrep.rttGlobal = addGlobal(rtt.0, false, rtt.1);

		// Finish meta struct
		finishMetaStruct(ct, metaStruct);

		// Build static fields
		var staticFields = ct.staticFields;
		if (staticFields != null && staticFields.length > 0) {
			gcrep.staticFields = Array.new(staticFields.length);
			for (i < staticFields.length) {
				var f = staticFields[i], v = f.defaultValue();
				gcrep.staticFields[i] = addGlobal(toWasmType(f.typ), true, v);
			}
		}

		// Finish field types of struct (recursively)
		var field_types = struct.field_types;
		var meta_field = StorageType.new(ValueType.RefStruct(false, metaStruct), Packedness.UNPACKED, false);
		var j = 0;
		field_types[j++] = meta_field;
		if (ct.instanceFields != null) {
			for (f in ct.instanceFields) {
				field_types[j++] = toStorageType(f.typ, true);
			}
		}
		return gcrep;
	}
	def finishMetaStruct(ct: JawaClassType, metaStruct: StructDecl) {
		var jlcrep = makeClassRep(JawaLang.CLASS);
		var meta_field_types = metaStruct.field_types;
		meta_field_types[0] = StorageType.new(jlcrep.valueType, Packedness.UNPACKED, false);
		meta_field_types[1] = StorageType.new(ValueType.RefStruct(true, makeITableStruct()), Packedness.UNPACKED, false);
		if (ct.vtable == null) return;
		for (i < ct.vtable.length) {
			var m = ct.vtable[i];
			var receiver = ct;
			if (trace != null) trace.put2("  vtable[%d] = %q", i, m.render).outln();
			while (true) {  // find the highest parent that defined this method
				var pt = receiver.parent;
				if (pt == null || pt.vtable.length < i) break;
				receiver = pt;
			}
			var sig = m.sig.sig(receiver, toWasmType);
			meta_field_types[META_STRUCT_HEADER_FIELDS + i] = StorageType.new(
				ValueType.RefFunc(false, sig), Packedness.UNPACKED, false);
		}
	}
	def newStructs(rt: JawaRefType, num_fields: int, num_meta_fields: int) -> (StructDecl, StructDecl) {
		// Build struct before recursing on field/method types
		var field_types = Array<StorageType>.new(CLASS_HEADER_FIELDS + num_fields);
		var struct = newStruct(rt, field_types);

		// Build meta struct
		var meta_field_types = Array<StorageType>.new(META_STRUCT_HEADER_FIELDS + num_meta_fields);
		var metaStruct = newStruct(null, meta_field_types);

		return (metaStruct, struct);
	}
	def newRtt(parent: JawaClassType, decl: HeapTypeDecl) -> (ValueType.Rtt, Value.Rtt) {
		if (parent == null) {
			var rtt = Value.Rtt(decl, Array<Value.Rtt>.new(1));
			rtt.supers[0] = rtt;
			return (ValueType.Rtt(0, decl), rtt);
		} else {
			var prep = makeClassRep(parent);
			var rtt = Values.subRtt(prep.rtt, decl);
			return (ValueType.Rtt(rtt.supers.length, decl), rtt);
		}
	}
	def makeMetaObject(gcrep: JawaGcRuntimeHeapRep, vtable: Array<JawaMethod>,
			itable: List<(JawaInterfaceType, Array<int>)>) -> Value {
		if (gcrep.metaObject.val != null) return gcrep.metaObject;

		var jlcrep = makeClassRep(JawaLang.CLASS);
		var vals = Array<Value>.new(gcrep.metaStruct.field_types.length);
		gcrep.metaObject = Value.Ref(HeapStruct.new(gcrep.metaStruct, jlcrep.rtt, vals));
		var jlcobj = makeMetaObject(jlcrep, JawaLang.CLASS.vtable, JawaLang.CLASS.itable);
		vals[0] = jlcobj;  // all meta-objects have jlc as their metaobject, including itself
		if (vtable != null) for (i < vtable.length) {
			var m = vtable[i];
			vals[META_STRUCT_HEADER_FIELDS + i] = Value.FuncRef(m.impl);
		}
		// Build a list of interface dispatch tables
		var itable_val: Value.Ref;
		for (l = itable; l != null; l = l.tail) {
			var irep = makeInterfaceRep(l.head.0);
			var vals = Array<Value>.new(irep.itableStruct.field_types.length);
			var istruct = HeapStruct.new(irep.itableStruct, irep.rtt, vals);
			vals[0] = itable_val;
			for (j < l.head.1.length) {
				var m = vtable[l.head.1[j]];
				vals[1 + j] = Value.FuncRef(m.impl);
			}
			itable_val = Value.Ref(istruct);
		}
		vals[1] = itable_val;
		gcrep.metaObjectGlobal = addGlobal(ValueType.RefStruct(false, gcrep.metaStruct), false, gcrep.metaObject);

		return gcrep.metaObject;
	}
	def makeInterfaceRep(it: JawaInterfaceType) -> JawaGcRuntimeInterfaceRep {
		var gcrep = interfaceReps[it];
		if (gcrep != null) return gcrep;

		// Reuse the struct and meta struct representations from jawa/lang/Object, since interfaces do not have fields.
		var parent = JawaLang.OBJECT;
		var jlorep = makeClassRep(parent);
		var metaStruct = jlorep.metaStruct, struct = jlorep.struct;

		// Build the itable struct before recursing
		var field_types = Array<StorageType>.new(1 + if (it.instanceMethods != null, it.instanceMethods.length));
		var itable = newStruct(it, field_types);
		interfaceReps[it] = gcrep = JawaGcRuntimeInterfaceRep.new(metaStruct, struct, itable);

		// Create RTT for itable struct
		var rtt = newRtt(null, itable);
		gcrep.rtt = rtt.1;
		gcrep.rttGlobal = addGlobal(rtt.0, false, rtt.1);

		// Recursively build the itable fields (i.e. method slots).
		field_types[0] = StorageType.new(ValueType.RefStruct(true, makeITableStruct()), Packedness.UNPACKED, false);
		if (it.instanceMethods != null) for (i < it.instanceMethods.length) {
			var m = it.instanceMethods[i];
			var sig = m.sig.sig(parent, toWasmType);
			field_types[1 + i] = StorageType.new(
				ValueType.RefFunc(false, sig), Packedness.UNPACKED, false);
		}
		return gcrep;
	}
	def newStruct(t: JawaRefType, field_types: Array<StorageType>) -> StructDecl {
		var struct = StructDecl.new(field_types);
		comodule.addDecl(struct);
		if (t != null) reftypes.set(struct.struct_index, t);
		return struct;
	}
	def makeITableStruct() -> StructDecl {
		if (iTableStruct != null) return iTableStruct;
		iTableStruct = newStruct(null, Array<StorageType>.new(1));
		iTableStruct.field_types[0] = StorageType.new(
			ValueType.RefStruct(true, iTableStruct), Packedness.UNPACKED, false);
		return iTableStruct;
	}
	def toExportedType(t: JawaRefType) -> ExportedType {
		return ExportedValueType.new(t, toWasmType(t));
	}
	def nullRef() -> Value {
		return Values.REF_NULL;
	}
	def newObject(ct: JawaClassType) -> Value {
		var gcrep = makeClassRep(ct);
		var obj = HeapStruct.new(gcrep.struct, gcrep.rtt, Array<Value>.new(gcrep.struct.field_types.length));
		obj.vals[0] = makeMetaObject(gcrep, ct.vtable, ct.itable);
		Arrays.copyInto(obj.vals, CLASS_HEADER_FIELDS, ct.boilerplate, 0, ct.boilerplate.length);
		return Value.Ref(obj);
	}
	def getStatic(ct: JawaClassType, f: JawaField) -> Value {
		var gcrep = makeClassRep(ct);
		return coinstance.globals[gcrep.staticFields[f.index].global_index].value;
	}
	def setStatic(ct: JawaClassType, f: JawaField, v: Value) {
		var gcrep = makeClassRep(ct);
		coinstance.globals[gcrep.staticFields[f.index].global_index].value = v;
	}
	def getField(obj: Value, i: int) -> Value {
		var ref = HeapStruct.!(Value.Ref.!(obj).val);
		return ref.vals[i + CLASS_HEADER_FIELDS];
	}
	def setField(obj: Value, i: int, v: Value) {
		var ref = HeapStruct.!(Value.Ref.!(obj).val);
		ref.vals[i + CLASS_HEADER_FIELDS] = v;
	}
	def newArray(at: JawaArrayType, length: int) -> Value {
		var gcrep = makeArrayRep(at);
		var array = HeapArray.new(gcrep.array, gcrep.arrayRtt, Array<Value>.new(length));
		var obj = HeapStruct.new(gcrep.struct, gcrep.rtt, Array<Value>.new(gcrep.struct.field_types.length));
		obj.vals[0] = makeMetaObject(gcrep, JawaLang.OBJECT.vtable, JawaLang.OBJECT.itable);
		obj.vals[1] = Value.Ref(array);
		return Value.Ref(obj);
	}
	def initArray<T>(at: JawaArrayType, vals: Array<T>, map: JawaPrimMappingOf<T>) -> Value {
		var gcrep = makeArrayRep(at);
		var vals = Arrays.map(vals, map.box);
		var array = HeapArray.new(gcrep.array, gcrep.arrayRtt, vals);
		var obj = HeapStruct.new(gcrep.struct, gcrep.rtt, Array<Value>.new(gcrep.struct.field_types.length));
		obj.vals[0] = makeMetaObject(gcrep, JawaLang.OBJECT.vtable, JawaLang.OBJECT.itable);
		obj.vals[1] = Value.Ref(array);
		return Value.Ref(obj);
	}
	def getElem(arr: Value, i: int) -> Value {
		return toHeapArray(arr).vals[i];
	}
	def setElem(arr: Value, i: int, v: Value) {
		toHeapArray(arr).vals[i] = v;
	}
	def getLength(arr: Value) -> int {
		return toHeapArray(arr).vals.length;
	}
	private def toHeapArray(arr: Value) -> HeapArray {
		var struct = HeapStruct.!(Value.Ref.!(arr).val);
		return HeapArray.!(Value.Ref.!(struct.vals[ARRAY_STRUCT_ARRAY_FIELD]).val);
	}
	def getJawaType(v: Value) -> JawaType {
		match (v) {
			I32 => return JawaTypes.INT;
			I64 => return JawaTypes.LONG;
			F32 => return JawaTypes.FLOAT;
			F64 => return JawaTypes.DOUBLE;
			Ref(ref) => match (ref) {
				x: HeapStruct => {
					var i = x.decl.struct_index;
					if (i <= comodule.heaptypes.length && comodule.heaptypes[i] == x.decl) return reftypes[i];
				}
				x: HeapArray => {
					var i = x.decl.array_index;
					if (i <= comodule.heaptypes.length && comodule.heaptypes[i] == x.decl) return reftypes[i];
				}
			}
			_ => ;
		}
		return null;
	}

	def toStorageType(jt: JawaType, mutable: bool) -> StorageType {
		var packing = Packedness.UNPACKED;
		var vt: ValueType;
		match (jt) {
			x: JawaPrimType => {
				match (x.kind) {
					CHAR, SHORT => packing = Packedness.PACKED_I16;
					BYTE, BOOL => packing = Packedness.PACKED_I8;
					_ => ;
				}
				vt = x.prim.valueType;
			}
			x: JawaArrayType => vt = makeArrayRep(x).valueType;
			x: JawaClassType => vt = makeClassRep(x).valueType;
			x: JawaInterfaceType => vt = makeInterfaceRep(x).valueType;
		}
		return StorageType.new(vt, packing, mutable);
	}
	def addGlobal(vt: ValueType, mutable: bool, v: Value) -> GlobalDecl {
		var decl = GlobalDecl.new(vt, mutable, InitExpr.Const(v));
		comodule.addDecl(decl);
		// XXX: better management of coinstance globals
		var g = Global.new(vt, decl);
		coinstance.globals = Arrays.append(g, coinstance.globals);
		g.value = v;
		return decl;
	}

//-------------------------------------------------------------------------------------------------
	def AALOAD(at: JawaArrayType) -> WasmFunction {
		var gcrep = makeArrayRep(at);
		start0()
			.local_get(0)
			.struct_get(gcrep.struct, ARRAY_STRUCT_ARRAY_FIELD)
			.local_get(1)
			.array_get(gcrep.array);
		return makeFuncP([gcrep.valueType, ValueType.I32], [toWasmType(at.elem)]);
	}
//-------------------------------------------------------------------------------------------------
	def AASTORE(at: JawaArrayType) -> WasmFunction {
		var gcrep = makeArrayRep(at);
		start0()
			.local_get(0)
			.struct_get(gcrep.struct, ARRAY_STRUCT_ARRAY_FIELD)
			.local_get(1)
			.local_get(2)
			.array_set(gcrep.array);
		return makeFuncP([gcrep.valueType, ValueType.I32, toWasmType(at.elem)], SigCache.arr_v);
	}
//-------------------------------------------------------------------------------------------------
	def TODO: Function;
	def ACMPEQ() -> Function {
		var gcrep = makeClassRep(JawaLang.OBJECT);
		start0()
			.local_get(0)
			.local_get(1)
			.ref_eq();
		return makeFuncP([gcrep.valueType, gcrep.valueType], SigCache.arr_i);
	}
	def ANEWARRAY(at: JawaArrayType) -> Function {
		return NEWARRAY(at);
	}
	def ARRAYLENGTH(at: JawaArrayType) -> Function {
		var gcrep = makeArrayRep(at);
		start0()
			.local_get(0)
			.struct_get(gcrep.struct, ARRAY_STRUCT_ARRAY_FIELD)
			.array_len(gcrep.array);
		return makeFuncP([gcrep.valueType], SigCache.arr_i);
	}
	def NEWARRAY(at: JawaArrayType) -> Function {
		var gcrep = makeArrayRep(at);
		makeMetaObject(gcrep, JawaLang.OBJECT.vtable, JawaLang.OBJECT.itable);
		start0()
			.global_get(gcrep.metaObjectGlobal.global_index)
			.local_get(0)
			.global_get(gcrep.elemsRttGlobal.global_index)
			.array_new_default_with_rtt(gcrep.array)
			.global_get(gcrep.rttGlobal.global_index)
			.struct_new_with_rtt(gcrep.struct);
		return makeFuncP(SigCache.arr_i, [gcrep.valueType]);
	}
	def ALOAD<T>(at: JawaArrayType, map: JawaPrimMappingOf<T>) -> Function {
		var gcrep = makeArrayRep(at);
		start0()
			.local_get(0)
			.struct_get(gcrep.struct, ARRAY_STRUCT_ARRAY_FIELD)
			.local_get(1);
		match (at.elem.kind) {
			BYTE, SHORT => asm.array_get_s(gcrep.array);
			CHAR => asm.array_get_u(gcrep.array);
			_ => asm.array_get(gcrep.array);
		}
		return makeFuncP([gcrep.valueType, ValueType.I32], [toWasmType(at.elem)]);
	}
	def ASTORE<T>(at: JawaArrayType, map: JawaPrimMappingOf<T>) -> Function {
		var gcrep = makeArrayRep(at);
		start0()
			.local_get(0)
			.struct_get(gcrep.struct, ARRAY_STRUCT_ARRAY_FIELD)
			.local_get(1)
			.local_get(2)
			.array_set(gcrep.array);
		return makeFuncP([gcrep.valueType, ValueType.I32, toWasmType(at.elem)], SigCache.arr_v);
	}
	def CHECKCAST(t: JawaRefType) -> Function {
		var ot = makeClassRep(JawaLang.OBJECT);
		var gcrep: JawaGcRuntimeHeapRep;
		match (t) {
			at: JawaArrayType => gcrep = makeArrayRep(at);
			ct: JawaClassType => gcrep = makeClassRep(ct);
			it: JawaInterfaceType => return TODO;
			_ => return TODO;
		}
		start0()
			.local_get(0)
			.ref_is_null()
			.if_(gcrep.valueType)
				.ref_null(gcrep.struct.struct_index)
			.else_()
				.local_get(0)
				.global_get(gcrep.rttGlobal.global_index)
				.ref_cast(ot.struct.struct_index, gcrep.struct.struct_index)
			.end();
		return makeFuncP([ot.valueType], [gcrep.valueType]);
	}
	def DCMPG() -> Function {
		return xCMPy(Opcode.F64_GT, Opcode.F64_EQ, SigCache.dd_i);
	}
	def DCMPL() -> Function {
		return xCMPy(Opcode.F64_LT, Opcode.F64_EQ, SigCache.dd_i);
	}
	def DREM() -> Function { return TODO; }
	def FCMPG() -> Function {
		return xCMPy(Opcode.F32_GT, Opcode.F32_EQ, SigCache.ff_i);
	}
	def FCMPL() -> Function {
		return xCMPy(Opcode.F32_GT, Opcode.F32_EQ, SigCache.ff_i);
	}
	def FREM() -> Function { return TODO; }
	private def xCMPy(cmp: Opcode, eq: Opcode, sig: SigDecl) -> Function {
		start0()  // XXX: spell out code as a byte array?
			.local_get(0)
			.local_get(1)
			.op(cmp)
			.if_(ValueType.I32)
				.i32const(1)
			.else_()
				.local_get(0)
				.local_get(1)
				.op(eq)
			.end()
			.i32const(1)
			.op(Opcode.I32_SUB);
		return makeFuncS(sig);
	}
	def GETFIELD(ct: JawaClassType, f: JawaField) -> Function {
		var gcrep = makeClassRep(ct);
		start0()
			.local_get(0);
		var index = f.index + CLASS_HEADER_FIELDS;
		match (f.typ.kind) {
			BYTE, SHORT => asm.struct_get_s(gcrep.struct, index);
			CHAR => asm.struct_get_u(gcrep.struct, index);
			_ => asm.struct_get(gcrep.struct, index);
		}
		return makeFuncP([gcrep.valueType], [toWasmType(f.typ)]);
	}
	def GETSTATIC(ct: JawaClassType, f: JawaField) -> Function {
		var gcrep = makeClassRep(ct);
		start0()
			.global_get(gcrep.staticFields[f.index].global_index);
		return makeFuncP(SigCache.arr_v, [toWasmType(f.typ)]);
	}
	def INSTANCEOF(t: JawaRefType) -> Function {
		match (t) {
			at: JawaArrayType => {
				return TODO;
			}
			ct: JawaClassType => {
				var ot = makeClassRep(JawaLang.OBJECT);
				var gcrep = makeClassRep(ct);
				start0()
					.local_get(0)
					.global_get(gcrep.rttGlobal.global_index)
					.ref_test(ot.struct.struct_index, gcrep.struct.struct_index);
				return makeFuncP([ot.valueType], SigCache.arr_i);
			}
			it: JawaInterfaceType => {
				return TODO;
			}
		}
		return null;
	}
	def INVOKEINTERFACE(it: JawaInterfaceType, m: JawaMethod) -> Function {
		var gcrep = makeInterfaceRep(it);
		start0().local_get(0);  // load receiver and all args
		for (i < m.sig.params.length) asm.local_get(1 + i);
		var itable_struct = makeITableStruct(), itable_type = ValueType.RefStruct(true, itable_struct);
		var blockSig = SigDecl.new([itable_type], [ValueType.RefStruct(false, gcrep.itableStruct)]);
		comodule.addDecl(blockSig);
		asm.local_get(0) // load receiver for method lookup
			.struct_get(gcrep.struct, 0) // metaobject ptr
			.struct_get(gcrep.metaStruct, 1)  // itable list
			.blocks(blockSig)
				.loops(blockSig)
					.global_get(gcrep.rttGlobal.global_index) // XXX: cache the RTT in a local variable
					.br_on_cast(1, itable_struct.struct_index, gcrep.itableStruct.struct_index)
					.br(0)
				.end()
			.end()
			.struct_get(gcrep.itableStruct, 1 + m.itable_index)
			./*XXX:return_*/call_ref();
		return makeFuncS(m.sig.sig(it, toWasmType));
	}
	def INVOKESPECIAL(ct: JawaClassType, m: JawaMethod) -> Function {
		var gcrep = makeClassRep(JawaLang.OBJECT);
		start0()
			.local_get(0)
			.ref_as_non_null();
		for (i < m.sig.params.length) asm.local_get(i + 1);
		var funcSig = m.sig.sig(ct, toWasmType);
		var g = addGlobal(ValueType.RefFunc(false, funcSig), false, Value.FuncRef(m.impl)); // XXX: cache impl ref?
		asm.global_get(g.global_index);
		asm./*XXX:return_*/call_ref();
		return makeFuncS(funcSig);
	}
	def INVOKESTATIC(ct: JawaClassType, m: JawaMethod) -> Function {
		return m.impl;
	}
	def INVOKEVIRTUAL(ct: JawaClassType, m: JawaMethod) -> Function {
		var gcrep = makeClassRep(ct);
		start0().local_get(0);
		for (i < m.sig.params.length) asm.local_get(1 + i);
		asm
			.local_get(0)
			.struct_get(gcrep.struct, 0)
			.struct_get(gcrep.metaStruct, META_STRUCT_HEADER_FIELDS + m.vtable_index)
			./*XXX:return_*/call_ref();
		return makeFuncS(m.sig.sig(ct, toWasmType));
	}
	def ISNULL() -> Function {
		var gcrep = makeClassRep(JawaLang.OBJECT);
		start0()
			.local_get(0)
			.ref_is_null();
		return makeFuncP([gcrep.valueType], SigCache.arr_i);
	}
	def MONITORENTER() -> Function {
		var gcrep = makeClassRep(JawaLang.OBJECT);
		start0()
			.local_get(0)
			.struct_get(gcrep.struct, 0)  // trap on null
			.drop();
		return makeFuncP([gcrep.valueType], SigCache.arr_v);
	}
	def MONITOREXIT() -> Function {
		var gcrep = makeClassRep(JawaLang.OBJECT);
		start0()
			.local_get(0)
			.struct_get(gcrep.struct, 0)  // trap on null
			.drop();
		return makeFuncP([gcrep.valueType], SigCache.arr_v);
	}
	def MULTIANEWARRAY(dims: int, at: JawaArrayType) -> Function {
		start();
		var tmps = Array<int>.new(dims - 1);
		asm.put_u32leb(dims - 1);
		var ct = at;
		for (i < dims - 1) {  // allocate temporary variables for intermediate arrays
			asm.put(1);
			var gcrep = makeArrayRep(ct);
			asm.put_blockType(ValueType.RefArray(true, gcrep.array));
			tmps[i] = dims + i;
			ct = JawaArrayType.!(ct.elem);
		}
		recursiveMakeArray(dims, at, tmps, 0);
		var params = Array<ValueType>.new(dims);
		for (i < dims) params[i] = ValueType.I32;
		return makeFuncP(params, [toWasmType(at)]);
	}
	def recursiveMakeArray(dims: int, at: JawaArrayType, tmps: Array<int>, pos: int) {
		var gcrep = makeArrayRep(at);
		makeMetaObject(gcrep, JawaLang.OBJECT.vtable, JawaLang.OBJECT.itable);
		// allocate array elements
		var cnt_local = pos;
		asm
		.global_get(gcrep.metaObjectGlobal.global_index)
		.local_get(cnt_local)
		.global_get(gcrep.elemsRttGlobal.global_index)
		.array_new_default_with_rtt(gcrep.array);

		if (pos != dims - 1) {
			// recursively allocate and initialize elements
			asm
			.local_set(tmps[pos])
			.loop0()
			.local_get(cnt_local)
			.if0()
				.local_get(tmps[pos])
				.local_get(cnt_local)
				.i32const(1)
				.op(Opcode.I32_SUB)
				.local_tee(cnt_local);

			recursiveMakeArray(dims, JawaArrayType.!(at.elem), tmps, pos + 1);

			asm
				.array_set(gcrep.array)
				.br(1)
			.end()
			.end()
			.local_get(tmps[cnt_local])
			.ref_as_non_null();
		}
		// allocate array object
		asm
		.global_get(gcrep.rttGlobal.global_index)
		.struct_new_with_rtt(gcrep.struct);
	}
	def NEW(ct: JawaClassType) -> Function {
		var gcrep = makeClassRep(ct);
		makeMetaObject(gcrep, ct.vtable, ct.itable);
		// Declare globals that contain default values for fields
		start().put_u32leb(gcrep.struct.field_types.length - 1); // # locals
		for (i = 1; i < gcrep.struct.field_types.length; i++) {
			asm.locals(1, gcrep.struct.field_types[i].val_type);
		}
		// Load the meta object
		asm.global_get(gcrep.metaObjectGlobal.global_index);
		for (i = 1; i < gcrep.struct.field_types.length; i++) {
			asm.local_get(i - 1);
		}
		// Load the RTT
		asm.global_get(gcrep.rttGlobal.global_index)
			.struct_new_with_rtt(gcrep.struct);
		return makeFuncP(SigCache.arr_v, [gcrep.valueType]);
	}
	def PUTFIELD(ct: JawaClassType, f: JawaField) -> Function {
		var gcrep = makeClassRep(ct);
		start0()
			.local_get(0)
			.local_get(1)
			.struct_set(gcrep.struct, f.index + CLASS_HEADER_FIELDS);
		return makeFuncP([gcrep.valueType, toWasmType(f.typ)], SigCache.arr_v);
	}
	def PUTSTATIC(ct: JawaClassType, f: JawaField) -> Function {
		var gcrep = makeClassRep(ct);
		start0()
			.local_get(0)
			.global_set(gcrep.staticFields[f.index].global_index);
		return makeFuncP([toWasmType(f.typ)], SigCache.arr_v);
	}

//-------------------------------------------------------------------------------------------------

	def start0() -> WasmGcAsm {
		return asm.resize(0)
			.put(0 /*locals*/);
	}
	def start() -> WasmGcAsm {
		return asm.resize(0);
	}
	def makeFuncP(params: Array<ValueType>, results: Array<ValueType>) -> WasmFunction {
		return makeFuncS(SigDecl.new(params, results));
	}
	def makeFuncS(sig: SigDecl) -> WasmFunction {
		comodule.addDecl(sig);
		var func = FuncDecl.new(sig);
		func.code = Code.new(asm.put(Opcode.END.code).extract());
		comodule.addDecl(func);
		var f = WasmFunction.new(coinstance, func);
		var r = validator.validateFunc(f.decl); // XXX: don't make a decoder every time
		match (r) {
			Error(error, pc, msg) => {
				System.error("JawaGcRuntimeError", Strings.format2("stub validation failed @ +%d: %s", pc, msg));
			}
			_ => ;
		}
		return f;
	}
}

class WasmGcAsm extends Vector<byte> {
	def put_u32leb(v: int) -> this {
		var data = u32.view(v);
		while (data >= 0x80) {
			put(byte.view(0x80u | (data & 0x7F)));
			data = data >> 7;
		}
		put(byte.view(data));
	}
	def locals(count: int, vt: ValueType) -> this {
		put_u32leb(count);
		put_blockType(vt);
	}
	def local_get(index: int) -> this {
		put(Opcode.LOCAL_GET.code).put_u32leb(index);
	}
	def local_tee(index: int) -> this {
		put(Opcode.LOCAL_TEE.code).put_u32leb(index);
	}
	def global_get(index: int) -> this {
		put(Opcode.GLOBAL_GET.code).put_u32leb(index);
	}
	def global_set(index: int) -> this {
		put(Opcode.GLOBAL_SET.code).put_u32leb(index);
	}
	def local_set(index: int) -> this {
		put(Opcode.LOCAL_SET.code).put_u32leb(index);
	}
	def i32const(v: int) -> this {
		put(Opcode.I32_CONST.code).put_u32leb(v);
	}
	def struct_get(decl: StructDecl, field: int) -> this {
		put(Opcode.STRUCT_GET.prefix).put(Opcode.STRUCT_GET.code).put_u32leb(decl.struct_index).put_u32leb(field);
	}
	def struct_get_s(decl: StructDecl, field: int) -> this {
		put(Opcode.STRUCT_GET_S.prefix).put(Opcode.STRUCT_GET_S.code).put_u32leb(decl.struct_index).put_u32leb(field);
	}
	def struct_get_u(decl: StructDecl, field: int) -> this {
		put(Opcode.STRUCT_GET_U.prefix).put(Opcode.STRUCT_GET_U.code).put_u32leb(decl.struct_index).put_u32leb(field);
	}
	def struct_set(decl: StructDecl, field: int) -> this {
		put(Opcode.STRUCT_SET.prefix).put(Opcode.STRUCT_SET.code).put_u32leb(decl.struct_index).put_u32leb(field);
	}
	def struct_new_default_with_rtt(decl: StructDecl) -> this {
		put(Opcode.STRUCT_NEW_DEFAULT_WITH_RTT.prefix)
			.put(Opcode.STRUCT_NEW_DEFAULT_WITH_RTT.code).put_u32leb(decl.struct_index);
	}
	def struct_new_with_rtt(decl: StructDecl) -> this {
		put(Opcode.STRUCT_NEW_WITH_RTT.prefix)
			.put(Opcode.STRUCT_NEW_WITH_RTT.code).put_u32leb(decl.struct_index);
	}
	def array_get(decl: ArrayDecl) -> this {
		put(Opcode.ARRAY_GET.prefix).put(Opcode.ARRAY_GET.code).put_u32leb(decl.array_index);
	}
	def array_get_s(decl: ArrayDecl) -> this {
		put(Opcode.ARRAY_GET_S.prefix).put(Opcode.ARRAY_GET_S.code).put_u32leb(decl.array_index);
	}
	def array_get_u(decl: ArrayDecl) -> this {
		put(Opcode.ARRAY_GET_U.prefix).put(Opcode.ARRAY_GET_U.code).put_u32leb(decl.array_index);
	}
	def array_set(decl: ArrayDecl) -> this {
		put(Opcode.ARRAY_SET.prefix).put(Opcode.ARRAY_SET.code).put_u32leb(decl.array_index);
	}
	def array_new(decl: ArrayDecl) -> this {
		put(Opcode.ARRAY_NEW.prefix).put(Opcode.ARRAY_NEW.code).put_u32leb(decl.array_index);
	}
	def array_new_default_with_rtt(decl: ArrayDecl) -> this {
		put(Opcode.ARRAY_NEW_DEFAULT_WITH_RTT.prefix)
			.put(Opcode.ARRAY_NEW_DEFAULT_WITH_RTT.code).put_u32leb(decl.array_index);
	}
	def array_len(decl: ArrayDecl) -> this {
		put(Opcode.ARRAY_LEN.prefix).put(Opcode.ARRAY_LEN.code).put_u32leb(decl.array_index);
	}
	def ref_eq() -> this {
		put(Opcode.REF_EQ.code);
	}
	def ref_is_null() -> this {
		put(Opcode.REF_IS_NULL.code);
	}
	def ref_cast(ft: int, tt: int) -> this {
		put(Opcode.REF_CAST.prefix).put(Opcode.REF_CAST.code).put_u32leb(ft).put_u32leb(tt);
	}
	def ref_test(ft: int, tt: int) -> this {
		put(Opcode.REF_TEST.prefix).put(Opcode.REF_TEST.code).put_u32leb(ft).put_u32leb(tt);
	}
	def ref_null(i: int) -> this {
		put(Opcode.REF_NULL.code).put_u32leb(i);
	}
	def ref_as_non_null() -> this {
		put(Opcode.REF_AS_NON_NULL.code);
	}
	def call_ref() -> this {
		put(Opcode.CALL_REF.code);
	}
	def return_call_ref() -> this {
		put(Opcode.RETURN_CALL_REF.code);
	}
	def loop0() -> this {
		put(Opcode.LOOP.code);
		put(BpTypecon.EmptyBlock.code);
	}
	def blocks(sig: SigDecl) -> this {
		put(Opcode.BLOCK.code);
		put_u32leb(sig.sig_index);
	}
	def loops(sig: SigDecl) -> this {
		put(Opcode.LOOP.code);
		put_u32leb(sig.sig_index);
	}
	def if_(t: ValueType) -> this {
		put(Opcode.IF.code);
		put_blockType(t);
	}
	def if0() -> this {
		put(Opcode.IF.code);
		put(BpTypecon.EmptyBlock.code);
	}
	def else_() -> this {
		put(Opcode.ELSE.code);
	}
	def drop() -> this {
		put(Opcode.DROP.code);
	}
	def br_if(depth: int) -> this {
		put(Opcode.BR_IF.code);
		put_u32leb(depth);
	}
	def br(depth: int) -> this {
		put(Opcode.BR.code);
		put_u32leb(depth);
	}
	def br_on_cast(depth: int, t1: int, t2: int) -> this {
		put(Opcode.BR_ON_CAST.prefix).put(Opcode.BR_ON_CAST.code);
		put_u32leb(depth);
		put_u32leb(t1);
		put_u32leb(t2);
	}
	def end() -> this {
		put(Opcode.END.code);
	}
	def op(opcode: Opcode) -> this {
		put(opcode.code);
	}
	def put_blockType(t: ValueType) -> this {
		match (t) {
			I32 => put(BpTypecon.I32.code);
			I64 => put(BpTypecon.I64.code);
			F32 => put(BpTypecon.F32.code);
			F64 => put(BpTypecon.F64.code);
			VEC128 => put(BpTypecon.VEC128.code);
			FUNCREF => put(BpTypecon.FUNCREF.code);
			EXTERNREF => put(BpTypecon.EXTERNREF.code);
			ANYREF => ; // TODO
			I31REF => put(BpTypecon.I31REF.code);
			RefFunc(nullable, sig) => put(if(nullable, BpTypecon.RefNullT.code, BpTypecon.RefT.code))
				.put_u32leb(sig.sig_index);
			RefStruct(nullable, struct) => put(if(nullable, BpTypecon.RefNullT.code, BpTypecon.RefT.code))
				.put_u32leb(struct.struct_index);
			RefArray(nullable, array) => put(if(nullable, BpTypecon.RefNullT.code, BpTypecon.RefT.code))
				.put_u32leb(array.array_index);
			_ => ; // TODO
		}
	}
}

class JawaGcStringLens extends JawaWasmLens<JawaString, Value> {
	def toWasm(s: JawaString) -> Value {
		return Values.REF_NULL; // TODO
	}
	def fromWasm(v: Value) -> JawaString {
		return null; // TODO
	}
}
class JawaGcRefTypeClassObjectLens extends JawaWasmLens<JawaRefType, Value> {
	def toWasm(ct: JawaRefType) -> Value {
		return Values.REF_NULL; // TODO
	}
	def fromWasm(v: Value) -> JawaRefType {
		return null; // TODO
	}
}

class JawaGcRuntimeHeapRep(metaStruct: StructDecl, struct: StructDecl) {
	var rtt: Value.Rtt;
	var rttGlobal: GlobalDecl;
	var metaObject: Value.Ref;
	var metaObjectGlobal: GlobalDecl;
	def valueType = ValueType.RefStruct(true, struct);
}

class JawaGcRuntimeArrayRep extends JawaGcRuntimeHeapRep {
	def array: ArrayDecl;
	var arrayRtt: Value.Rtt;
	var elemsRttGlobal: GlobalDecl;

	new(metaStruct: StructDecl, struct: StructDecl, array) super(metaStruct, struct) { }
}

class JawaGcRuntimeClassRep extends JawaGcRuntimeHeapRep {
	var staticFields: Array<GlobalDecl>;

	new(metaStruct: StructDecl, struct: StructDecl) super(metaStruct, struct) { }
}

class JawaGcRuntimeInterfaceRep  extends JawaGcRuntimeHeapRep {
	def itableStruct: StructDecl;

	new(metaStruct: StructDecl, struct: StructDecl, itableStruct) super(metaStruct, struct) { }
}
