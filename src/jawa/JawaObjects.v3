// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// An instance of an on-heap Jawa object
class JawaObject extends HostObject {
	// TODO: uid for identity hashcode
	// TODO: monitor state
	/* TODO: Jawa API
	def hashCode() -> int;
	def clone() -> JawaObject;
	def equals(that: JawaObject);
	def finalize();
	def getClass() -> JawaClass;
	def notify();
	def notifyAll();
	def toString() -> JawaString;
	def wait();
	def wait(timeout: long);
	def wait(timeout: long, nanos: int);
*/
	def jawaType() -> JawaType;
}
class JawaArrayObject extends JawaObject {
	def length() -> int;
}
class JawaArrayObjectOf<T>(elems: Array<T>) extends JawaArrayObject {
	def length() -> int { return elems.length; }
	def jawaType() -> JawaType {
		// XXX: bake array type into Jawa primitive array objects?
		match (this) {
			x: JawaArrayObjectOf<i8> => return JawaTypes.BYTE_ARRAY;
			x: JawaArrayObjectOf<bool> => return JawaTypes.BOOL_ARRAY;
			x: JawaArrayObjectOf<u16> => return JawaTypes.CHAR_ARRAY;
			x: JawaArrayObjectOf<i16> => return JawaTypes.SHORT_ARRAY;
			x: JawaArrayObjectOf<i32> => return JawaTypes.INT_ARRAY;
			x: JawaArrayObjectOf<i64> => return JawaTypes.LONG_ARRAY;
			x: JawaArrayObjectOf<float> => return JawaTypes.FLOAT_ARRAY;
			x: JawaArrayObjectOf<double> => return JawaTypes.DOUBLE_ARRAY;
		}
		return null;
	}
}
class JawaRefArrayObject(at: JawaArrayType, elems: Array<JawaObject>) extends JawaArrayObject {
	def length() -> int { return elems.length; }
	def jawaType() -> JawaType { return at; }
}
class JawaInstanceObject(clazz: JawaClassType) extends JawaObject {
	def fields = Arrays.dup(clazz.boilerplate);
	def jawaType() -> JawaType { return clazz; }
}
class JawaStringObject(rep: JawaString) extends JawaInstanceObject(JawaLang.STRING) {
}
class JawaClassObject(rep: JawaClassType) extends JawaInstanceObject(JawaLang.CLASS) {
}
class JawaInterfaceObject(rep: JawaInterfaceType) extends JawaInstanceObject(JawaLang.CLASS) {
}
// Global state associated with JawaObjects
class JawaHeap {
	var uid: int;
}
