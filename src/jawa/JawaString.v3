// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// An immutable UTF-16 string with a cached hashcode.
class JawaString(chars: Array<u16>) {
	private var hashed: bool;
	private var hash: u32;
	var obj: JawaStringObject; // XXX: only for JawaHostRuntime

	def equals(that: JawaString) -> bool {
		if (this == that) return true;
		if (that == null) return false;
		return this.hashCode() == that.hashCode() &&
			Arrays.equal(this.chars, that.chars);
	}
	def hashCode() -> u32 {
		if (hashed) return hash;
		var r = 0u;
		for (i < chars.length) r = r * 31 + chars[i];
		hashed = true;
		return hash = r;
	}
	def render(buf: StringBuilder) -> StringBuilder {
		for (c in chars) buf.pututf8(c);
		return buf;
	}
	def toObject() -> JawaStringObject {
		var ref = obj;
		if (ref == null) ref = obj = JawaStringObject.new(this);
		return ref;
	}
}

// Utility methods associated with strings.
component JawaStrings {
	def of(s: string) -> JawaString {
		var chars = Array<u16>.new(s.length);
		for (i < s.length) chars[i] = s[i];
		return JawaString.new(chars);
	}
}