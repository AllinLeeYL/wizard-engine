// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Implementation of the jawa runtime using only the host language, Virgil.
class JawaHostRuntime extends JawaRuntime {
	def toType(ct: JawaType) -> ValueType {
		return ValueType.Host(ct);
	}
	def stringToValue(s: JawaString) -> Value {
		return Value.ExternRef(s.toObject());
	}
	def classToValue(s: JawaClassType) -> Value {
		return Value.ExternRef(s.toObject());
	}
	def nullRef() -> Value {
		return Values.EXTERNREF_NULL;
	}
	def newObject(ct: JawaClassType) -> Value {
		return Value.ExternRef(JawaInstanceObject.new(ct));
	}
	def getField(obj: Value, i: int) -> Value {
		return JawaInstanceObject.!(Value.ExternRef.!(obj).val).fields[i];
	}
	def setField(obj: Value, i: int, v: Value) {
		JawaInstanceObject.!(Value.ExternRef.!(obj).val).fields[i] = v;
	}
}
