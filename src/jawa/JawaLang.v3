// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// All built-in classes in Jawa.
component JawaLang {
	def NO_INTERFACES: Array<JawaInterfaceType> = [];
	def NO_CLASS_ATTRS: JawaClassAttr.set;
	def NO_FIELD_ATTRS: JawaFieldAttr.set;
	def NO_METHOD_ATTRS: JawaMethodAttr.set;
	def NO_METHODS = Array<JawaMethod>.new(0);
	def NO_FIELDS = Array<JawaField>.new(0);

	def OBJECT = newEmptyClass("jawa/lang/Object", null);
	def STRING = newEmptyClass("jawa/lang/String", OBJECT);
	def CLASS = newEmptyClass("jawa/lang/Class", OBJECT);

	new() {
		def PUBLIC: JawaMethodAttr.set = JawaMethodAttr.PUBLIC;
		var init = JawaStrings.of("<init>");
		var equals = JawaStrings.of("equals");
		var hashCode = JawaStrings.of("hashCode");
		var getClass = JawaStrings.of("getClass");

		var sig_v_v = JawaMethodSig.new([], JawaTypes.VOID);
		var sig_v_c = JawaMethodSig.new([], CLASS);
		var sig_v_i = JawaMethodSig.new([], JawaTypes.INT);
		var sig_o_z = JawaMethodSig.new([OBJECT], JawaTypes.BOOL);

		OBJECT.instanceMethods = [
			newInstanceMethod(OBJECT, init, PUBLIC, sig_v_v, nop),
			newInstanceMethod(OBJECT, equals, PUBLIC, sig_o_z, Object_equals),
			newInstanceMethod(OBJECT, hashCode, PUBLIC, sig_v_i, Object_hashCode),
			newInstanceMethod(OBJECT, getClass, JawaMethodAttr.PUBLIC | JawaMethodAttr.FINAL, sig_v_c, Object_getClass)
		];

		STRING.instanceMethods = [
			newInstanceMethod(STRING, equals, PUBLIC, sig_o_z, String_equals),
			newInstanceMethod(STRING, hashCode, PUBLIC, sig_v_i, String_hashCode)
		];

		var err = ErrorGen.new();
		OBJECT.finish(err);
		STRING.finish(err);
		CLASS.finish(err);
	}

	def install(env: JawaEnvironment) {
		env.add(OBJECT);
		env.add(STRING);
		env.add(CLASS);
	}
	def newEmptyClass(name: string, sup: JawaClassType) -> JawaClassType {
		var ct = JawaClassType.new(JawaStrings.of(name), JawaClassAttr.PUBLIC, sup, NO_INTERFACES);
		ct.instanceFields = ct.staticFields = NO_FIELDS;
		ct.instanceMethods = ct.staticMethods = NO_METHODS;
		ct.boilerplate = Values.NONE;
		return ct;
	}

	private def newInstanceMethod(t: JawaDeclType, name: JawaString, attrs: JawaMethodAttr.set,
			sig: JawaMethodSig, eval: Array<Value> -> HostResult) -> JawaMethod {
		var buf = StringBuilder.new();
		t.name.render(buf);
		buf.putc('.');
		name.render(buf);
		var hf = HostFunctionN.new(buf.extract(), sig.sig(t, toHostType), eval);
		return JawaMethod.new(name, attrs, sig, hf);
	}
	private def toHostType(t: JawaType) -> ValueType { // XXX: reduce duplication with HostRuntime
		match (t) {
			x: JawaPrimType => return x.prim.valueType;
			_ => return ValueType.Host(t);
		}
	}

// ---- Implementation of jawa.lang.* methods -----------------------------------
	def TRUE = HostResult.Value1(Values.I32_1);
	def FALSE = HostResult.Value1(Values.I32_0);
	def VOID = HostResult.Value0;

	private def nop(args: Array<Value>) -> HostResult {
		return VOID;
	}
	private def Object_equals(args: Array<Value>) -> HostResult {
		var x = args[0], y = args[1];
		return if(x == y, TRUE, FALSE);
	}
	private def Object_hashCode(args: Array<Value>) -> HostResult {
		var a = args[0];
		if (!Value.Ref.?(a)) return FALSE;
		var x = Value.Ref.!(a).val;
		if (JawaObject.?(x)) {
			var xx = JawaObject.!(x);
			return HostResult.Value1(Value.I32(xx.hashCode()));
		}
		return FALSE;
	}
	private def Object_getClass(args: Array<Value>) -> HostResult {
		var cl: JawaClassObject;
		match (args[0]) {
			Ref(host) => match (host) {
				x: JawaInstanceObject => {
					cl = x.clazz.obj;
					if (cl == null) cl = x.clazz.obj = JawaClassObject.new(x.clazz);
				}
				x: JawaArrayObject => {
					var jt = JawaRefType.!(x.jawaType());
					cl = jt.obj;
					if (cl == null) cl = jt.obj = JawaClassObject.new(jt);
				}
				_ => ;
			}
			_ => ;
		}
		return HostResult.Value1(Value.Ref(cl));
	}
	private def String_equals(args: Array<Value>) -> HostResult {
		var a = args[0], b = args[1];
		if (!Value.Ref.?(a)) return FALSE;
		if (!Value.Ref.?(b)) return FALSE;
		var x = Value.Ref.!(a).val, y = Value.Ref.!(b).val;
		if (x == y) return TRUE;
		if (JawaStringObject.?(x) && JawaStringObject.?(y)) {
			var xx = JawaStringObject.!(x), yy = JawaStringObject.!(y);
			return if(xx.rep.equals(yy.rep), TRUE, FALSE);
		}
		return FALSE;
	}
	private def String_hashCode(args: Array<Value>) -> HostResult {
		var a = args[0];
		if (!Value.Ref.?(a)) return FALSE;
		var x = Value.Ref.!(a).val;
		if (JawaStringObject.?(x)) {
			var xx = JawaStringObject.!(x);
			return HostResult.Value1(Value.I32(xx.rep.hashCode()));
		}
		return FALSE;
	}
}
