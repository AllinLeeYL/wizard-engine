// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Host objects must extend this class to be extern refs.
class HostObject extends Object {
	def render(buf: StringBuilder) -> StringBuilder {
		return buf.puts("<hostobj>");
	}
}

// Host functions must extend this class to be func refs.
class HostFunction extends Function {
	def name: string;
	new(name, sig: SigDecl) super(sig) { }
	def render(buf: StringBuilder) -> StringBuilder {
		return if(name != null, buf.put1("<hostfunc: %s>", name), buf.puts("<hostfunc>"));
	}
}
// FAST: Subclasses by arity reduce the invocation overhead from marshalling argument arrays.
class HostFunction0 extends HostFunction {
	def invoke0: () -> HostResult;
	new(name: string, sig: SigDecl, invoke0) super(name, sig) { }
}
class HostFunction1 extends HostFunction {
	def invoke1: Value -> HostResult;
	new(name: string, sig: SigDecl, invoke1) super(name, sig) { }
}
class HostFunction2 extends HostFunction {
	def invoke2: (Value, Value) -> HostResult;
	new(name: string, sig: SigDecl, invoke2) super(name, sig) { }
}
class HostFunction3 extends HostFunction {
	def invoke3: (Value, Value, Value) -> HostResult;
	new(name: string, sig: SigDecl, invoke3) super(name, sig) { }
}
class HostFunctionN extends HostFunction {
	def invokeN: Array<Value> -> HostResult;
	new(name: string, sig: SigDecl, invokeN) super(name, sig) { }
}

// The possible return values from calling a host function.
type HostResult {
	case Throw(thrown: Throwable);
	case Value0;
	case Value1(val: Value);
	case ValueN(vals: Array<Value>);
	case TailCall(func: Function, args: Array<Value>);
}

// Host types must extend this class to be importable.
class HostType extends ExportedType {
	def isAssignableTo(t: ValueType) -> bool {
		match (t) {
			Host(host) => return this == host;
			Ref(nullable, heap) => match (heap) {
				EXTERN => return true;
				_ => return false;
			}
			_ => return false;
		}
	}
	def render(buf: StringBuilder) -> StringBuilder {
		return buf.puts("<hosttype>");
	}
}
