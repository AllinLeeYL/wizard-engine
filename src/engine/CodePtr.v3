// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// DataReader with additional utility methods for the interpreter.
class CodePtr extends DataReader {
	new(data: Array<byte>) super(data) { }
	def skip_block_type() {
		var b = read1();  // TODO: read full i32 LEB for block type?
		match (b) {
			BpTypeCode.RefT.code,
			BpTypeCode.RefNullT.code,
			BpTypeCode.AbstractT.code => read_uleb32();
		}
	}
	def skip_label() {
		read_uleb32();
	}
	def read_value_types() -> int {
		var count = int.!(read_uleb32()); // value count
		for (i < count) skip_value_type();
		return count;
	}
	def skip_local_decls() {
		var count = read_uleb32();
		for (i < count) {
			var d = read_uleb32();
			skip_value_type();
		}
	}
	def skip_value_type() {
		var b = read1();
		match (b) {
			BpTypeCode.RefT.code,
			BpTypeCode.RefNullT.code,
			BpTypeCode.AbstractT.code => read_uleb32();
		}
	}
}
