// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// The allowable types of any value in WebAssembly.
type ValueType {
	case I32;
	case I64;
	case F32;
	case F64;
	case VEC128;
	case FUNCREF;
	case EXTERNREF;
	case Function(params: Array<ValueType>, results: Array<ValueType>);
	case Extern(host: ExternType);
//	case Struct(fields: Array<ValueType>);
//	case Array(elem: ValueType);
}
// External types, such as type imports or host types. Extensible.
class ExternType {
	def isAssignableTo(t: ValueType) -> bool {
		match (t) {
			Extern(ext) => return this == ext;
			EXTERNREF => return true;
			_ => return false;
		}
	}
	def render(buf: StringBuilder) -> StringBuilder {
		return buf.puts("<externtype>");
	}
}
// Utility functions associated with value types.
component ValueTypes {
	def NONE = Array<ValueType>.new(0);
	def render(buf: StringBuilder, t: ValueType) -> StringBuilder {
		var s: string;
		match (t) {
			I32 => s = "i32";
			I64 => s = "i64";
			F32 => s = "f32";
			F64 => s = "f64";
			VEC128 => s = "v128";
			Extern(host) => return host.render(buf);
			FUNCREF => s = "funcref";
			EXTERNREF => s = "externref";
			Function(p, r) => return renderPR(buf, p, r);
		}
		return buf.puts(s);
	}
	def isNumeric(t: ValueType) -> bool {
		match (t) {
			I32, I64, F32, F64 => return true;
			_ => return false;
		}
	}
	def isAssignable(from: ValueType, to: ValueType) -> bool {
		match (from) {
			Function(pf, rf) => {
				match (to) {
					Function(pt, rt) => return co(pf, rf, pt, rt, isAssignable, isAssignable);
					FUNCREF => return true;
					_ => return false;
				}
			}
			Extern(host) => {
				return host.isAssignableTo(to);
			}
			_ => return from == to;
		}
	}
	def isAssignableSig(from: ValueType.Function, to: ValueType.Function) -> bool {
		return co(from.params, from.results, to.params, to.results, isAssignable, isAssignable);
	}
	def renderSig(buf: StringBuilder, t: ValueType.Function) -> StringBuilder {
		return renderPR(buf, t.params, t.results);
	}
	def renderPR(buf: StringBuilder, p: Array<ValueType>, r: Array<ValueType>) -> StringBuilder {
		buf.puts("[");
		for (i < p.length) {
			if (i > 0) buf.puts(" ");
			render(buf, p[i]);
		}
		buf.puts("] -> [");
		for (i < r.length) {
			if (i > 0) buf.puts(" ");
			render(buf, r[i]);
		}
		buf.puts("]");
		return buf;
	}
	def leastUpperBound(t: ValueType, f: ValueType) -> (bool, ValueType) {
		if (t == f) return (true, t);
		return (false, t);
	}
	def co<A, B>(fp: Array<A>, fr: Array<A>, tp: Array<B>, tr: Array<B>, l: (A, B) -> bool, r: (B, A) -> bool) -> bool {
		if (fp.length != tp.length) return false;
		if (fr.length != tr.length) return false;
		for (i < fp.length) {
			var f = fp[i], t = tp[i];
			if (!r(t, f)) return false; // contra-variant
		}
		for (i < fr.length) {
			var f = fr[i], t = tr[i];
			if (!l(f, t)) return false; // co-variant
		}
		return true;
	}
}