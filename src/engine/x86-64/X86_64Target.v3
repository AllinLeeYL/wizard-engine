// Copyright 2021 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Contains target-specific factory functions.
component Target {
	def V3_PARAM_GPRS = [X86_64Regs.RDI, X86_64Regs.RSI, X86_64Regs.RDX, X86_64Regs.RCX, X86_64Regs.R8, X86_64Regs.R9]; 		// System-V
	def V3_RET_GPRS = [X86_64Regs.RAX, X86_64Regs.RDX, X86_64Regs.RCX, X86_64Regs.RSI]; 			// System-V + 2

	def limit_memory_pages = 65536u;
	def newMemory = X86_64Memory.new;
	def forceGC = RiGc.forceGC;
	def reset = X86_64Interpreter.reset;
	def call = X86_64Interpreter.run;
	def getWasmTopFrame = X86_64Interpreter.getTopFrame;
	def getWasmCallStack = X86_64Interpreter.getCallStack;
	def getWasmCallDepth = X86_64Interpreter.getCallDepth;
	var asmSpcEntry: (WasmFunction, Pointer, Pointer) -> (ExecState, TrapReason);
	var globalCode: Mapping;
	def tagging = Tagging.new(true, false);

	new() {
		Execute.probes.onEnable = X86_64Interpreter.onProbeEnable;
		Execute.probes.onDisable = X86_64Interpreter.onProbeDisable;
	}

	def setMode(m: EngineMode) -> bool {
		match (m) {
			JIT => {
				Execute.mode = Mode.JIT;
				return true;
			}
			FAST => {
				Execute.mode = Mode.MIXED;
				return true;
			}
			INT, DEBUG => {
				// x86-64 target has only one execution mode; interpreter only
				Execute.mode = Mode.INTERPRETER;
				return true;
			}
		}
	}
	def modeHelp(m: EngineMode) -> string {
		match (m) {
			JIT => return "ahead-of-time compile all modules with the single-pass compiler (spc)";
			FAST => return "mixed mode with the fast interpreter (fast-int) and (spc)";
			INT, DEBUG => return "fast interpreter only (fast-int)";
		}
	}
	def finishModule(module: Module, err: ErrorGen) {
		if (Execute.mode == Mode.INTERPRETER) return; // interpreter needs no post-processing for a module
		spcCompileModule(module, err);
	}
	def spcCompileModule(module: Module, err: ErrorGen) {
		ensureSpcEntry();

		var extensions = Extension.set.all; // TODO
		var limits = Limits.new();
		var compiler = X86_64SinglePassCompiler.new(extensions, limits, X86_64Regs2.CONFIG, module);
		var w = compiler.w;
		w.put_b32(0xFFDDEECC); // ensure non-zero offset

		// generate code for all functions
		var starts = Array<int>.new(module.functions.length);
		for (i < module.functions.length) {
			var f = module.functions[i];
			if (f.imported()) continue;
			starts[i] = w.atEnd().pos;
			var compiled = compiler.gen(f);
			if (!compiled) {
				err.abs(i).FailedToCompile(i);
				starts[i] = -1;
			}
		}
		var oobMemoryHandlerOffset = w.atEnd().pos;
		compiler.emitTrapReturn(TrapReason.MEM_OUT_OF_BOUNDS);

		var divZeroHandlerOffset = w.atEnd().pos;
		compiler.emitTrapReturn(TrapReason.DIV_BY_ZERO);

		var stackOverflowHandlerOffset = w.atEnd().pos;
		compiler.emitTrapReturn(TrapReason.STACK_OVERFLOW);

		// copy and map code
		var masm = X86_64MacroAssembler.!(compiler.masm);
		var mapping = mapCode(masm.asm, masm.prepareToCopyInto);
		for (i < starts.length) {
			if (starts[i] > 0) {
				var addr = mapping.range.start + starts[i];
				if (Trace.compiler) Trace.OUT.put2("func[%d].spc_entry: break *0x%x", i, addr - Pointer.NULL).outln();
				setTargetCode(module.functions[i], addr);
			}
		}
		var code = X86_64SpcCode.new(mapping);
		code.oobMemoryHandlerOffset = oobMemoryHandlerOffset;
		code.divZeroHandlerOffset = divZeroHandlerOffset;
		code.stackOverflowHandlerOffset = stackOverflowHandlerOffset;

		module.target_module = TargetModule(code);
		RiRuntime.registerUserCode(code);
	}
	def setTargetCode(f: FuncDecl, addr: Pointer) {
		f.code.target_code = TargetCode(addr);
	}
	def callSpcEntry(wf: WasmFunction) -> (ExecState, TrapReason) {
		var valueStack = X86_64Interpreter.valueStack;
		return asmSpcEntry(wf, wf.decl.code.target_code.spc_entry, valueStack.sp);
	}
	def ensureSpcEntry() {
		if (asmSpcEntry == null) {
			var w = DataWriter.new();
			var asm = X86_64Assemblers.create64(w);
			var c = X86_64Regs2.CONFIG;
			def G = X86_64Regs2.toGpr;
			asm.movq_r_r(G(c.regs.func_arg), Target.V3_PARAM_GPRS[1]); // function
			asm.movq_r_r(G(c.regs.vsp), Target.V3_PARAM_GPRS[3]); // vsp
			asm.icall_r(Target.V3_PARAM_GPRS[2]);
			asm.ret();
			globalCode = mapCode(asm, null);
			asmSpcEntry = CiRuntime.forgeClosure<void, (WasmFunction, Pointer, Pointer), (ExecState, TrapReason)>(globalCode.range.start, ());
			if (Trace.compiler) Trace.OUT.put1("spc_entry: break *0x%x", globalCode.range.start - Pointer.NULL).outln();
		}
	}
	def genInterpreterIntoFile(filename: string) -> ErrorBuilder {
		var data = System.fileLoad(filename);
		var err = ErrorBuilder.new().puts("interpreter generator: ");
		if (data == null) return err.put1("could not load executable %s\n", filename);
		var ok = X86_64Interpreter.serializeInterpreterCodeIntoExecutable(data);
		if (ok == false) return err.put1("could not patch executable %s\n", filename);
		var fd = System.fileOpen(filename, false);
		if (fd < 0) return err.put1("could not write executable: %s\n", filename);
		System.fileWriteK(fd, data, 0, data.length);
		System.fileClose(fd);
		return null;
	}
	def hasBuiltinInterpreterTracing() -> bool {
		return false; // XXX: remove once --trace-int has full functionality with probes
	}
}

type TargetCode(spc_entry: Pointer) #unboxed { }
type TargetModule(code: X86_64SpcCode) #unboxed { }

def mapCode(asm: X86_64Assembler, prepare: (X86_64Assembler, u64) -> void) -> Mapping {
	var w = asm.w;
	var length = u64.view(w.atEnd().pos);
	var mapping = Mmap.reserve(length, Mmap.PROT_WRITE), range = mapping.range;
	if (prepare != null) prepare(asm, u64.view(range.start - Pointer.NULL));
	var t = range.start;
	var f = Pointer.atContents(w.data);
	for (i = 0; i < length; i += Pointer.SIZE) { // XXX: manual memcopy
		t.store<Pointer>(f.load<Pointer>());
		t += Pointer.SIZE;
		f += Pointer.SIZE;
	}
	Mmap.protect(range.start, u64.!(range.end - range.start), Mmap.PROT_READ | Mmap.PROT_EXEC);
	return mapping;
}
