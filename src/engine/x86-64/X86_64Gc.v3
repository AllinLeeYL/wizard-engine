// Copyright 2022 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Runtime functions called by the interpreter and JITed code.
component X86_64Gc {
	def runtime_STRUCT_NEW_CANON(instance: Instance, index: u32) { // XXX: unify with V3Interpreter
		var decl = StructDecl.!(instance.heaptypes[index]);
		var fields = Array<Value>.new(decl.field_types.length);
		for (i = fields.length - 1; i >= 0; i--) {
			fields[i] = X86_64Interpreter.valueStack.pop(decl.field_types[i].valtype);
		}
		X86_64Interpreter.valueStack.push(Value.Ref(HeapStruct.new(decl, fields)));
	}
	def runtime_STRUCT_NEW_CANON_DEFAULT(instance: Instance, index: u32) { // XXX: unify with V3Interpreter
		var decl = StructDecl.!(instance.heaptypes[index]);
		var fields = Array<Value>.new(decl.field_types.length);
		for (i < fields.length) {
			fields[i] = Values.default(decl.field_types[i].valtype);
		}
		X86_64Interpreter.valueStack.push(Value.Ref(HeapStruct.new(decl, fields)));
	}
	def runtime_STRUCT_GET(instance: Instance, sindex: u32, findex: u32) -> AbruptReturn { // XXX: unify with V3Interpreter
		var obj = HeapStruct.!(X86_64Interpreter.valueStack.popObject());
		if (obj == null) return Trap.new(TrapReason.NULL_DEREF, null, null);
		X86_64Interpreter.valueStack.push(obj.vals[findex]);
		return null;
	}
	def runtime_STRUCT_GET_S(instance: Instance, sindex: u32, findex: u32) -> AbruptReturn { // XXX: unify with V3Interpreter
		var obj = HeapStruct.!(X86_64Interpreter.valueStack.popObject());
		var decl = StructDecl.!(instance.heaptypes[int.view(sindex)]);
		if (obj == null) return Trap.new(TrapReason.NULL_DEREF, null, null);
		X86_64Interpreter.valueStack.push(V3Eval.signExtend(decl.field_types[findex], obj.vals[findex]));
		return null;
	}
	def runtime_STRUCT_GET_U(instance: Instance, sindex: u32, findex: u32) -> AbruptReturn { // XXX: unify with V3Interpreter
		var obj = HeapStruct.!(X86_64Interpreter.valueStack.popObject());
		var decl = StructDecl.!(instance.heaptypes[int.view(sindex)]);
		if (obj == null) return Trap.new(TrapReason.NULL_DEREF, null, null);
		X86_64Interpreter.valueStack.push(V3Eval.zeroExtend(decl.field_types[findex], obj.vals[findex]));
		return null;
	}
	def runtime_STRUCT_SET(instance: Instance, sindex: u32, findex: u32) -> AbruptReturn { // XXX: unify with V3Interpreter
		var decl = StructDecl.!(instance.heaptypes[int.view(sindex)]);
		var val = X86_64Interpreter.valueStack.pop(decl.field_types[findex].valtype);
		var obj = HeapStruct.!(X86_64Interpreter.valueStack.popObject());
		if (obj == null) return Trap.new(TrapReason.NULL_DEREF, null, null);
		obj.vals[findex] = val;
		return null;
	}
}