// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

class ErrorGen {
	var error_pos: int = int.max;
	var error_msg: string;
	var next_error_pos = -1;
	// Returns {true} if no error has been recorded yet.
	def ok() -> bool {
		return error_pos == int.max;
	}
	// Returns {true} if any error has been recorded yet.
	def error() -> bool {
		return error_pos < int.max;
	}
	// Set an error at the given position if no error has already been
	// recorded at an earlier position.
	def set(msg: string) -> bool {
		var pos = next_error_pos;
		if (pos < 0) System.error("ErrorGenError", "next error position not set");
		next_error_pos = -1;
		if (error_pos <= pos) return false;
		error_pos = pos;
		error_msg = msg;
		return true;
	}
	// Reset any errors currently recorded.
	def reset() -> this {
		error_pos = int.max;
		error_msg = null;
		next_error_pos = -1;
	}
	def at(d: Decoder) -> this {
		next_error_pos = d.abs_pos();
	}
	def rel(d: Decoder, pt: int) -> this {
		next_error_pos = d.abs(pt);
	}
	def abs(pt: int) -> this {
		next_error_pos = pt;
	}
}