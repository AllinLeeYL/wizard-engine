// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Executes {WasmFunction}s (which reference their enclosing {Instance})
// applied to arguments.
class Interpreter extends Executor {
	def val_stack = Stack<Value>.new();
	def call_stack = Stack<Frame>.new();
	def codeptr = Decoder.new(EMPTY, ERROR);
	var frame: Frame;	// cache of top-of-stack
	var state: ExecState;
	var trap_reason: TrapReason;
	var error_msg: string;
	var step_count: int;
	var max_call_depth = 10000;
	def trace = if(Trace.interpreter, StringBuilder.new());

	def run(count: int, f: Function, args: Array<Value>) -> Result {
		val_stack.resize(0);
		if (args != null) for (a in args) val_stack.push(a);
		if (HostFunction.?(f)) {
			state = ExecState.RUNNING;
			doCallHostFunction(HostFunction.!(f));
			if (state == ExecState.RUNNING) state = ExecState.FINISHED;
		} else {
			pushFrame(WasmFunction.!(f));
			loop(count);
		}
		match (state) {
			FINISHED => {
				var r = Array<Value>.new(f.sig.results.length);
				for (i < r.length) r[i] = val_stack.elems[i];
				return Result.Value(r);
			}
			TRAPPED => return Result.Trap(trap_reason);
			PAUSED, BREAKPOINT, TIMEOUT => return Result.Break;
			_ => System.error("InterpreterError", "unexpected state");
		}
		return Result.Break;
	}

	def pushFrame(f: WasmFunction) {
		if (call_stack.top >= max_call_depth) {
			return trap(TrapReason.STACK_OVERFLOW);
		}
		if (trace != null) traceFrame(f);
		var nf = call_stack.next();
		if (nf != null) {  // reuse cached Frame object if possible
			call_stack.top++;
		} else {
			nf = Frame.new();
			call_stack.push(nf);
		}
		var sp = val_stack.top;
		nf.fp = sp - f.decl.sig.params.length;
		val_stack.resize(sp + f.decl.code.num_locals);
		nf.func = f;
		// Initialize locals
		nf.pc = 0;
		var code = f.decl.code.code;
		codeptr.reset(code, 0, code.length);
		var count = codeptr.read_u32leb();
		for (i < count) {
			var d = codeptr.read_u32leb();
			var t = codeptr.read_u8();
			var val: Value;
			match (t) {
				BpTypecon.I32.code => val = Values.I32_0;
				BpTypecon.I64.code => val = Values.I64_0;
				BpTypecon.F32.code => val = Values.F32_0;
				BpTypecon.F64.code => val = Values.F64_0;
				_ => error("invalid local value type");
			}
			for (i < d) val_stack.elems[sp++] = val;
		}
		nf.pc = codeptr.pos;
		frame = nf;
	}
	def traceFrame(f: WasmFunction) {
		for (i < call_stack.top) trace.sp();
		trace.put2("pushFrame(func #%d: %q)\n",
			f.decl.index, ValueTypes.renderSig(_, f.decl.sig));
		trace.out(Trace.STDOUT);
		trace.reset();
	}
	private def loop(count: int) -> ExecState {
		match (state) {
			PAUSED,
			RUNNING,
			RESUME,
			STOPPED => state = ExecState.RUNNING;
			BREAKPOINT => state = ExecState.RESUME;
			FINISHED,
			TIMEOUT,
			TRAPPED => return state;
		}
		if (count <= 0) {
			// Run until exhaustion.
			while (true) {
				var completed = step();
				if (completed) step_count++;
				if (state != ExecState.RUNNING) break;
			}
		} else {
			// Run for a limited number of steps.
			while (true) {
				var completed = step();
				if (completed) {
					step_count++;
					count--;
					if (count <= 0) {
						if (state == ExecState.RUNNING) state = ExecState.TIMEOUT;
					}
				}
				if (state != ExecState.RUNNING) break;
			}
		}
		return state;
	}
	// Execute a single instruction. Return {true} if the instruction was completed.
	def step() -> bool {
		if (trace != null) traceStep();
		var pc = codeptr.pos;
		var opcode = codeptr.read_u8();
		while (true) {
			match (opcode) {
				Opcode.UNREACHABLE.code => {
					trap(TrapReason.UNREACHABLE);
				}
				Opcode.NOP.code => {
					// do nothing
				}
				Opcode.BLOCK.code,
				Opcode.LOOP.code => {
					codeptr.read_u32leb(); // skip blocktype
				}
				Opcode.IF.code => {
					codeptr.read_u32leb(); // skip blocktype
					var cond = popi32();
					if (cond == 0) codeptr.pos = doBranch(pc);
				}
				Opcode.ELSE.code => {
					codeptr.pos = doBranch(pc);
				}
				Opcode.END.code => {
					if (codeptr.available() == 0) doReturn();
				}
				Opcode.BR.code => {
					codeptr.pos = doBranch(pc);
				}
				Opcode.BR_IF.code => {
					var cond = popi32();
					if (cond == 0) codeptr.read_u32leb();  // read and skip label
					else codeptr.pos = doBranch(pc);
				}
				Opcode.BR_TABLE.code => {
					var key = popu32();
					codeptr.pos = doSwitch(pc, key);
				}
				Opcode.RETURN.code => {
					doReturn();
				}
				Opcode.CALL.code => {
					var func_index = codeptr.read_u32leb();
					doCall(func_index);
				}
				Opcode.CALL_INDIRECT.code => {
					var sig_index = codeptr.read_u32leb();
					var table_index = codeptr.read_u32leb();
					var func_index = popu32();
					doCallIndirect(sig_index, table_index, func_index);
				}
				Opcode.DROP.code => {
					pop();
				}
				Opcode.SELECT.code => {
					var cond = popu32(), b = pop(), a = pop();
					push(if(cond != 0, a, b));
				}
				Opcode.LOCAL_GET.code => {
					var index = codeptr.read_u32leb();
					push(val_stack.elems[frame.fp + int.!(index)]);
				}
				Opcode.LOCAL_SET.code => {
					var index = codeptr.read_u32leb();
					val_stack.elems[frame.fp + int.!(index)] = pop();
				}
				Opcode.LOCAL_TEE.code => {
					var index = codeptr.read_u32leb();
					val_stack.elems[frame.fp + int.!(index)] = val_stack.peek();
				}
				Opcode.GLOBAL_GET.code => {
					var index = codeptr.read_u32leb();
					push(frame.func.instance.globals[int.!(index)].value);
				}
				Opcode.GLOBAL_SET.code => {
					var index = codeptr.read_u32leb();
					frame.func.instance.globals[int.!(index)].value = pop();
				}

				Opcode.I32_LOAD.code => doLoad<u32, u32, u32>(Memory.read_u32, Value.I32);
				Opcode.I64_LOAD.code => doLoad<u64, u64, u64>(Memory.read_u64, Value.I64);
				Opcode.F32_LOAD.code => doLoad<u32, u32, u32>(Memory.read_u32, Value.F32);
				Opcode.F64_LOAD.code => doLoad<u64, u64, u64>(Memory.read_u64, Value.F64);
				Opcode.I32_LOAD8_S.code => doLoad<u8, i8, u32>(Memory.read_u8, Value.I32);
				Opcode.I32_LOAD8_U.code => doLoad<u8, u8, u32>(Memory.read_u8, Value.I32);
				Opcode.I32_LOAD16_S.code => doLoad<u16, i16, u32>(Memory.read_u16, Value.I32);
				Opcode.I32_LOAD16_U.code => doLoad<u16, u16, u32>(Memory.read_u16, Value.I32);
				Opcode.I64_LOAD8_S.code => doLoad<u8, i8, u64>(Memory.read_u8, Value.I64);
				Opcode.I64_LOAD8_U.code => doLoad<u8, u8, u64>(Memory.read_u8, Value.I64);
				Opcode.I64_LOAD16_S.code => doLoad<u16, i16, u64>(Memory.read_u16, Value.I64);
				Opcode.I64_LOAD16_U.code => doLoad<u16, u16, u64>(Memory.read_u16, Value.I64);
				Opcode.I64_LOAD32_S.code => doLoad<u32, i32, u64>(Memory.read_u32, Value.I64);
				Opcode.I64_LOAD32_U.code => doLoad<u32, u32, u64>(Memory.read_u32, Value.I64);
				Opcode.I32_STORE.code => doStore<u32, u32>(Memory.write_u32, Values.v_u);
				Opcode.I64_STORE.code => doStore<u64, u64>(Memory.write_u64, Values.v_w);
				Opcode.F32_STORE.code => doStore<u32, u32>(Memory.write_u32, Values.v_f);
				Opcode.F64_STORE.code => doStore<u64, u64>(Memory.write_u64, Values.v_d);
				Opcode.I32_STORE8.code => doStore<u8, u32>(Memory.write_u8, Values.v_u);
				Opcode.I32_STORE16.code => doStore<u16, u32>(Memory.write_u16, Values.v_u);
				Opcode.I64_STORE8.code => doStore<u8, u64>(Memory.write_u8, Values.v_w);
				Opcode.I64_STORE16.code => doStore<u16, u64>(Memory.write_u16, Values.v_w);
				Opcode.I64_STORE32.code => doStore<u32, u64>(Memory.write_u32, Values.v_w);

				Opcode.MEMORY_SIZE.code => {
					var index = codeptr.read_u32leb();
					var pages = frame.func.instance.memories[int.!(index)].size();
					pushu32(pages);
				}
				Opcode.MEMORY_GROW.code => {
					var index = codeptr.read_u32leb();
					var result = frame.func.instance.memories[int.!(index)].grow(popu32());
					pushu32(u32.!(result));
				}
				Opcode.I32_CONST.code => {
					var val = codeptr.read_i32leb();
					pushi32(val);
				}
				Opcode.I64_CONST.code => {
					var val = codeptr.read_i64leb();
					pushi64(val);
				}
				Opcode.F32_CONST.code => {
					var val = codeptr.read_u32();
					push(Value.F32(val));
				}
				Opcode.F64_CONST.code => {
					var val = codeptr.read_u64();
					push(Value.F64(val));
				}

				Opcode.I32_EQZ.code => pushbool(popu32() == 0);
				Opcode.I32_EQ.code => do_uu_z(u32.==);
				Opcode.I32_NE.code => do_uu_z(u32.!=);
				Opcode.I32_LT_S.code => do_ii_z(i32.<);
				Opcode.I32_LT_U.code => do_uu_z(u32.<);
				Opcode.I32_GT_S.code => do_ii_z(i32.>);
				Opcode.I32_GT_U.code => do_uu_z(u32.>);
				Opcode.I32_LE_S.code => do_ii_z(i32.<=);
				Opcode.I32_LE_U.code => do_uu_z(u32.<=);
				Opcode.I32_GE_S.code => do_ii_z(i32.>=);
				Opcode.I32_GE_U.code => do_uu_z(u32.>=);

				Opcode.I64_EQZ.code => pushbool(popu64() == 0);
				Opcode.I64_EQ.code => do_ww_z(u64.==);
				Opcode.I64_NE.code => do_ww_z(u64.!=);
				Opcode.I64_LT_S.code => do_ll_z(i64.<);
				Opcode.I64_LT_U.code => do_ww_z(u64.<);
				Opcode.I64_GT_S.code => do_ll_z(i64.>);
				Opcode.I64_GT_U.code => do_ww_z(u64.>);
				Opcode.I64_LE_S.code => do_ll_z(i64.<=);
				Opcode.I64_LE_U.code => do_ww_z(u64.<=);
				Opcode.I64_GE_S.code => do_ll_z(i64.>=);
				Opcode.I64_GE_U.code => do_ww_z(u64.>=);

				// TODO: floating point comparisons

				Opcode.I32_CLZ.code => do_u_u(ExecUtil.I32_CLZ);
				Opcode.I32_CTZ.code => do_u_u(ExecUtil.I32_CTZ);
				Opcode.I32_POPCNT.code => do_u_u(ExecUtil.I32_POPCNT);
				Opcode.I32_ADD.code => do_uu_u(u32.+);
				Opcode.I32_SUB.code => do_uu_u(u32.-);
				Opcode.I32_MUL.code => do_uu_u(u32.*);
				Opcode.I32_DIV_S.code => {
					var y = popi32(), x = popi32();
					if (y == 0) trap(TrapReason.DIV_BY_ZERO);
					else if (y == -1 && x == int.min) trap(TrapReason.DIV_UNREPRESENTABLE);
					else pushi32(x / y);
				}
				Opcode.I32_DIV_U.code => {
					var y = popu32(), x = popu32();
					if (y == 0) trap(TrapReason.DIV_BY_ZERO);
					else pushu32(x / y);
				}
				Opcode.I32_REM_S.code => {
					var y = popi32(), x = popi32();
					if (y == 0) trap(TrapReason.REM_BY_ZERO);
					else if (y == -1) pushi32(0);
					else pushi32(x % y);
				}
				Opcode.I32_REM_U.code => {
					var y = popu32(), x = popu32();
					if (y == 0) trap(TrapReason.REM_BY_ZERO);
					else if (y == 1) pushi32(0);
					else pushu32(x % y);
				}
				Opcode.I32_AND.code => do_uu_u(u32.&);
				Opcode.I32_OR.code => do_uu_u(u32.|);
				Opcode.I32_XOR.code => do_uu_u(u32.^);
				Opcode.I32_SHL.code => do_uu_u(ExecUtil.I32_SHL);
				Opcode.I32_SHR_S.code => do_ii_i(ExecUtil.I32_SHR_S);
				Opcode.I32_SHR_U.code => do_uu_u(ExecUtil.I32_SHR_U);
				Opcode.I32_ROTL.code => do_uu_u(ExecUtil.I32_ROTL);
				Opcode.I32_ROTR.code => do_uu_u(ExecUtil.I32_ROTR);

				Opcode.I64_CLZ.code => do_w_w(ExecUtil.I64_CLZ);
				Opcode.I64_CTZ.code => do_w_w(ExecUtil.I64_CTZ);
				Opcode.I64_POPCNT.code => do_w_w(ExecUtil.I64_POPCNT);

				Opcode.I64_ADD.code => do_ww_w(u64.+);
				Opcode.I64_SUB.code => do_ww_w(u64.-);
				Opcode.I64_MUL.code => do_ww_w(u64.*);
				Opcode.I64_DIV_S.code => {
					var y = popi64(), x = popi64();
					if (y == 0) trap(TrapReason.DIV_BY_ZERO);
					else if (y == -1 && x == long.min) trap(TrapReason.DIV_UNREPRESENTABLE);
					else pushi64(x / y);
				}
				Opcode.I64_DIV_U.code => {
					var y = popu64(), x = popu64();
					if (y == 0) trap(TrapReason.DIV_BY_ZERO);
					else pushu64(x / y);
				}
				Opcode.I64_REM_S.code => {
					var y = popi64(), x = popi64();
					if (y == 0) trap(TrapReason.REM_BY_ZERO);
					else if (y == -1) pushu64(0);
					else pushi64(x % y);
				}
				Opcode.I64_REM_U.code => {
					var y = popu64(), x = popu64();
					if (y == 0) trap(TrapReason.REM_BY_ZERO);
					else if (y == 1) pushu64(0);
					else pushu64(x % y);
				}
				Opcode.I64_AND.code => do_ww_w(u64.&);
				Opcode.I64_OR.code => do_ww_w(u64.|);
				Opcode.I64_XOR.code => do_ww_w(u64.^);
				Opcode.I64_SHL.code => do_ww_w(ExecUtil.I64_SHL);
				Opcode.I64_SHR_S.code => do_ll_l(ExecUtil.I64_SHR_S);
				Opcode.I64_SHR_U.code => do_ww_w(ExecUtil.I64_SHR_U);
				Opcode.I64_ROTL.code => do_ww_w(ExecUtil.I64_ROTL);
				Opcode.I64_ROTR.code => do_ww_w(ExecUtil.I64_ROTR);

				// TODO: floating point arithmetic

				Opcode.I32_WRAP_I64.code => pushu32(u32.!(popu64()));

				// TODO: floating point conversions

				Opcode.I64_EXTEND_I32_S.code => pushu64(u64.!(i64.!(popi32())));
				Opcode.I64_EXTEND_I32_U.code => pushu64(popu32());

				Opcode.I32_REINTERPRET_F32.code => {
					var x = Value.F32.!(pop());
					pushu32(x.bits);
				}
				Opcode.F32_REINTERPRET_I32.code => {
					push(Value.F32(popu32()));
				}
				Opcode.I64_REINTERPRET_F64.code => {
					var x = Value.F64.!(pop());
					pushu64(x.bits);
				}
				Opcode.F64_REINTERPRET_I64.code => {
					push(Value.F64(popu64()));
				}

				InternalOpcode.BREAK.code => {
					if (state == ExecState.RUNNING) {
						state = ExecState.BREAKPOINT;
						return false; // did not execute instruction
					} else {
						var orig_opcode = frame.func.decl.code.orig[pc];
						opcode = orig_opcode;
						continue;  // re-execute switch with original opcode
					}
				}
				InternalOpcode.PROBE.code => ; // TODO
				_ => {
					if (Opcodes.valid[opcode]) {
						trap(TrapReason.UNIMPLEMENTED);
					} else {
						error("invalid opcode");
					}
				}
			}
			if (frame != null) frame.pc = codeptr.pos;
			break;
		}
		return true;
	}
	def doLoad<RT, VT, T>(load: (Memory, u32, u32) -> (TrapReason, RT), box: T -> Value) {
		var flags = codeptr.read_u32leb();
		var offset = codeptr.read_u32leb();
		var mem_index = 0; // TODO
		var index = popu32();
		var memory = frame.func.instance.memories[int.!(mem_index)];
		var t = load(memory, offset, index);
		if (t.0 != TrapReason.NONE) trap(t.0);
		else push(box(T.!(VT.!(t.1))));
	}
	def doStore<VT, T>(store: (Memory, u32, u32, VT) -> TrapReason, unbox: Value -> T) {
		var flags = codeptr.read_u32leb();
		var offset = codeptr.read_u32leb();
		var mem_index = 0; // TODO
		var val = pop();
		var index = popu32();
		var memory = frame.func.instance.memories[int.!(mem_index)];
		var t = store(memory, offset, index, VT.!(unbox(val)));
		if (t != TrapReason.NONE) trap(t);
	}
	def doBranch(pc: int) -> int {
		var t = findCtlXfer(pc);
		if (t.0 == null) {
			error("control transfer not found");
			return pc;
		}
		var e = t.0[t.1];
		var pc_delta = e.1, valcount = e.2, popcount = e.3;
		if (popcount > 0) {
			copyVals(valcount, val_stack.top - int.!(popcount + valcount));
			val_stack.top -= popcount;
		}
		return pc + pc_delta;
	}
	def doSwitch(pc: int, key: u32) -> int {
		var t = findCtlXfer(pc);
		if (t.0 == null) {
			error("control transfer not found");
			return pc;
		}
		var i = t.1, e = t.0[i], max = e.1;
		var off = 1 + if(key >= max, max - 1, int.!(key));
		e = t.0[i + off];
		var pc_delta = e.1, valcount = e.2, popcount = e.3;
		if (popcount > 0) {
			copyVals(valcount, val_stack.top - int.!(popcount + valcount));
			val_stack.top -= popcount;
		}
		return pc + off + pc_delta;
	}
	def findCtlXfer(pc: int) -> (Array<(u23, i23, u10, u23)>, int) {
		var c = frame.func.decl.code.ctl_xfer;
		for (i < c.length) {
			if (c[i].0 == pc) return (c, i);
		}
		return (null, -1);
	}
	def doReturn() {
		var count = frame.func.decl.sig.results.length;
		copyVals(count, frame.fp);
		val_stack.resize(frame.fp + count);
		call_stack.pop();
		frame = call_stack.peek();
		if (frame == null) {
			state = ExecState.FINISHED;
			codeptr.reset(null, 0, 0);
		} else {
			// Advance past caller's call instruction.
			var code = frame.func.decl.code.code;
			codeptr.reset(code, frame.pc, code.length - frame.pc);
			var opcode = codeptr.read_u8();
			match (opcode) {
				Opcode.CALL.code => {
					codeptr.read_u32leb();
				}
				Opcode.CALL_INDIRECT.code => {
					codeptr.read_u32leb();
					codeptr.read_u32leb();
				}
			}
			frame.pc = codeptr.pos;
		}
	}
	def doCall(func_index: u32) {
		var f = frame.func.instance.getFunction(int.!(func_index));
		doCallFunction(f);
	}
	def doCallIndirect(sig_index: u32, table_index: u32, func_index: u32) {
		var table = FunctionTable.!(frame.func.instance.tables[int.!(table_index)]);
		if (func_index >= table.elems.length) {
			return trap(TrapReason.FUNC_INVALID);
		}
		var f = table.elems[int.!(func_index)];
		if (f == null) {
			return trap(TrapReason.FUNC_INVALID);
		}
		var sig = TypeDecl.Function.!(frame.func.instance.module.types[int.!(sig_index)]);
		if (!ValueTypes.isAssignableSig(f.sig, sig)) {
			return trap(TrapReason.FUNC_SIG_MISMATCH);
		}
		doCallFunction(f);
	}
	def doCallFunction(f: Function) {
		match (f) {
			hf: HostFunction => doCallHostFunction(hf);
			wf: WasmFunction => pushFrame(wf);
		}
	}
	def doCallHostFunction(hf: HostFunction) {
		var count = hf.sig.params.length;
		var args = Array<Value>.new(count);
		var sp = val_stack.top - count;
		for (i < count) args[i] = val_stack.elems[sp + i];
		var results = hf.invoke(this, args);
		count = hf.sig.results.length;
		val_stack.resize(sp + count);
		for (i < count) val_stack.elems[sp + i] = results[i];
	}
	def trap(reason: TrapReason) {
		state = ExecState.TRAPPED;
		trap_reason = reason;
	}
	def pause();
	def error(msg: string) {
		state = ExecState.TRAPPED;
		trap_reason = TrapReason.ERROR;
		error_msg = msg;
	}
	def copyVals(count: int, dest: int) {
		var sp = val_stack.top - count;
		if (sp != dest) {
			var e = val_stack.elems;
			for (i < count) e[dest + i] = e[sp + i];
		}
	}
	def pop() -> Value {
		return val_stack.pop();
	}
	def popi32() -> i32 { return Values.v_i(val_stack.pop()); }
	def popu32() -> u32 { return Values.v_u(val_stack.pop()); }
	def popi64() -> i64 { return Values.v_l(val_stack.pop()); }
	def popu64() -> u64 { return Values.v_w(val_stack.pop()); }
	def push(val: Value) {
		val_stack.push(val);
	}
	def pushbool(val: bool) {
		val_stack.push(if(val, Values.I32_1, Values.I32_0));
	}
	def pushu32(val: u32) {
		val_stack.push(Value.I32(val));
	}
	def pushi32(val: i32) {
		val_stack.push(Value.I32(u32.!(val)));
	}
	def pushu64(val: u64) {
		val_stack.push(Value.I64(val));
	}
	def pushi64(val: i64) {
		val_stack.push(Value.I64(u64.!(val)));
	}
	def traceStep() {
		for (i < call_stack.top) trace.sp();
		trace.put1("%x: ", codeptr.pos);
		Opcodes.renderInstr(trace, frame.func.instance.module, codeptr);
		trace.ln();
		trace.out(Trace.STDOUT);
		trace.reset();
	}
	def do_ii_i(f: (i32, i32) -> i32) {
		var y = Values.v_i(val_stack.elems[val_stack.top-1]);
		var x = Values.v_i(val_stack.elems[val_stack.top-2]);
		var r = f(x, y);
		val_stack.elems[val_stack.top-2] = Value.I32(u32.!(r));
		val_stack.top--;
	}
	def do_uu_u(f: (u32, u32) -> u32) {
		var y = Values.v_u(val_stack.elems[val_stack.top-1]);
		var x = Values.v_u(val_stack.elems[val_stack.top-2]);
		var r = f(x, y);
		val_stack.elems[val_stack.top-2] = Value.I32(r);
		val_stack.top--;
	}
	def do_u_u(f: u32 -> u32) {
		var x = Values.v_u(val_stack.elems[val_stack.top-1]);
		var r = f(x);
		val_stack.elems[val_stack.top-1] = Value.I32(r);
	}
	def do_ii_z(f: (i32, i32) -> bool) {
		var y = Values.v_i(val_stack.elems[val_stack.top-1]);
		var x = Values.v_i(val_stack.elems[val_stack.top-2]);
		var r = f(x, y);
		val_stack.elems[val_stack.top-2] = if(r, Values.I32_1, Values.I32_0);
		val_stack.top--;
	}
	def do_uu_z(f: (u32, u32) -> bool) {
		var y = Values.v_u(val_stack.elems[val_stack.top-1]);
		var x = Values.v_u(val_stack.elems[val_stack.top-2]);
		var r = f(x, y);
		val_stack.elems[val_stack.top-2] = if(r, Values.I32_1, Values.I32_0);
		val_stack.top--;
	}
	def do_ll_l(f: (i64, i64) -> i64) {
		var y = Values.v_l(val_stack.elems[val_stack.top-1]);
		var x = Values.v_l(val_stack.elems[val_stack.top-2]);
		var r = f(x, y);
		val_stack.elems[val_stack.top-2] = Value.I64(u64.!(r));
		val_stack.top--;
	}
	def do_ww_w(f: (u64, u64) -> u64) {
		var y = Values.v_w(val_stack.elems[val_stack.top-1]);
		var x = Values.v_w(val_stack.elems[val_stack.top-2]);
		var r = f(x, y);
		val_stack.elems[val_stack.top-2] = Value.I64(r);
		val_stack.top--;
	}
	def do_w_w(f: u64 -> u64) {
		var x = Values.v_w(val_stack.elems[val_stack.top-1]);
		var r = f(x);
		val_stack.elems[val_stack.top-1] = Value.I64(r);
	}
	def do_ll_z(f: (i64, i64) -> bool) {
		var y = Values.v_l(val_stack.elems[val_stack.top-1]);
		var x = Values.v_l(val_stack.elems[val_stack.top-2]);
		var r = f(x, y);
		val_stack.elems[val_stack.top-2] = if(r, Values.I32_1, Values.I32_0);
		val_stack.top--;
	}
	def do_ww_z(f: (u64, u64) -> bool) {
		var y = Values.v_w(val_stack.elems[val_stack.top-1]);
		var x = Values.v_w(val_stack.elems[val_stack.top-2]);
		var r = f(x, y);
		val_stack.elems[val_stack.top-2] = if(r, Values.I32_1, Values.I32_0);
		val_stack.top--;
	}
}
enum ExecState {
	STOPPED,	// execution has not yet begun
	RUNNING,	// currently running
	BREAKPOINT,	// stopped at (code) breakpoint
	RESUME,		// continuing over current breakpoint
	PAUSED,		// paused by probe
	FINISHED,	// execution completed bottom-most function
	TIMEOUT,	// execution timed out
	TRAPPED		// execution encountered a trap
}
class Frame {
	var func: WasmFunction;
	var fp: int;
	var pc: int;
}
enum InternalOpcode(code: u8) {
	BREAK(0x12),
	PROBE(0x13)
}
type Result {
	case Break {}
	case Value(vals: Array<Value>) {}
	case Trap(reason: TrapReason) {}
	def equals(that: Result) -> bool {
		if (this == that) return true;
		if (Result.Value.?(this) && Result.Value.?(that)) {
			var a = Result.Value.!(this).vals;
			var b = Result.Value.!(that).vals;
			if (a.length != b.length) return false;
			for (i < a.length) if (a[i] != b[i]) return false;
			return true;
		}
		return false;
	}
	def render(buf: StringBuilder) -> StringBuilder {
		match (this) {
			Break => buf.puts("break");
			Value(vals) => {
				if (vals.length == 1) {
					Values.render(buf, vals[0]);
				} else {
					buf.puts("[");
					for (i < vals.length) {
						if (i > 0) buf.puts(", ");
						Values.render(buf, vals[i]);
					}
					buf.puts("]");
				}
			}
			Trap(reason) => buf.put1("trap %s", reason.name);
		}
		return buf;
	}
}
component ExecUtil {
	def I32_CLZ(x: u32) -> u32 {
		var count = 0u;
		if (x == 0) return 32;
		while ((x & 0x80000000u) == 0) { count++; x <<= 1; }
		return count;
	}
	def I32_CTZ(x: u32) -> u32 {
		var count = 0u;
		if (x == 0) return 32;
		while ((x & 1u) == 0) { count++; x >>= 1; }
		return count;
	}
	def I32_POPCNT(x: u32) -> u32 {
		var count = 0u;
		for (i < 32) {
			if ((x & 1) == 1) count++;
			x >>= 1;
		}
		return count;
	}
	def I32_ROTL(x: u32, z: u32) -> u32 {
		var y = u5.!(z);
		if (y != 0) {
			var upper = x << y;
			var lower = x >> byte.!(32) - y;
			x = upper | lower;
		}
		return x;
	}
	def I32_ROTR(x: u32, z: u32) -> u32 {
		var y = u5.!(z);
		if (y != 0) {
			var upper = x << byte.!(32) - y;
			var lower = x >> y;
			x = upper | lower;
		}
		return x;
	}
	def I64_CLZ(x: u64) -> u64 {
		var count = 0u;
		if (x == 0) return 64;
		while ((x & 0x8000000000000000ul) == 0) { count++; x <<= 1; }
		return count;
	}
	def I64_CTZ(x: u64) -> u64 {
		var count = 0u;
		if (x == 0) return 64;
		while ((x & 1u) == 0) { count++; x >>= 1; }
		return count;
	}
	def I64_POPCNT(x: u64) -> u64 {
		var count = 0u;
		for (i < 64) {
			if ((x & 1) == 1) count++;
			x >>= 1;
		}
		return count;
	}
	def I64_ROTL(x: u64, z: u64) -> u64 {
		var y = u6.!(z);
		if (y != 0) {
			var upper = x << y;
			var lower = x >> byte.!(64) - y;
			x = upper | lower;
		}
		return x;
	}
	def I64_ROTR(x: u64, z: u64) -> u64 {
		var y = u6.!(z);
		if (y != 0) {
			var upper = x << byte.!(64) - y;
			var lower = x >> y;
			x = upper | lower;
		}
		return x;
	}
	def I32_SHL(x: u32, y: u32) -> u32 {
		return x << u5.!(y);
	}
	def I32_SHR_S(x: i32, y: i32) -> i32 {
		return x >> u5.!(y);
	}
	def I32_SHR_U(x: u32, y: u32) -> u32 {
		return x >> u5.!(y);
	}
	def I64_SHL(x: u64, y: u64) -> u64 {
		return x << u6.!(y);
	}
	def I64_SHR_S(x: i64, y: i64) -> i64 {
		return x >> u6.!(y);
	}
	def I64_SHR_U(x: u64, y: u64) -> u64 {
		return x >> u6.!(y);
	}
}
def EMPTY: Array<byte> = [];
def ERROR = ErrorGen.new();
