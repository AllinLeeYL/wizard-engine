// Copyright 2023 Wizard Authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A simple host module that offers functions that implement saturating arithmetic.
// Demonstrates the use of the {HostModuleBuilder} utility that handles adaption between
// V3 functions and Wasm, as well as registering the module with the global registry.
def X_ = HostModuleBuilder.new("wizeng:arith")
	.func("i32.sat_add_s", SigCache.ii_i, HostAdapters.ii_i(I32_SAT_ADD_S))
	.func("i32.sat_add_u", SigCache.ii_i, HostAdapters.uu_u(I32_SAT_ADD_U))
	.register(false);

// Exposed: implements signed 32-bit integer saturation addition.
def I32_SAT_ADD_S(x: int, y: int) -> int {
	var zz = long.!(x) + long.!(y);
	if (zz < int.min) return int.min;
	if (zz > int.max) return int.max;
	return int.!(zz);
}

// Exposed: implements unsigned 32-bit integer saturation addition.
def I32_SAT_ADD_U(x: u32, y: u32) -> u32 {
	var zz = u64.!(x) + u64.!(y);
	if (zz > u32.max) return u32.max;
	return u32.!(zz);
}
