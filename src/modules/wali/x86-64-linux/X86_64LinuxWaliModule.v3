// Copyright 2024 Wizard authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def unused_ = HostModuleBuilderOf<WaliInstance>.new("wali", WaliInstance.new)
	.func_M("SYS_read", SigCache.iii_l, WaliInstance.read)
	.func_M("SYS_write", SigCache.iii_l, WaliInstance.write)
	.postprocess(WaliInstance.setMemory)
	.init(init)
	.getMain(getMain)
	.register(false);

class WaliInstance {
	private var memory: Memory;
	private def fdmap = FileDescriptorMap.new(MAX_FDS); // per-instance: fd table
	// TODO: per-instance memory mappings

	def setMemory(instance: Instance) {
		// Find memory and save internally
		memory = instance.findExportOfType<Memory>(null);
	}

	// Exported functions.
	def read(args: Range<Value>) -> HostResult {
		var fd = Values.v_i(args[0]);
		var ptr = Values.v_u(args[1]);
		var len = Values.v_u(args[2]);
		var range = memory.range_ol_32(ptr, len);
		if (range.reason != TrapReason.NONE) return HostResult.Throw(Trap.new(range.reason, null, null));
		var sysfd = fdmap.get(fd);
		if (sysfd < 0) return MINUS_1;
		var r = Linux.syscall(LinuxConst.SYS_read, (sysfd, Pointer.atContents(range.result), len));
		return HostResult.Value1(Values.l_v(r.0));
	}
	def write(args: Range<Value>) -> HostResult {
		var fd = Values.v_i(args[0]);
		var ptr = Values.v_u(args[1]);
		var len = Values.v_u(args[2]);
		var range = memory.range_ol_32(ptr, len);
		if (range.reason != TrapReason.NONE) return HostResult.Throw(Trap.new(range.reason, null, null));
		var sysfd = fdmap.get(fd);
		if (sysfd < 0) return MINUS_1;
		var r = Linux.syscall(LinuxConst.SYS_write, (sysfd, Pointer.atContents(range.result), len));
		return HostResult.Value1(Values.l_v(r.0));
	}
}

def MAX_FDS = 1000;
var cmdline_args: Array<string>; // global: environment
var cmdline_env: Array<string>; // global: environment, TODO
var trace: bool;

def init(args: Array<string>, t: bool, err: ErrorGen) {
	cmdline_args = args;
	trace = t;
}

def getMain(instance: Instance) -> (Function, Array<Value>) {
	var main = instance.findExportOfType<Function>(GlobMatcher.new("main"));
	if (main == null) {
		for (iex in instance.exports) {
			match (iex) {
				x: Function => {
					// If more than one exported function, don't know
					if (main != null) return (null, null);
					main = x;
				}
			}
		}
	}
	if (main == null) return (null, null);
	if (Arrays.equal(main.sig.params, SigCache.arr_i)) return (main, [Values.i_v(cmdline_args.length)]);
	return (main, Arrays.map(main.sig.params, Values.default));
}

def MINUS_1 = HostResult.Value1(Value.I64(0xFFFFFFFF_FFFFFFFFuL));


layout WaliStruct_stat { // TODO
	=0;
}
