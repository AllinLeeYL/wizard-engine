// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Enumeration of the exported functions
def OUT = Trace.OUT;
enum WaveExports(sig: SigDecl, invoke: (WaveModule, Array<Value>) -> HostResult) {
	arg_len(SigCache.i_i, WaveModule.arg_len),
	arg_copy(SigCache.iii_i, WaveModule.arg_copy),
	fs_size(SigCache.ii_i, WaveModule.fs_size),
	fs_chmod(SigCache.iii_i, WaveModule.fs_chmod),
	fs_open(SigCache.iii_i, WaveModule.fs_open),
	fs_read(SigCache.iii_i, WaveModule.fs_read),
	fs_write(SigCache.iii_i, WaveModule.fs_write),
	fs_avail(SigCache.i_i, WaveModule.fs_avail),
	fs_close(SigCache.i_v, WaveModule.fs_close),
	ticks_ms(SigCache.v_i, WaveModule.ticks_ms),
	ticks_us(SigCache.v_i, WaveModule.ticks_us),
	ticks_ns(SigCache.v_i, WaveModule.ticks_ns),
	throw_ex(SigCache.iiii_v, WaveModule.throw_ex)
}

// Implements the "wave" module interface for programs compiled from Virgil.
class WaveModule extends HostModule("wave") {
	private var fdmap: FileDescriptorMap;
	private var lastfd: int;
	private def oob = HostResult.Throw(Trap.new(TrapReason.MEM_OUT_OF_BOUNDS, null, null));
	private var memory: Memory;
	private var cmdline_args: Array<string>;
	private var trace: bool;

	def init(args: Array<string>, trace: bool) -> HostResult {
		this.fdmap = FileDescriptorMap.new(MAX_FDS);
		this.cmdline_args = args;
		this.trace = trace;
		return i(0);
	}

	def preprocess(module: Module, result: Array<Exportable>) {
		var imports = module.imports;
		for (j < imports.length) {
			var i = imports[j];
			if (!Strings.equal(this.name, i.imp.module_name)) continue;
			if (!FuncDecl.?(i)) continue;
			var impf = FuncDecl.!(i);
			for (ex in WaveExports) {
				if (Strings.equal(ex.name, i.imp.field_name)) {
					var func = ex.invoke(this, _);
					result[j] = HostFunctionN.new(ex.name, ex.sig, func);
					break;
				}
			}
		}
	}
	def postprocess(instance: Instance) {
		// Find memory and save internally
		var module = instance.module;
		var exports = module.exports;
		for (j < exports.length) {
			var mex = module.exports[j], iex = instance.exports[j];
			if (Memory.?(iex)) {
				memory = Memory.!(iex);
				return;
			}
		}
	}
	def getEntry(instance: Instance) -> (Function, Array<Value>) {
		var module = instance.module;
		var exports = module.exports;
		var found: Function;
		var count = 0;
		for (j < exports.length) {
			var mex = module.exports[j], iex = instance.exports[j];
			if (!Function.?(iex)) continue;
			found = Function.!(iex);
			if (Strings.equal("main", mex.0)) return toEntry(found); // explicit name match
			count++;
		}
		return if(count == 1, toEntry(found)); // if only one exported function, assume it is main
	}
	def toEntry(main: Function) -> (Function, Array<Value>) {
		if (Arrays.equal(main.sig.params, SigCache.arr_i)) return (main, [Values.i_v(cmdline_args.length)]);
		return (main, Arrays.map(main.sig.params, Values.default));
	}
	def deinit(result: Result) {
		fdmap.closeAll();
	}

	// ==={Provided functions}============================================
	def arg_len(args: Array<Value>) -> HostResult {
		var arg = Values.v_u(args[0]);
		if (arg >= cmdline_args.length) return MINUS_1;
		return i(cmdline_args[arg].length);
	}
	def arg_copy(args: Array<Value>) -> HostResult {
		if (memory == null) return oob;
		var arg = Values.v_u(args[0]);
		var ptr = Values.v_u(args[1]);
		var len = Values.v_u(args[2]);
		if (arg >= cmdline_args.length) return MINUS_1;
		var data = cmdline_args[arg];
		if (len > data.length) len = u32.!(data.length);
		var i = memory.boundsCheck(0, ptr, len);
		if (i < 0) return oob;
		memory.copyIn(ptr, data, 0, len);
		return this.i(int.!(len));
	}

	def fs_size(args: Array<Value>) -> HostResult {
		if (memory == null) return oob;
		var ptr = Values.v_u(args[0]);
		var len = Values.v_u(args[1]);
		var path = memory.extract(ptr, len);
		if (path == null) return oob;
		var fd = System.fileOpen(path, true);
		if (fd < 0) return i(0);
		var size = System.fileLeft(fd);
		System.fileClose(fd);
		return i(size);
	}
	def fs_chmod(args: Array<Value>) -> HostResult {
		if (memory == null) return oob;
		var ptr = Values.v_u(args[0]);
		var len = Values.v_u(args[1]);
		var perm = Values.v_u(args[2]);
		var path = memory.extract(ptr, len);
		if (path == null) return MINUS_1;
		System.chmod(path, int.view(perm));
		return i(0);
	}
	def fs_open(args: Array<Value>) -> HostResult {
		if (memory == null) return oob;
		var ptr = Values.v_u(args[0]);
		var len = Values.v_u(args[1]);
		var mode = Values.v_u(args[2]);
		var path = memory.extract(ptr, len);
		if (path == null) return MINUS_1;
		var fd = fdmap.alloc();
		if (fd < 0) return MINUS_1; // out of file descriptors
		var s = System.fileOpen(path, mode == 0);
		if (s < 0) return i(s);
		fdmap.set(fd, s);
		return i(fd);
	}
	def fs_read(args: Array<Value>) -> HostResult {
		if (memory == null) return oob;
		var fd = Values.v_i(args[0]);
		var buf = Values.v_u(args[1]);
		var len = Values.v_u(args[2]);
		var s = fdmap.get(fd);
		if (s < 0) return MINUS_1;
		return i(memory.readIn(s, buf, len));
	}
	def fs_write(args: Array<Value>) -> HostResult {
		if (memory == null) return oob;
		var fd = Values.v_i(args[0]);
		var buf = Values.v_u(args[1]);
		var len = Values.v_u(args[2]);
		var s = fdmap.get(fd);
		if (s < 0) return MINUS_1;
		return i(memory.writeOut(s, buf, len));
	}
	def fs_avail(args: Array<Value>) -> HostResult {
		var fd = Values.v_i(args[0]);
		var s = fdmap.get(fd);
		if (s < 0) return MINUS_1;
		var r = System.fileLeft(s);
		return i(r);
	}
	def fs_close(args: Array<Value>) -> HostResult {
		var fd = Values.v_i(args[0]);
		var s = fdmap.get(fd);
		if (s < 0) return MINUS_1;
		System.fileClose(s);
		fdmap.free(fd);
		return NONE;
	}
	def ticks_ms(args: Array<Value>) -> HostResult {
		return i(System.ticksMs());
	}
	def ticks_us(args: Array<Value>) -> HostResult {
		return i(System.ticksUs());
	}
	def ticks_ns(args: Array<Value>) -> HostResult {
		return i(System.ticksNs());
	}
	def throw_ex(args: Array<Value>) -> HostResult {
		if (memory == null) return oob;
		var ex = Values.v_u(args[0]);
		var ex_len = Values.v_u(args[1]);
		var msg = Values.v_u(args[2]);
		var msg_len = Values.v_u(args[3]);
		var errstr = memory.extract(ex, ex_len);
		var msgstr = memory.extract(msg, msg_len);
		System.error(errstr, msgstr);  // TODO: print wasm-level stack trace.
		return NONE;
	}
	// ===================================================================

	private def i(v: int) -> HostResult {
		return HostResult.Value1(Value.I32(u32.view(v)));
	}
}
def MINUS_1 = HostResult.Value1(Value.I32(0xFFFFFFFFu));
def NONE = HostResult.Value0;
def MAX_FDS = 256;
def TRACE_BUILDER = TraceBuilder.new();
def WaveModule_installed = ModuleRegistry.add(WaveModule.new());
