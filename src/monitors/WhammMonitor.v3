// Copyright 2024 Wizard authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// This class implements a monitor that is implemented in WebAssembly, i.e. as a Whamm
// module. Such modules have exported functions whose names describe how to attach
// behavior to events.

// Event names supported:
//   before:opcode:<mnemonic>			Before matching opcodes execute


// TODO:
//  - thread error reporter through monitor
//  - report callbacks
//  - binding callbacks
//  - define frame accessor methods
//  - externref in Virgil
//   before:opcode:call(<func pattern>)		Upon static direct calls to func
//   before:call:<func pattern>
//   before:enter:<func pattern>			Entry of function(s)
//   before:return:<func pattern>
//   before:unwind:<func pattern>
//   before:throw
//   before:catch
//   before:parse					Upon module parse
//   before:instantiate
//   before:start
//   before:main
//   before:exit
class WhammMonitor(whamm: Module) extends Monitor {
	var onParseF: int = -1;
	var onInstantiateF: int = -1;
	var onStartF: int = -1;
	var onMainF: int = -1;
	var onInstanceF: int = -1;
	var onExitF: int = -1;
	var opcodePatterns: Array<List<(WhammOpcodePattern, FuncDecl)>>;
	var whammInstances: List<(Module, Instance, InstanceRef)>; // XXX: leaky

	new() {
		if (Trace.whamm) Trace.OUT.put1("parsing whamm monitor: %s", whamm.filename).ln();
		for (i < whamm.exports.length) {
			var e = whamm.exports[i], name = e.0, decl = e.1;
			if (!FuncDecl.?(decl)) continue;
			if (Trace.whamm) Trace.OUT.put2("  export %d: \"%s\" = ", i, name);
			var func = FuncDecl.!(decl);
			var p = WhammEventParser.new(whamm.filename, name);
			if (p.opcodePattern != null) addOpcodePattern(p.opcodePattern, func);
			else if (Trace.whamm) Trace.OUT.puts("no events matched").ln();
		}

	}
	def addOpcodePattern(b: WhammOpcodePattern, func: FuncDecl) {
		if (Trace.whamm) {
			Trace.OUT.put1("opcode %s", b.op.mnemonic);
			if (b.params.length > 0) {
				Trace.OUT.puts("(");
				for (i < b.params.length) {
					if (i > 0) Trace.OUT.csp();
					b.params[i].render(Trace.OUT);
				}
				Trace.OUT.puts(")").ln();
			}
		}
		if (opcodePatterns == null) opcodePatterns = Array.new(Opcodes.count);
		opcodePatterns[b.op.tag] = List.new((b, func), opcodePatterns[b.op.tag]);
	}
	def onParse(module: Module) {
		var wi = getWhammInstance(module).0;
		if (wi == null) return;
		if (onParseF >= 0) {
			def moduleRef = ModuleRef.new(module);
			Execute.call(wi.functions[onParseF], [Value.Ref(moduleRef)]); // TODO: report traps
		}
		if (opcodePatterns != null) {
			var it = BytecodeIterator.new();
			for (i < module.functions.length) {
				var func = module.functions[i];
				if (func.imp != null) continue; // imported
				for (bi = it.reset(func); bi.more(); bi.next()) {
					var bp = opcodePatterns[bi.current().tag];
					for (l = bp; l != null; l = l.tail) {
						if (Trace.whamm) Trace.OUT.put3("  match func %q+%d: %s", func.render(module.names, _), bi.pc, bi.current().mnemonic).ln();
						var wf = WasmFunction.!(wi.functions[bp.head.1.func_index]);
						var args = bindParams(it, bp.head.0.params);
						Instrumentation.insertLocalProbe(module, func.func_index, bi.pc, WhammProbe.new(wf, args));
					}
				}
			}
		}
	}
	def bindParams(it: BytecodeIterator, params: Array<WhammParam>) -> Array<WhammArg> {
		var result = Array<WhammArg>.new(params.length);
		for (i < result.length) {
			var arg: WhammArg;
			match (params[i]) {
				DynamicLoc => ;
				FrameAccessor => arg = WhammArg.FrameAccessor;
				Pc => arg = WhammArg.Val(Values.i_v(it.pc));
				Func => arg = WhammArg.Val(Values.i_v(it.func.func_index));
				Imm(orig, i) => ; // TODO
				Stack(orig, i) => ; // TODO
				Local(orig, i) => ;
				Call(target, params) => ;
			}
			result[i] = arg;
		}
		return result;
	}
	def onInstantiate(i: Instance) {
		if (onInstanceF < 0) return;
		var t = getWhammInstance(i.module), wi = t.0;
		if (wi == null) return;
		def instRef = InstanceRef.new(i);
		Execute.call(wi.functions[onInstanceF], [Value.Ref(instRef)]); // TODO: report traps
	}
	private def getWhammInstance(module: Module) -> (Instance, InstanceRef) {
		for (l = whammInstances; l != null; l = l.tail) {
			if (l.head.0 == module) return (l.head.1, l.head.2);
		}
		if (Trace.whamm) Trace.OUT.put2("Instantiating whamm module %s for %s", whamm.filename, module.filename).ln();
		var imports = Array<Exportable>.new(whamm.imports.length), err = ErrorGen.new(whamm.filename);
		var i = Instantiator.new(Extension.set.all, whamm, imports, err);
		i.processors.put(WizengModule.new());
		var instance = i.run();
		if (!err.ok()) return ErrorBuilder.new()
			.render(err.error_code, whamm.filename, err.section, err.error_index, err.error_pos, err.error_msg)
			.exit((null, null));
		var instRef = InstanceRef.new(instance);
		whammInstances = List.new((module, instance, instRef), whammInstances);
		return (instance, instRef);
	}
}

// A parser that extracts opcode patterns or other patterns from the name of an exported function.
class WhammEventParser(filename: string, expname: string) {
	def r = TextReader.new(filename, expname);
	var opcodePattern: WhammOpcodePattern;

	new() {
		if (r.optN("wasm:") >= 0) {
			if (r.optN("opcode:") >= 0) {
				var op = Opcodes.parseName(r.data[r.pos ...]);
				if (op != Opcode.INVALID) {
					r.advance(op.mnemonic.length);
					var params = NO_PARAMS;
					if (r.char == '(') params = WhammUtil.parseParams(r);
					opcodePattern = WhammOpcodePattern.new(op, params);
				}
			}
		}
	}
}

def NO_PARAMS: Array<WhammParam> = [];

// Pattern that describes which opcodes instrumentation may be applied to.
class WhammOpcodePattern(op: Opcode, params: Array<WhammParam>) {
}

// A probe that adapts a Wasm function to be called by the engine-internal probing mechanism.
class WhammProbe(func: WasmFunction, sig: Array<WhammArg>) extends Probe {
	private def args = if(sig.length == 0, Values.NONE, Array<Value>.new(sig.length));

	def fire(loc: DynamicLoc) -> Resumption {
		for (i < sig.length) {
			var v: Value;
			match (sig[i]) {
				FrameAccessor => v = Value.Ref(loc.frame.getFrameAccessor().getMetaRef());
				Val(val) => v = val;
				Operand(t, i) => v = loc.frame.getFrameAccessor().getOperand(i);
				Local(t, i) => v = loc.frame.getFrameAccessor().getLocal(i);
			}
			args[i] = v;
		}
		Execute.call(func, args); // XXX: runs on a new stack
		return Resumption.Continue;
	}
}
