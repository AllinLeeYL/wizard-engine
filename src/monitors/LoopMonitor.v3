// Copyright 2023 Wizard Authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Implements a simple monitor that counts the number of iterations of each loop.
def monitor_ = MonitorRegistry.add(
	"loops", "Tallies counts of loops.",
	LoopMonitor.new());
def inCodeModeOpt_ = monitor_.option_group.addAlias(
	monitor_.option_group.newFlag("code", setIncCodeMode, "emit report with code"),
	"c");

def showZeroCountsOpt_ = monitor_.option_group.addAlias(
	monitor_.option_group.newFlag("zero", setZeroCounts, "show zero counts"),
	"z");

var inCodeMode = false;
var showZeroCounts = false;
def setIncCodeMode(str: string) { inCodeMode = true; }
def setZeroCounts(str: string) { showZeroCounts = true; }
class LoopMonitor extends Monitor {
	def entries = Vector<LmEntry>.new();
	def it = BytecodeIterator.new();

	def onParse(m: Module) {
		ModuleInstrumenter.new(m).forEachFunc(instrument);
	}

	def onFinish(i: Instance, r: Result) {
		if (inCodeMode){
			reportCodeMode(out, entries);
		} else if (MonitorOptions.CSV.val) {
			reportCsv(out, entries);
		} else {
			report(out, entries);
		}
	}

	private def instrument(mm: ModuleInstrumenter, func: FuncDecl) {
		var loopDepth = 0;
		var controlStack = ArrayStack<(Opcode, LmEntry)>.new();

		for (bi = it.reset(func); bi.more(); bi.next()) {
			var op = bi.current();
			match (op) {
				BLOCK, IF => controlStack.push((op, null));
				LOOP => {
					var lmEntry = LmEntry.new(mm.module, func, loopDepth, bi.pc);
					Instrumentation.insertLocalProbe(mm.module, func.func_index, bi.pc, lmEntry);
					entries.put(lmEntry);
					controlStack.push((op, lmEntry));
					loopDepth++;
				}
				// TODO: try and exception bytecodes 
				END => {
					if ((bi.pc + 1) < func.cur_bytecode.length) {
						var topControl = controlStack.pop();
						if (topControl.0 == Opcode.LOOP) {
							loopDepth--;
							topControl.1.endPc = bi.pc;
						}
					}
				}
				_ => ;
			}
		}
	}
}
// An {LmEntry} is an entry that stores the execution count of each loop.
private class LmEntry(module: Module, func: FuncDecl, depth: int, pc: int) extends CountProbe {
	var endPc: int;
}

// Report summary
def reportCodeMode(out: TraceBuilder, entries: Vector<LmEntry>) {
	// XXX: In code mode, show a break from a BR, BR_IF, or BR_TABLE instead of END
	def tracer = InstrTracer.new();
	var bi = BytecodeIterator.new();
	var j = 0;
	while (j < entries.length) {
		var e = entries[j];
		bi = bi.reset(e.func);
		var indent = 0;
		out.beginColor(Color.FUNC)
			.put1("func %q:", e.func.render(e.module.names, _))
			.endColors()
			.ln();
		var controlStack = ArrayStack<Opcode>.new();
		var midPoints = Vector<int>.new();
		while (bi.more()) {
			var op = bi.current();
			match (op) {
				BLOCK, IF => controlStack.push(op);
				LOOP => {
					controlStack.push(op);
					e = entries[j++];
					midPoints.put((e.pc + e.endPc) / 2);
					indent = e.depth + 1;
					out.beginColor(Color.SOURCE)
						.mark()
						.put1("+%d", bi.pc)
						.rjustify_mark(5);

					for (k < indent - 1) out.puts("\t|");
					out.beginColor(Color.SOURCE).puts("\t-->\t");
					bi.traceOrig(out, e.module, tracer);
					out.endColors().ln();
					bi.next();
					continue;
				}
				END => {
					var topControl = controlStack.pop();
					if (topControl == Opcode.LOOP) {
						out.beginColor(Color.SOURCE)
							.mark()
							.put1("+%d", bi.pc)
							.rjustify_mark(5);
						for (k < indent - 1) out.puts("\t|");
						out.beginColor(Color.SOURCE).puts("\t<--\t");
						bi.traceOrig(out, e.module, tracer);
						out.endColors().ln();
						indent = if(e.depth > 0, indent - 1, 0);
						midPoints.resize(midPoints.length - 1);
						bi.next();
						continue;
					}
				}
				_ => ;
			}
			out.beginColor(Color.LINE)
				.mark()
				.put1("+%d", bi.pc)
				.rjustify_mark(5);
			for (k < indent) {
				if (midPoints.length > 0 && bi.pc == midPoints[k]) {
					var count = e.count;
					if (count == 0) out.beginColor(Color.UNREACHABLE);
					else out.beginColor(Color.COUNT);
					out.put1("\t(%d)", count).endColors();
				} else {
					out.beginColor(Color.SOURCE).puts("\t|").endColors();
				}
			}
			out.endColors().putc('\t');
			bi.traceOrig(out, e.module, tracer);
			out.ln();
			bi.next();
		}
	}
}
def report(out: TraceBuilder, entries: Vector<LmEntry>) {
	var prev: FuncDecl;
	for (j < entries.length) {
		var e = entries[j];
		if (e.count == 0 && !showZeroCounts) continue;
		if (e.func != prev) {
			prev = e.func;
			out.beginColor(Color.FUNC)
				.put1("func %q:", prev.render(e.module.names, _))
				.endColors().ln();
		}
		out.beginColor(Color.LINE)
			.mark()
			.put2(" +%d ... +%d ", e.pc, e.endPc)
			.rjustify_mark(14)
			.endColors();
		out.puts("loop: ")
			.beginColor(Color.COUNT)
			.putd_rjustified(5, e.count)
			.endColors()
			.ln();
	}

}
def reportCsv(out: TraceBuilder, entries: Vector<LmEntry>) {
	//calling function, starting pc, ending pc, number of times looped
	out.puts("loop,func,pc,end_pc,count\n");
	for (i < entries.length) {
		var e = entries[i];
		if (e.count == 0 && !showZeroCounts) continue;
		//add each line of data so the out buffer
		out.puts("loop,")
			.put1("%q", e.func.render(e.module.names, _))
			.putc(',')
			.putd(e.pc)
			.putc(',')
			.putd(e.endPc)
			.putc(',')
			.putd(e.count);
		if(i < entries.length - 2) 
			out.putc('\n');
	}
	out.ln();
}
