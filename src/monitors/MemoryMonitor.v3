// Copyright 2023 Wizard Authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Implements a simple monitor that prints all memory reads/writes.
def monitor_ = MonitorRegistry.add(
	"memory", "Traces accesses to Wasm memory.",
	MemoryMonitor.new());

class MemoryMonitor extends Monitor {
	var locations = true;
	var prevFunc: WasmFunction = null;

	def onParse(module: Module, err: ErrorGen) {
		var mm = ModuleInstrumenter.new(module);
		mm.beforeMemRead(beforeMemRead);
		mm.beforeMemWrite(beforeMemWrite);
		mm.beforeMemGrow(beforeMemGrow);
	}

	private def putMemIndexAndAddress(index: int, address: u64) {
		if (index > 0) out.putx(index).putc(',');
		out.putx(address);
	}
	private def putMemIndex(index: int) {
		if (index != 0) out.putx(index).putc(',');
	}
	private def putLocation(dynamicLoc: DynamicLoc) {
		if (dynamicLoc.func != prevFunc) {
			out.beginColor(Color.FUNC);
			dynamicLoc.func.render(out);
			out.ln();
			prevFunc = dynamicLoc.func;
		}
		out.beginColor(Color.LINE)
			.mark()
			.put1("+%x", dynamicLoc.pc)
			.rjustify_mark(5);
	}
	private def beforeMemRead(dynamicLoc: DynamicLoc, mem: Memory, address: u64, size: u64) -> Resumption {
		if (locations) putLocation(dynamicLoc);
		out.beginColor(Color.SOURCE)
			.puts("   read ");
		putMemIndexAndAddress(mem.decl.memory_index, address);
		out.putc(' ').endColors();
		for (i < u32.!(size)) { // TODO: memory64
			var b = mem.read_u8(u32.!(address), i); // TODO: memory64
			if (!b.ok()) {
				out.putc('!');
				break;
			}
			out.putx_8(b.result);
		}
		out.rjustify(' ', 8, 10)
		    .ln();
		return Resumption.Continue;
	}
	private def beforeMemWrite(dynamicLoc: DynamicLoc, mem: Memory, address: u64, size: u64) -> Resumption {
		if (locations) putLocation(dynamicLoc);
		out.beginColor(Color.TARGET)
			.puts("  write ");
		putMemIndexAndAddress(mem.decl.memory_index, address);
		out.putc(' ').endColors();
		var val = dynamicLoc.frame.getFrameAccessor().getOperand(0);
		match (size) {
			1 => match (val) {
				I32(v) => out.putx_8(byte.view(v));
				I64(v) => out.putx_8(byte.view(v));
				_ => ; // TODO: error
			}
			2 => match (val) {
				I32(v) => out.putx_16(u16.view(v));
				I64(v) => out.putx_16(u16.view(v));
				_ => ; // TODO: error
			}
			4 => match (val) {
				I32(v) => out.putx_32(u32.view(v));
				I64(v) => out.putx_32(u32.view(v));
				F32(v) => out.putx_32(u32.view(v));
				_ => ; // TODO: error
			}
			8 =>  match (val) {
				I64(v) => out.putx_64(u64.view(v));
				F64(v) => out.putx_64(u64.view(v));
				_ => ; // TODO: error
			}
			_ => ; // TODO: other sizes
		}
		out.rjustify(' ', 8, 10)
		    .ln();
		return Resumption.Continue;
	}
	private def beforeMemGrow(dynamicLoc: DynamicLoc, mem: Memory, pages: u32) -> Resumption {
		if (locations) putLocation(dynamicLoc);
		out.beginColor(Color.SUCCESS)
			.puts("   grow ")
			.endColors();
		putMemIndex(mem.decl.memory_index);
		out.putx_32(pages);
		out.putc(' ');
		// TODO: put result
		out.rjustify(' ', 9, 14)
		    .ln();
		return Resumption.Continue;
	}
}
