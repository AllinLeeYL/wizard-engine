// Copyright 2023 Wizard Authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Implements a simple monitor that prints all memory reads/writes.
class MemoryMonitor extends Monitor {
	var reads = Vector<u64>.new();
	var writes = Vector<u64>.new();

	def onParse(module: Module) {
		var mm = ModuleMonitor.new(module);
		mm.beforeMemRead(beforeMemRead);
		mm.beforeMemWrite(beforeMemWrite);
		mm.beforeMemGrow(beforeMemGrow);
	}

	private def putMemIndex(index: int) {
		if (index != 0) Trace.OUT.put1("[%d]", index);
	}
	private def beforeMemRead(dynamicLoc: DynamicLoc, mem: Memory, index: u64, size: u64) -> Resumption {
		Trace.OUT.puts("read  mem");
		putMemIndex(mem.decl.memory_index);
		Trace.OUT.put1(" @ 0x%x", index);
		Trace.OUT.put1("[size=%d]", size).outln();
		return Resumption.Continue;
	}
	private def beforeMemWrite(dynamicLoc: DynamicLoc, mem: Memory, index: u64, size: u64) -> Resumption {
		Trace.OUT.puts("write mem");
		putMemIndex(mem.decl.memory_index);
		Trace.OUT.put1(" @ 0x%x", index);
		Trace.OUT.put1("[size=%d]", size).outln();
		return Resumption.Continue;
	}
	private def beforeMemGrow(dynamicLoc: DynamicLoc, mem: Memory, pages: u32) -> Resumption {
		Trace.OUT.puts("grow  mem");
		putMemIndex(mem.decl.memory_index);
		Trace.OUT.put1(" +%d", pages).outln();
		return Resumption.Continue;
	}
}
