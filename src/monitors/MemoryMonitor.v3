// Copyright 2023 Wizard Authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Implements a simple monitor that prints all memory reads/writes.
class MemoryMonitor extends Monitor {
	var reads = Vector<u64>.new();
	var writes = Vector<u64>.new();

	def onParse(module: Module) {
		var mm = ModuleMonitor.new(module);
		mm.beforeMemRead(beforeMemRead);
		mm.beforeMemWrite(beforeMemWrite);
		mm.beforeMemGrow(beforeMemGrow);
	}
	
	private def u32_max: u32 = 4294967295u;
	private def putIndex(index: u64) {
		if (index <= u32_max) {
			Trace.OUT.putx_32(u32.view(index));
		} else {
			Trace.OUT.putx_64(index);
		}
	}
	private def beforeMemRead(dynamicLoc: DynamicLoc, mem: Memory, index: u64, size: u64) -> Resumption {
		Trace.OUT.put1("read  mem[%d] @ 0x", mem.decl.memory_index);
		putIndex(index);
		Trace.OUT.put1("[size=%d]", size).outln();
		return Resumption.Continue;
	}
	private def beforeMemWrite(dynamicLoc: DynamicLoc, mem: Memory, index: u64, size: u64) -> Resumption {
		Trace.OUT.put1("write mem[%d] @ 0x", mem.decl.memory_index);
		putIndex(index);
		Trace.OUT.put1("[size=%d]", size).outln();
		return Resumption.Continue;
	}
	private def beforeMemGrow(dynamicLoc: DynamicLoc, mem: Memory, pages: u32) -> Resumption {
		Trace.OUT.put2("grow  mem[%d] +%d", mem.decl.memory_index, pages).outln();
		return Resumption.Continue;
	}
}
