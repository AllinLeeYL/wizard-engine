// Copyright 2022 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

enum MonitorOption(option: string, help: string, init: void -> Monitor) {
	loops("loops", "{code}", LoopMonitor.new(_)),
	coverage("coverage", "{code[=instr]}", CoverageMonitor.new(_)),
	calls("calls", "{code[=<function pattern*>]}", CallsMonitor.new(_)),
	branches("branches", "{code}", BranchMonitor.new(_)),
	profile("profile", "{depth=<num>|calls=<function pattern*>}", ProfileMonitor.new(_)),
	hotness("hotness", "{code}", HotnessMonitor.new(_)),
	globals("globals", "", GlobalsMonitor.new(_)),
	memory("memory", "", MemoryMonitor.new(_)),
	debug("debug", "", DebugMonitor.new(_))
}

// Monitors used for testing
enum MonitorOptionTest(option: string, init: void -> Monitor) {
	postInstr("_postInstr", PostInstructionTestTracer.new(_)),
	hostCall("_hostCall", HostCallTestTracer.new(_)),
}

// Parses and updates monitor options based on arguments.
component MonitorOptions {
	private var monitors = Vector<Monitor>.new();

	new () {
		for (monitor in MonitorOption) {
			MonitorRegistry.register(monitor.option, monitor.init());
		}
		for (monitor in MonitorOptionTest) {
			MonitorRegistry.register(monitor.option, monitor.init());
		}
	}

	// Parse a command-line argument, configuring and updating monitors. Return {true}
	// if the argument was matched and parsed successfully.
	def parse(arg: string) -> bool {
		if (!Strings.startsWith(arg, "--monitors=")) return false;
		var s = 11;
		var inArg = false;
		for (i = s; i < arg.length; i++) {
			// Allow commas between monitor arguments
			if (arg[i] == '{') inArg = true;
			if (arg[i] == '}') inArg = false;
			if (!inArg && arg[i] == ',') {
				addMonitor(arg, s, i);
				s = i + 1;
			}
		}
		if (s < arg.length) addMonitor(arg, s, arg.length);
		return true;
	}
	private def addMonitor(arg: string, start: int, end: int) {
		var name = Arrays.range(arg, start, end);
		var args: string;

		for (i < name.length) {
			if (name[i] == '{' && name[name.length - 1] == '}') {
				args = Arrays.range(name, i + 1, name.length - 1);
				name = Arrays.range(name, 0, i);
			}
		}
		var m = MonitorRegistry.lookupName(name);
		if (m != null) {
			if (args != null) {
				var msg = m.configure(args);
				if (msg != null) Trace.OUT.put2("%s: %s", name, msg).outln();
			}
			monitors.put(m);
		}
	}
	def insertMonitor(m: Monitor) {
		monitors.put(m);
	}
	// Get the monitors, if any, configured by the above.
	def getMonitors() -> Vector<Monitor> {
		return monitors;
	}
	def printHelp() {
		var out = Trace.OUT, H = out.putsln;
		H("");
		H("MONITOR OPTIONS");
		H("");
		H("A number of monitors are available for instrumentation and dynamic analysis");
		H("of WASM programs. Multiple monitors may be specified together.");
		H("");
		H("   --monitors=<monitor>{<opts>}[,<monitor>{<opts>}]");
		H("");
		H("Each monitor comes with its own set of optional arguments and their modifiers.");
		H("");
		H("   <monitor>{<opt>=<modifier>[,<opt>=<modifier>]}");
		H("");

		for (m in MonitorOption) {
			out.puts("   ");
			out.puts(m.option);
			out.pad(' ', 16);
			out.puts(m.help);
			out.outln();
		}

		H("");
		H("Example usage:");
		H("");
		H("   --monitors=loops,coverage{code=instr}");
		out.outln();
	}
}
