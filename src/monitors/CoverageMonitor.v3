// Copyright 2022 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Implements a simple monitor that tracks code coverage.
class CoverageMonitor extends Monitor {
	def entries = Vector<CoverageMonitorProbe>.new();
	def isStartOfBlock = Vector<bool>.new();
	def executed = Vector<bool>.new();

	def onParse(m: Module) {
		for (i < m.functions.length) {
			var f = m.functions[i];
			if (f.imp != null) continue; // skip imported functions
			var offset = executed.length;
			var p = CoverageMonitorProbe.new(m, f, this, offset);
			entries.put(p);
			isStartOfBlock.putn(false, f.cur_bytecode.length);
			executed.putn(false, f.cur_bytecode.length);
			var addToNext = true;
			for (bi = BytecodeIterator.new().reset(f); bi.more(); bi.next()) {
				var op = bi.current();
				if (addToNext) {
					addToNext = false;
					isStartOfBlock[offset + bi.pc] = true;
					m.insertProbeAt(f.func_index, bi.pc, p);
				}
				match (op) {
					BLOCK, LOOP, ELSE, TRY, CATCH, CATCH_ALL => {
						isStartOfBlock[offset + bi.pc] = true;
						m.insertProbeAt(f.func_index, bi.pc, p);
					}
					BR, BR_IF, BR_TABLE, IF, THROW, RETHROW, END => addToNext = true;
					_ => ;
				}
			}
		}
	}
	def onExit(code: int) {
		onFinish(null);
	}
	def onFinish(i: Instance) {
		var prev: FuncDecl;
		var tracer = InstrTracer.new(), d = DataReader.new([]);
		var blockCovered = false;
		var it = BytecodeIterator.new();

		for (j < entries.length) {
			var e = entries[j];
			Trace.OUT.put1("func %q:", e.func.render(e.module.names, _)).outln();
			for (bi = it.reset(e.func); bi.more(); bi.next()) {
				bi.current();
				d.reset(e.func.orig_bytecode, bi.pc, e.func.orig_bytecode.length);
				if (isStartOfBlock[e.start + bi.pc]) {
					blockCovered = executed[e.start + bi.pc];
					Trace.OUT.puts("->");
				} else {
					Trace.OUT.puts("  ");
				}

				Trace.OUT.puts(if(blockCovered, " * ", "   "));
				tracer.putInstr(Trace.OUT, e.module, d);
				Trace.OUT.outln();
			}
		}
	}
}
class CoverageMonitorProbe(module: Module, func: FuncDecl, monitor: CoverageMonitor, start: int) extends Probe {
	def fire(dynamicLoc: DynamicLoc) -> Resumption {
		monitor.executed[dynamicLoc.pc + start] = true;
		module.removeProbeAt(dynamicLoc.func.decl.func_index, dynamicLoc.pc, this);
		return Resumption.Continue;
	}
}